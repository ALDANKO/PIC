// Удаляет пустые значения из массива (для которых НЕ ЗначениеЗаполнено = Истина)
//
// Параметры:
//	Массив
//
Процедура УдалитьПустыеЭлементы(Массив) Экспорт

	Если ТипЗнч(Массив) = Тип("Массив") Тогда
		
		Инд=0;
		
		Пока Инд<Массив.Количество() Цикл
			
			Если НЕ ЗначениеЗаполнено(Массив[Инд]) Тогда
				Массив.Удалить(Инд);
			Иначе
				Инд=Инд+1;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

// Возвращает таблицу значений с данными учетной политики
// Данные берутся на заданный момент времени 
//
// Параметры:
//	Момент времени, дата
//
// Возварщаемое значение: 
//	Таблица значений
//
Функция ПолучитьУчетнуюПолитику(МоментКон, Организация = Неопределено) Экспорт

	Если ТипЗнч(МоментКон) = Тип("Дата") Тогда
		ДатаСрезаОстатков = МоментКон;
	ИначеЕсли ТипЗнч(МоментКон) = Тип("МоментВремени") Тогда
		ДатаСрезаОстатков = МоментКон.Дата;
	ИначеЕсли ТипЗнч(МоментКон) = Тип("Граница") Тогда
		Если ТипЗнч(МоментКон.Значение) = Тип("Дата") Тогда
			ДатаСрезаОстатков = МоментКон.Значение;
		ИначеЕсли ТипЗнч(МоментКон.Значение) = Тип("МоментВремени") Тогда
			ДатаСрезаОстатков = МоментКон.Значение.Дата;
		Иначе
			ДатаСрезаОстатков = '00010101';
		КонецЕсли;
	Иначе
		ДатаСрезаОстатков = '00010101';
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МоментКон",   ДатаСрезаОстатков);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	УчетнаяПолитикаУправленческийУчетСрезПоследних.СпособОценкиМПЗ КАК СпособОценкиМПЗУпр,
	|	УчетнаяПолитикаУправленческийУчетСрезПоследних.ВестиПартионныйУчетПоСкладам КАК ВестиПартионныйУчетПоСкладамУпр,
	|	УчетнаяПолитикаУправленческийУчетСрезПоследних.СписыватьПартииПриПроведенииДокументов КАК СписыватьПартииПриПроведенииДокументовУпр,
	|	УчетнаяПолитикаУправленческийУчетСрезПоследних.СтратегияСписанияПартийТоваровПоСтатусам КАК СтратегияСписанияПоСтатусамУпр,
	|	УчетнаяПолитикаБухгалтерскийУчетСрезПоследних.СпособОценкиМПЗ КАК СпособОценкиМПЗБух,
	|	УчетнаяПолитикаНалоговыйУчетСрезПоследних.СхемаНалогообложения.НалогНаПрибыль КАК ЕстьНалогНаПрибыльНал,
	|	УчетнаяПолитикаНалоговыйУчетСрезПоследних.СхемаНалогообложения.НДС КАК ЕстьНДСНал
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБухгалтерскийУчет.СрезПоследних(&МоментКон, ) КАК УчетнаяПолитикаБухгалтерскийУчетСрезПоследних
	|		ПО УчетнаяПолитикаБухгалтерскийУчетСрезПоследних.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&МоментКон, ) КАК УчетнаяПолитикаНалоговыйУчетСрезПоследних
	|		ПО УчетнаяПолитикаНалоговыйУчетСрезПоследних.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитика.СрезПоследних(&МоментКон, ) КАК УчетнаяПолитикаУправленческийУчетСрезПоследних
	|		ПО (ИСТИНА)";
	
	Если Организация <> Неопределено Тогда Запрос.Текст = Запрос.Текст + "
	|ГДЕ
	|	Организации.Ссылка = &Организация";
	КонецЕсли;

	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьУчетнуюПолитику()

// Читает заданные в структуре реквизиты объекта, переданного ссылкой
// Параметры:
// Ссылка - Ссылка на объект (Документ, Справочник)
// СтруктураРеквизитов - Структура реквизитов объекта, которые надо получить
Процедура ПолучитьРеквизитыОбъекта(ДокументСсылка, СтруктураРеквизитов) Экспорт
	
	ТипДок = ТипЗнч(ДокументСсылка);
	МетаДок = Метаданные.НайтиПоТипу(ТипДок);
	
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипДок) тогда
		НазваниеТаблицы = "Справочник."+МетаДок.Имя;
	ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипДок) тогда
		НазваниеТаблицы = "Документ."+МетаДок.Имя;
	Иначе
		Возврат;
	КонецЕсли;
		
	Если МетаДок <> Неопределено Тогда
		
		СтрРеквизиты = "";
		Для Каждого Элемент Из СтруктураРеквизитов Цикл
			СтрРеквизиты = СтрРеквизиты +", "+Элемент.Ключ;
		КонецЦикла;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	" + Сред(СтрРеквизиты, 2) +"
		|ИЗ
		|	"+НазваниеТаблицы+"
		|
		|ГДЕ
		|	Ссылка = &Ссылка");
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Для Каждого Элемент Из СтруктураРеквизитов Цикл
				СтруктураРеквизитов.Вставить(Элемент.Ключ, Выборка[Элемент.Ключ]);
			КонецЦикла
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры//ПолучитьРеквизитыОбъекта()

// Получает параметр учетной политики для заданной организации по заданному учету
// Перед использованием функции должна быть заполнена таблица параметров учетной политики
//
// Параметры:
//	ИмяПараметра - Строка, имя запрашиваемого параметра учетной политики,
//	Учет         - Может принимать три значения: "Упр", "Бух", "Нал", "Меж". Задает учет
//	Организация,
//	СтруктураПараметров - структура, содержащая все общие параметры.
//
// Возвращаемое значение: 
//	Значение учетной политики
//
Функция УчетнаяПолитика(ИмяПараметра, Учет, Организация=Неопределено, СтруктураПараметров) Экспорт
	СтрокаУчетнойПолитики = Неопределено;
	Результат = Ложь;
	
	Если Учет = "Упр" Тогда
		Если СтруктураПараметров.УчетнаяПолитика.Количество()>0 Тогда
			СтрокаУчетнойПолитики = СтруктураПараметров.УчетнаяПолитика[0];
		КонецЕсли;
	Иначе
		СтрокаУчетнойПолитики = СтруктураПараметров.УчетнаяПолитика.Найти(Организация);
	КонецЕсли;
	
	Если СтрокаУчетнойПолитики<>Неопределено Тогда
		Результат = СтрокаУчетнойПолитики[ИмяПараметра+Учет];
		Если Результат = Null тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не задан параметр учетной политики """ +ИмяПараметра+ """ "+Учет+" учета" + ?(Учет<>"Упр", " для организации " + Организация, "")+"!");
			СтруктураПараметров.Вставить("Отказ", Истина);
			Результат = Ложь;
		КонецЕсли;
	Иначе
		ОбщегоНазначения.СообщитьОбОшибке("Не задан параметр учетной политики """ +ИмяПараметра+ """ "+Учет+" учета" + ?(Учет<>"Упр", " для организации " + Организация, "")+"!");
		СтруктураПараметров.Вставить("Отказ", Истина);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // УчетнаяПолитика()

// Объект Описание типов с одним типом Подразделения
//
// Параметры
//  Нет.
//
// Возвращаемое значение:
//   Объект Описание типов с типом Подразделения
//
Функция ПолучитьОписаниеТиповПодразделения()
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Подразделения"));
	
	Возврат Новый ОписаниеТипов(МассивТипов);
	
КонецФункции

// Объект Описание типов с типами - заказами
//
// Параметры
//  Нет.
//
// Возвращаемое значение:
//   Объект Описание типов с типом Подразделения
//
Функция ПолучитьОписаниеТиповЗаказ()

	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
	
	Возврат Новый ОписаниеТипов(МассивТипов);

КонецФункции // ПолучитьОписаниеТиповЗаказ()

// Возвращает строковое описание регистра-приемника для заданного кода операции
//
// Параметры:
//	КодОперации   - код операции по регистру партий
//	СтатьяЗатрат  - статья затрат, определяющая направление списания в производство.
//
// Возвращаемое значение:
//	Направление списания.
//
Функция ПолучитьНаправлениеСписанияПоКодуОперации(КодОперации, СтатьяЗатрат = Неопределено) Экспорт
			
	КодыОпераций = Перечисления.КодыОперацийПартииТоваров;
	
	Если КодОперации=КодыОпераций.Реализация
		ИЛИ КодОперации=КодыОпераций.РеализацияКомиссия 
		ИЛИ КодОперации=КодыОпераций.РеализацияРозница
		ИЛИ КодОперации=КодыОпераций.ВозвратОтПокупателя 
		ИЛИ КодОперации=КодыОпераций.ВозвратОтПокупателяТекущийМесяц Тогда
		
		НаправлениеСписания = "СебестоимостьПродаж";
	
	ИначеЕсли КодОперации=КодыОпераций.ПередачаВПереработку ИЛИ 
		КодОперации=КодыОпераций.ПередачаНаКомиссию Тогда
	
		НаправлениеСписания = "Переданные";
		
	ИначеЕсли КодОперации=КодыОпераций.ПеремещениеМеждуСкладами
		ИЛИ КодОперации=КодыОпераций.КорректировкаСерийИХарактеристик
		ИЛИ КодОперации=КодыОпераций.КорректировкаКачества
		ИЛИ КодОперации=КодыОпераций.РезервированиеПодЗаказ
		ИЛИ КодОперации=КодыОпераций.СнятиеРезерваПодЗаказ
		ИЛИ КодОперации=КодыОпераций.ПереоценкаПринятыхНаКомиссию 
		ИЛИ КодОперации=КодыОпераций.ВозвратОтКомиссионера 
		ИЛИ КодОперации=КодыОпераций.Комплектация 
		ИЛИ КодОперации=КодыОпераций.ВозвратОтПереработчика
		Тогда
		
		НаправлениеСписания = "НаСкладах";
		
	ИначеЕсли КодОперации=КодыОпераций.СписаниеНаЗатраты Тогда
		
		Если СтатьяЗатрат <> Неопределено Тогда
			
			Если СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.АдминистративныеРасходы Тогда
				НаправлениеСписания = "АдминистративныеРасходы";
				
			ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ОбщепроизводственныеРасходы Тогда
				НаправлениеСписания = "ОбщепроизводственныеРасходы";
				
			ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.РасходыНаСбыт Тогда
				НаправлениеСписания = "РасходыНаСбыт";
				
			ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ПрочиеОперационныеРасходы Тогда
				НаправлениеСписания = "ПрочиеОперационныеРасходы";
				
			ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.Прочие Тогда
				НаправлениеСписания = "ПрочиеРасходы";
				
			ИначеЕсли СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.ТранспортноЗаготовительныеРасходы Тогда
				НаправлениеСписания = "ТранспортноЗаготовительныеРасходы";  
			
			КонецЕсли;
		Иначе
			
			НаправлениеСписания = "";
		КонецЕсли;
		
	ИначеЕсли КодОперации=КодыОпераций.ПринятиеКУчетуОС Тогда
		
		НаправлениеСписания = "СтоимостьОСПриПринятииКУчету";
		
	ИначеЕсли КодОперации = КодыОпераций.ПередачаМалоценкиВЭксплуатацию Тогда
		
		НаправлениеСписания = "ПартииМалоценкиВЭксплуатации";
		
	ИначеЕсли КодОперации = КодыОпераций.ПередачаОборудованияВМонтаж Тогда
		
		НаправлениеСписания = "ВложенияВоВнеоборотныеАктивы";
		
	ИначеЕсли КодОперации=КодыОпераций.СписаниеНаВложенияВоВнеоборотныеАктивы Тогда
		
		НаправлениеСписания = "ВложенияВоВнеоборотныеАктивы";
		
	ИначеЕсли КодОперации = КодыОпераций.СписаниеПартийВПроизводствоОперативно Тогда
		
		НаправлениеСписания = "ПроизводственныеРасходы";
		
	ИначеЕсли КодОперации = КодыОпераций.СписаниеПартийПереданныхВПроизводство Тогда
	  	
	  	НаправлениеСписания = "ПроизводственныеРасходы";
		
	ИначеЕсли КодОперации = КодыОпераций.СписаниеНаБрак Тогда
		
		НаправлениеСписания = "БракВПроизводстве";
		
	Иначе
			
		НаправлениеСписания = "";
				
	КонецЕсли; 
	
	Возврат НаправлениеСписания;

КонецФункции // ПолучитьНаправлениеСписанияПоКодуОперации(КодОперации)

// Возвращает строковое описание регистра-источника для заданного кода операции
//
// Параметры:
//	КодОперации   - код операции по регистру партий
//
// Возвращаемое значение:
//	Источник списания.
//
Функция ПолучитьИсточникПоКодуОперации(КодОперации)
	
	КодыОпераций = Перечисления.КодыОперацийПартииТоваров;
	Если КодОперации = КодыОпераций.Реализация
		ИЛИ КодОперации = КодыОпераций.РеализацияРозница
		ИЛИ КодОперации = КодыОпераций.ПередачаНаКомиссию 
		ИЛИ КодОперации = КодыОпераций.ВозвратПоставщику
		ИЛИ КодОперации = КодыОпераций.Комплектация
		ИЛИ КодОперации = КодыОпераций.КорректировкаСерийИХарактеристик
		ИЛИ КодОперации = КодыОпераций.КорректировкаКачества
		ИЛИ КодОперации = КодыОпераций.ПередачаТарыКонтрагенту
		ИЛИ КодОперации = КодыОпераций.ПеремещениеМеждуСкладами
		ИЛИ КодОперации = КодыОпераций.ПереоценкаПринятыхНаКомиссию
		ИЛИ КодОперации = КодыОпераций.РезервированиеПодЗаказ
		ИЛИ КодОперации = КодыОпераций.СнятиеРезерваПодЗаказ
		ИЛИ КодОперации = КодыОпераций.СписаниеНаЗатраты
		ИЛИ КодОперации = КодыОпераций.СписаниеПоИнвентаризации
		ИЛИ КодОперации = КодыОпераций.СписаниеПоОрдеру

	Тогда
	
		Возврат "НаСкладах";
	КонецЕсли;
	
	Если КодОперации = КодыОпераций.ВозвратОтКомиссионера
		ИЛИ КодОперации = КодыОпераций.РеализацияКомиссия
		ИЛИ КодОперации = КодыОпераций.СписаниеПартийПереданныхВПроизводство
		ИЛИ КодОперации = КодыОпераций.ВозвратОтПереработчика
		
	Тогда
		Возврат "Переданные";
	КонецЕсли;
	
	Возврат "НаСкладах"; // По умолчанию
	
КонецФункции // ПолучитьИсточникПоКоду()

// Общая процедура для добавления записей в набор записей с сопутствующими действиями.
// Добавляет строку в таблицу движений, инкрементирует номер последней строки,
// устанавливает признак модифицированности.
//
Функция ДобавитьДвижениеВСтруктуруПараметров(ИмяРегистра, СтруктураПараметров) Экспорт

	Движение = СтруктураПараметров["ТаблицаДвижений" + ИмяРегистра].Добавить();
	
	// Увеличим счетчик движений на 1:
	СтруктураПараметров["ТекНомерСтроки"+ИмяРегистра] = СтруктураПараметров["ТекНомерСтроки"+ИмяРегистра]+1;
	
	// Устанавливаем флаг модификации
	СтруктураПараметров["ИзмененыДвижения"+ИмяРегистра] = Истина;
    	
	Возврат Движение;

КонецФункции

// Создает наборы записей по регистрам управленческого учета
//
// Параметры:
//	Нет.
//
Процедура СоздатьНаборыЗаписейУпр(СтруктураПараметров)
	
	СтруктураПараметров.Вставить("ДвиженияПартииТоваровНаСкладахУпр",    РегистрыНакопления.ПартииТоваровНаСкладах.СоздатьНаборЗаписей());
	СтруктураПараметров.Вставить("ДвиженияПартииТоваровПереданныеУпр",   РегистрыНакопления.ПартииТоваровПереданные.СоздатьНаборЗаписей());
	СтруктураПараметров.Вставить("ДвиженияЗатратыУпр",                   РегистрыНакопления.Затраты.СоздатьНаборЗаписей());
	СтруктураПараметров.Вставить("ДвиженияПродажиСебестоимость",       	 РегистрыНакопления.ПродажиСебестоимость.СоздатьНаборЗаписей());
	
	СтруктураПараметров.Вставить("ДвиженияСтоимостьОСУпр", 				 РегистрыНакопления.СтоимостьОС.СоздатьНаборЗаписей());
	
	// Учет реализации принятых на комиссию товаров для отчета комитенту
	СтруктураПараметров.Вставить("ДвиженияРеализованныеТовары",            РегистрыНакопления.РеализованныеТовары.СоздатьНаборЗаписей());
	
	СтруктураПараметров.Вставить("ДвиженияСтроительствоОбъектовОсновныхСредствУпр", РегистрыНакопления.СтроительствоОбъектовОсновныхСредств.СоздатьНаборЗаписей());
	
	СтруктураПараметров.Вставить("ДвиженияПартииМалоценкиВЭксплуатацииУпр",    	    РегистрыНакопления.ПартииМалоценкиВЭксплуатации.СоздатьНаборЗаписей());
	
	
	СтруктураПараметров.Вставить("ДвиженияНезавершенноеПроизводствоУпр",    РегистрыНакопления.НезавершенноеПроизводство.СоздатьНаборЗаписей());
	СтруктураПараметров.Вставить("ДвиженияБракВПроизводствеУпр",            РегистрыНакопления.БракВПроизводстве.СоздатьНаборЗаписей());
	
	СтруктураПараметров.Вставить("ДвиженияТоварыВНТТВПродажныхЦенахУпр",    РегистрыНакопления.ТоварыВНТТВПродажныхЦенах.СоздатьНаборЗаписей());
	
КонецПроцедуры // СоздатьНаборыЗаписейУпр()

// Создает наборы записей для заданных учетов.
//
Процедура СоздатьНаборыЗаписей(СтруктураПараметров, ТаблицаСписания = Неопределено) Экспорт

	СоздатьНаборыЗаписейУпр(СтруктураПараметров);
	
КонецПроцедуры

// Подготовка наборов записей по регистрам Упр учета к добавлению в них строк
//
// Параметры:
//	Нет.
//
Процедура ПодготовитьНаборыЗаписейУпр(СтруктураПараметров, ТаблицаСписания, Период, Регистратор, ЗамещатьПриЗаписи)
	
	ОтражатьВУправленческомУчете = ТаблицаСписания.Найти(Истина, "ОтражатьВУправленческомУчете")<>Неопределено;
	
	СтруктураПараметров.Вставить("ИзмененыДвиженияПартииТоваровНаСкладахУпр", Ложь);
	СтруктураПараметров.Вставить("ИзмененыДвиженияПартииТоваровПереданныеУпр", Ложь);
	СтруктураПараметров.Вставить("ИзмененыДвиженияЗатратыУпр", Ложь);
	СтруктураПараметров.Вставить("ИзмененыДвиженияПродажиСебестоимость", Ложь);
	СтруктураПараметров.Вставить("ИзмененыДвиженияРеализованныеТовары", Ложь);
	
	СтруктураПараметров.Вставить("ИзмененыДвиженияСтоимостьОСУпр", Ложь);
	
	СтруктураПараметров.Вставить("ИзмененыДвиженияСтроительствоОбъектовОсновныхСредствУпр", Ложь);
	
	СтруктураПараметров.Вставить("ИзмененыДвиженияПартииМалоценкиВЭксплуатацииУпр", Ложь);
	
	СтруктураПараметров.Вставить("ИзмененыДвиженияНезавершенноеПроизводствоУпр",    Ложь);
	СтруктураПараметров.Вставить("ИзмененыДвиженияБракВПроизводствеУпр",            Ложь);
	
	СтруктураПараметров.Вставить("ИзмененыДвиженияТоварыВНТТВПродажныхЦенахУпр", Ложь);
	
	СтруктураПараметров.ДвиженияПартииТоваровНаСкладахУпр.Очистить();
	СтруктураПараметров.ДвиженияПартииТоваровНаСкладахУпр.Отбор.Регистратор.Установить(Регистратор);
	СтруктураПараметров.ДвиженияПартииТоваровПереданныеУпр.Очистить();
	СтруктураПараметров.ДвиженияПартииТоваровПереданныеУпр.Отбор.Регистратор.Установить(Регистратор);
	СтруктураПараметров.ДвиженияЗатратыУпр.Очистить();
	СтруктураПараметров.ДвиженияЗатратыУпр.Отбор.Регистратор.Установить(Регистратор);
	СтруктураПараметров.ДвиженияПродажиСебестоимость.Очистить();
	СтруктураПараметров.ДвиженияПродажиСебестоимость.Отбор.Регистратор.Установить(Регистратор);
	
	СтруктураПараметров.ДвиженияСтоимостьОСУпр.Очистить();
	СтруктураПараметров.ДвиженияСтоимостьОСУпр.Отбор.Регистратор.Установить(Регистратор);		
	
	СтруктураПараметров.ДвиженияСтроительствоОбъектовОсновныхСредствУпр.Очистить();
	СтруктураПараметров.ДвиженияСтроительствоОбъектовОсновныхСредствУпр.Отбор.Регистратор.Установить(Регистратор);
	
	СтруктураПараметров.ДвиженияПартииМалоценкиВЭксплуатацииУпр.Очистить();
	СтруктураПараметров.ДвиженияПартииМалоценкиВЭксплуатацииУпр.Отбор.Регистратор.Установить(Регистратор);		
	
	СтруктураПараметров.ДвиженияНезавершенноеПроизводствоУпр.Очистить();
	СтруктураПараметров.ДвиженияНезавершенноеПроизводствоУпр.Отбор.Регистратор.Установить(Регистратор);		
	
	СтруктураПараметров.ДвиженияБракВПроизводствеУпр.Очистить();
	СтруктураПараметров.ДвиженияБракВПроизводствеУпр.Отбор.Регистратор.Установить(Регистратор);	
	
	СтруктураПараметров.ДвиженияТоварыВНТТВПродажныхЦенахУпр.Очистить();
	СтруктураПараметров.ДвиженияТоварыВНТТВПродажныхЦенахУпр.Отбор.Регистратор.Установить(Регистратор);
	
	// Таблицы движений:
	
	// Для партий на складах
	СтруктураПараметров.Вставить("ТаблицаДвиженийПартииТоваровНаСкладахУпр", СтруктураПараметров.ДвиженияПартииТоваровНаСкладахУпр.Выгрузить());
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Очистить();
	
	// Служебные колонки
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Колонки.Добавить("ДокументОприходованияДата", ОбщегоНазначения.ПолучитьОписаниеТиповДаты(ЧастиДаты.ДатаВремя));
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Колонки.Добавить("СтоимостьПоступление", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Колонки.Добавить("Подразделение", ПолучитьОписаниеТиповПодразделения());
	
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Колонки.Добавить("СуммаВыручки", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	
	// Валюты и курсы - для пересчета
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Колонки.Добавить("ВалютаДокумента");
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Колонки.Добавить("КурсДокумента");
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Колонки.Добавить("КратностьДокумента");
	
	// Количество поступления - для комплектации
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Колонки.Добавить("КоличествоПоступление", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 3));
	
	// Номер строки таблицы списания
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Колонки.Добавить("НомерСтрокиТаблицыСписания", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15));
	
	Если НЕ ЗамещатьПриЗаписи Тогда
		СтруктураПараметров.Вставить("ТекНомерСтрокиПартииТоваровНаСкладахУпр", ПолныеПрава.МаксНомерСтрокиВНабореЗаписей("ПартииТоваровНаСкладах", Регистратор));
	Иначе
		СтруктураПараметров.Вставить("ТекНомерСтрокиПартииТоваровНаСкладахУпр", 0);
	КонецЕсли;
	
	
	// Для партий товаров переданных
	СтруктураПараметров.Вставить("ТаблицаДвиженийПартииТоваровПереданныеУпр", СтруктураПараметров.ДвиженияПартииТоваровПереданныеУпр.Выгрузить());
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Очистить();
	
	// Служебные колонки
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Колонки.Добавить("Заказ", ПолучитьОписаниеТиповЗаказ());
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Колонки.Добавить("ДокументОприходованияДата", ОбщегоНазначения.ПолучитьОписаниеТиповДаты(ЧастиДаты.ДатаВремя));
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Колонки.Добавить("СтоимостьПоступление", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Колонки.Добавить("Подразделение", ПолучитьОписаниеТиповПодразделения());
	
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Колонки.Добавить("СуммаВыручки", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	// Валюты и курсы - для пересчета
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Колонки.Добавить("ВалютаДокумента");
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Колонки.Добавить("КурсДокумента");
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Колонки.Добавить("КратностьДокумента");
	
	// Количество поступления - для комплектации
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Колонки.Добавить("КоличествоПоступление", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 3));
	
	// Номер строки таблицы списания
	СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Колонки.Добавить("НомерСтрокиТаблицыСписания", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15));
	
	
	Если НЕ ЗамещатьПриЗаписи Тогда
		СтруктураПараметров.Вставить("ТекНомерСтрокиПартииТоваровПереданныеУпр", ПолныеПрава.МаксНомерСтрокиВНабореЗаписей("ПартииТоваровПереданные", Регистратор));
	Иначе
		СтруктураПараметров.Вставить("ТекНомерСтрокиПартииТоваровПереданныеУпр", 0);
	КонецЕсли;
	
	// Для товаров, списанных на затраты
	СтруктураПараметров.Вставить("ТаблицаДвиженийЗатратыУпр", СтруктураПараметров.ДвиженияЗатратыУпр.Выгрузить());
	СтруктураПараметров.ТаблицаДвиженийЗатратыУпр.Очистить();
	
	Если Не ЗамещатьПриЗаписи Тогда
		СтруктураПараметров.Вставить("ТекНомерСтрокиЗатратыУпр", ПолныеПрава.МаксНомерСтрокиВНабореЗаписей("Затраты", Регистратор));
	Иначе
		СтруктураПараметров.Вставить("ТекНомерСтрокиЗатратыУпр", 0);
	КонецЕсли;
	
	// Для МПЗ, списанных на себестоимость продаж
	СтруктураПараметров.Вставить("ТаблицаДвиженийПродажиСебестоимость", СтруктураПараметров.ДвиженияПродажиСебестоимость.Выгрузить());
	СтруктураПараметров.ТаблицаДвиженийПродажиСебестоимость.Очистить();
	
	Если НЕ ЗамещатьПриЗаписи Тогда
		СтруктураПараметров.Вставить("ТекНомерСтрокиПродажиСебестоимость", ПолныеПрава.МаксНомерСтрокиВНабореЗаписей("ПродажиСебестоимость", Регистратор));
	Иначе
		СтруктураПараметров.Вставить("ТекНомерСтрокиПродажиСебестоимость", 0);
	КонецЕсли;
	
	
	СтруктураПараметров.Вставить("ТаблицаДвиженийСтоимостьОСУпр", СтруктураПараметров.ДвиженияСтоимостьОСУпр.Выгрузить());
	
	Если НЕ ЗамещатьПриЗаписи Тогда 
		СтруктураПараметров.Вставить("ТекНомерСтрокиСтоимостьОСУпр", ПолныеПрава.МаксНомерСтрокиВНабореЗаписей("СтоимостьОС", Регистратор));
	Иначе
		СтруктураПараметров.Вставить("ТекНомерСтрокиСтоимостьОСУпр", 0);
	КонецЕсли;
	
	
	СтруктураПараметров.Вставить("ТаблицаДвиженийСтроительствоОбъектовОсновныхСредствУпр", СтруктураПараметров.ДвиженияСтроительствоОбъектовОсновныхСредствУпр.Выгрузить());
	
	Если НЕ ЗамещатьПриЗаписи Тогда 
		СтруктураПараметров.Вставить("ТекНомерСтрокиСтроительствоОбъектовОсновныхСредствУпр", ПолныеПрава.МаксНомерСтрокиВНабореЗаписей("СтроительствоОбъектовОсновныхСредств", Регистратор));
	Иначе
		СтруктураПараметров.Вставить("ТекНомерСтрокиСтроительствоОбъектовОсновныхСредствУпр", 0);
	КонецЕсли;
	
	
	СтруктураПараметров.Вставить("ТаблицаДвиженийПартииМалоценкиВЭксплуатацииУпр", СтруктураПараметров.ДвиженияПартииМалоценкиВЭксплуатацииУпр.Выгрузить());
	
	Если НЕ ЗамещатьПриЗаписи Тогда 
		СтруктураПараметров.Вставить("ТекНомерСтрокиПартииМалоценкиВЭксплуатацииУпр", ПолныеПрава.МаксНомерСтрокиВНабореЗаписей("ПартииМалоценкиВЭксплуатации", Регистратор));
	Иначе
		СтруктураПараметров.Вставить("ТекНомерСтрокиПартииМалоценкиВЭксплуатацииУпр", 0);
	КонецЕсли;
	
	
	СтруктураПараметров.Вставить("ТаблицаДвиженийТоварыВНТТВПродажныхЦенахУпр", СтруктураПараметров.ДвиженияТоварыВНТТВПродажныхЦенахУпр.Выгрузить());
	
	Если НЕ ЗамещатьПриЗаписи Тогда 
		СтруктураПараметров.Вставить("ТекНомерСтрокиТоварыВНТТВПродажныхЦенахУпр", ПолныеПрава.МаксНомерСтрокиВНабореЗаписей("ТоварыВНТТВПродажныхЦенах", Регистратор));
	Иначе
		СтруктураПараметров.Вставить("ТекНомерСтрокиТоварыВНТТВПродажныхЦенахУпр", 0);
	КонецЕсли;	
	
	
	// Для товаров, списанных на НЗП
	СтруктураПараметров.Вставить("ТаблицаДвиженийНезавершенноеПроизводствоУпр", СтруктураПараметров.ДвиженияНезавершенноеПроизводствоУпр.Выгрузить());
	СтруктураПараметров.ТаблицаДвиженийНезавершенноеПроизводствоУпр.Очистить();
	
	Если Не ЗамещатьПриЗаписи Тогда
		СтруктураПараметров.Вставить("ТекНомерСтрокиНезавершенноеПроизводствоУпр", ПолныеПрава.МаксНомерСтрокиВНабореЗаписей("НезавершенноеПроизводство", Регистратор));
	Иначе
		СтруктураПараметров.Вставить("ТекНомерСтрокиНезавершенноеПроизводствоУпр", ПолныеПрава.МаксНомерСтрокиВНабореЗаписей("НезавершенноеПроизводство", Регистратор,"РегистрНакопления",Истина));
	КонецЕсли;
	
	// Для товаров, списанных на брак
	СтруктураПараметров.Вставить("ТаблицаДвиженийБракВПроизводствеУпр", СтруктураПараметров.ДвиженияБракВПроизводствеУпр.Выгрузить());
	СтруктураПараметров.ТаблицаДвиженийБракВПроизводствеУпр.Очистить();
	
	Если Не ЗамещатьПриЗаписи Тогда
		СтруктураПараметров.Вставить("ТекНомерСтрокиБракВПроизводствеУпр", ПолныеПрава.МаксНомерСтрокиВНабореЗаписей("БракВПроизводстве", Регистратор));
	Иначе
		СтруктураПараметров.Вставить("ТекНомерСтрокиБракВПроизводствеУпр", 0);
	КонецЕсли;
	
	СтруктураПараметров.ДвиженияРеализованныеТовары.Очистить();
	СтруктураПараметров.ДвиженияРеализованныеТовары.Отбор.Регистратор.Установить(Регистратор);
	СтруктураПараметров.Вставить("ТаблицаДвиженийРеализованныеТовары", СтруктураПараметров.ДвиженияРеализованныеТовары.Выгрузить());
	СтруктураПараметров.ТаблицаДвиженийРеализованныеТовары.Очистить();
	СтруктураПараметров.Вставить("ТекНомерСтрокиРеализованныеТовары", 0);
	
КонецПроцедуры // ПодготовитьНаборыЗаписейУпр()

// Подготавливает параметры, необходимые для работы с наборами записей для заданных учетов.
//
Процедура ПодготовитьНаборыЗаписей(СтруктураПараметров, ТаблицаСписания, Период, Регистратор, ЗамещатьПриЗаписи = Истина, ИзмененыДвижения = Истина) Экспорт
	
	СтруктураПараметров.Вставить("Период",         Период);
	СтруктураПараметров.Вставить("Регистратор",    Регистратор);
	
	ПодготовитьНаборыЗаписейУпр(СтруктураПараметров, ТаблицаСписания, Период, Регистратор, ЗамещатьПриЗаписи);
	
	// Установим флаги модифицированности для наборов записей всех регистров, 
	// для которых документ является регистратором
	// и по которым у документа есть движения
	// для того, чтобы набор записей был перезаписан 
	// При вызове из обработки проведение по регистрам НДС модифицированность не устанавливаем
	
	ТаблицаДвиженийРегистратора = ПолныеПрава.ОпределитьНаличиеДвиженийПоРегистратору(Регистратор);
	Для каждого СтрокаДвижения из ТаблицаДвиженийРегистратора цикл
		ПозицияТочки = Найти(СтрокаДвижения.Имя,"."); 
		ВидРегистра = Лев(СтрокаДвижения.Имя, ПозицияТочки -1);
		Если ВидРегистра = "РегистрСведений" Тогда
			Продолжить;
		КонецЕсли;
		ИмяРегистра = СокрП(Сред(СтрокаДвижения.Имя, ПозицияТочки+1));
		
		Если ВидРегистра = "РегистрНакопления" Тогда
			Если НЕ СтруктураПараметров.ЕстьСтрокиОтражатьВУправленческомУчете тогда 
				Продолжить;
			КонецЕсли;
			ИмяРегистра = ИмяРегистра + "Упр";
		КонецЕсли;
		ИмяФлагаМодифицированности = "ИзмененыДвижения" + ИмяРегистра;
		Если СтруктураПараметров.Свойство(ИмяФлагаМодифицированности) тогда
			СтруктураПараметров[ИмяФлагаМодифицированности] = Истина;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры // ПодготовитьНаборыЗаписей

// Записывет набор записей в базу данных.
//
Процедура ЗаписатьДвижения(ДвиженияРегистра, ТаблицаДвиженийРегистра, Замещать, ДвижениеНДСПродукции = Ложь, ТолькоВключениеНДСВСтоимость = Неопределено)

	// При замещении нужно удалять записи, сформированные прошлым списанием партий (СписаниеПартий=Истина),
	// если у регистра есть реквизит СписаниеПартий, то нужно удалить строки с Истина
	ДМ = ДвиженияРегистра.Метаданные();
	ЕстьРеквизитСписаниеПартий = (ДМ.Реквизиты.Найти("СписаниеПартий") <> Неопределено);
	
	Если Замещать И ЕстьРеквизитСписаниеПартий Тогда
		
		Если ТаблицаДвиженийРегистра=Неопределено Тогда
			ТаблицаДвиженийРегистра = ДвиженияРегистра.Выгрузить();
		КонецЕсли;
		
		// Установим УБ (эксклюзивную) перед чтением набора записей
		Если глЗначениеПеременной("ИспользоватьУправляемыеБлокировки")  Тогда
			Если Метаданные.РегистрыНакопления.Содержит(ДМ) Тогда
				ТипТаблицы = "РегистрНакопления";
			Иначе
				// Запись движений по бухгалтерскому или налоговому ПУ
				ТипТаблицы = "РегистрБухгалтерии";
			КонецЕсли;
			СтруктураПараметровБлокировки = Новый Структура(
				"ТипТаблицы,ИмяТаблицы"
				,ТипТаблицы
				,ДМ.Имя + ".НаборЗаписей");
				
			СтруктураЗначенийБлокировки = Новый Структура("Регистратор", ДвиженияРегистра.Отбор.Регистратор.Значение);
			
			ОбщегоНазначения.УстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки,СтруктураЗначенийБлокировки,, Ложь, "");
		КонецЕсли;
		
		ДвиженияРегистра.Прочитать();
		
		Инд = ДвиженияРегистра.Количество();
		Пока Инд >0 Цикл
			Инд = Инд - 1;
			Если ДвиженияРегистра[Инд].СписаниеПартий Тогда
				ДвиженияРегистра.Удалить(Инд);
			КонецЕсли;
		КонецЦикла;
		
		
		// Заполним параметр "Списание партий"
		ТаблицаДвиженийРегистра.ЗаполнитьЗначения(Истина, "СписаниеПартий");
		ОбщегоНазначения.ДобавитьСтрокиВНаборЗаписей(ДвиженияРегистра, ТаблицаДвиженийРегистра);
		
		ДвиженияРегистра.Записать(Истина);
		
	Иначе
		
		Если ТаблицаДвиженийРегистра<>Неопределено Тогда
			
			// Заполним параметр "Списание партий"
			Если ЕстьРеквизитСписаниеПартий Тогда
				ТаблицаДвиженийРегистра.ЗаполнитьЗначения(Истина, "СписаниеПартий");
			КонецЕсли;
			
			ОбщегоНазначения.ДобавитьСтрокиВНаборЗаписей(ДвиженияРегистра, ТаблицаДвиженийРегистра);
		КонецЕсли;
		
		ДвиженияРегистра.Записать(Замещать);
		
	КонецЕсли;

КонецПроцедуры //ЗаписатьДвижения

// Запись движений по упр. учету
//
// Параметры:
//	Нет.
//
Процедура ЗаписатьДвиженияДокументаУпр(СтруктураПараметров, ТаблицаСписания, Замещать)
	
	ОтражатьВУправленческомУчете = СтруктураПараметров.ЕстьСтрокиОтражатьВУправленческомУчете;
	
	Если ОтражатьВУправленческомУчете Тогда
		
		// Управленческий учет
		Если СтруктураПараметров.ИзмененыДвиженияПартииТоваровНаСкладахУпр Тогда
			
			ЗаписатьДвижения(СтруктураПараметров.ДвиженияПартииТоваровНаСкладахУпр, 
			СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр, Замещать);
			
		КонецЕсли;
		
		Если СтруктураПараметров.ИзмененыДвиженияПартииТоваровПереданныеУпр Тогда
			
			ЗаписатьДвижения(СтруктураПараметров.ДвиженияПартииТоваровПереданныеУпр, 
			СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр, Замещать);
			
		КонецЕсли;
		
		Если СтруктураПараметров.ИзмененыДвиженияПродажиСебестоимость Тогда
			
			ЗаписатьДвижения(СтруктураПараметров.ДвиженияПродажиСебестоимость, 
			СтруктураПараметров.ТаблицаДвиженийПродажиСебестоимость, Замещать);
			
		КонецЕсли;
		
		Если СтруктураПараметров.ИзмененыДвиженияЗатратыУпр Тогда
			
			ЗаписатьДвижения(СтруктураПараметров.ДвиженияЗатратыУпр, 
			СтруктураПараметров.ТаблицаДвиженийЗатратыУпр, Замещать);
			
		КонецЕсли;
		
		// Для отчета комитенту
		Если СтруктураПараметров.ИзмененыДвиженияРеализованныеТовары Тогда
			
			ЗаписатьДвижения(СтруктураПараметров.ДвиженияРеализованныеТовары, 
			СтруктураПараметров.ТаблицаДвиженийРеализованныеТовары, Замещать);
			
		КонецЕсли;
		
		Если СтруктураПараметров.ИзмененыДвиженияСтоимостьОСУпр Тогда
			
			ЗаписатьДвижения(СтруктураПараметров.ДвиженияСтоимостьОСУпр, 
			СтруктураПараметров.ТаблицаДвиженийСтоимостьОСУпр, Замещать);
			
		КонецЕсли;
		
		Если СтруктураПараметров.ИзмененыДвиженияСтроительствоОбъектовОсновныхСредствУпр Тогда
			
			ЗаписатьДвижения(СтруктураПараметров.ДвиженияСтроительствоОбъектовОсновныхСредствУпр, 
			СтруктураПараметров.ТаблицаДвиженийСтроительствоОбъектовОсновныхСредствУпр, Замещать);
			
		КонецЕсли;
		
		Если СтруктураПараметров.ИзмененыДвиженияПартииМалоценкиВЭксплуатацииУпр Тогда
			
			ЗаписатьДвижения(СтруктураПараметров.ДвиженияПартииМалоценкиВЭксплуатацииУпр, 
			СтруктураПараметров.ТаблицаДвиженийПартииМалоценкиВЭксплуатацииУпр, Замещать);
			
		КонецЕсли;
		
		Если СтруктураПараметров.ИзмененыДвиженияТоварыВНТТВПродажныхЦенахУпр Тогда
			
			ЗаписатьДвижения(СтруктураПараметров.ДвиженияТоварыВНТТВПродажныхЦенахУпр, 
			СтруктураПараметров.ТаблицаДвиженийТоварыВНТТВПродажныхЦенахУпр, Замещать);
			
		КонецЕсли;
		
		Если СтруктураПараметров.ИзмененыДвиженияНезавершенноеПроизводствоУпр Тогда
			
			ЗаписатьДвижения(СтруктураПараметров.ДвиженияНезавершенноеПроизводствоУпр, 
			СтруктураПараметров.ТаблицаДвиженийНезавершенноеПроизводствоУпр, Замещать);
			
		КонецЕсли;
		
		Если СтруктураПараметров.ИзмененыДвиженияБракВПроизводствеУпр Тогда
			
			ЗаписатьДвижения(СтруктураПараметров.ДвиженияБракВПроизводствеУпр, 
			СтруктураПараметров.ТаблицаДвиженийБракВПроизводствеУпр, Замещать);
			
		КонецЕсли;
		
		
		
	КонецЕсли
	
КонецПроцедуры // ЗаписатьДвиженияДокументаУпр()

// Процедура-вход для записи движений документа в базу данных
//
Процедура ЗаписатьДвиженияДокумента(СтруктураПараметров, ТаблицаСписания, Замещать = Истина) Экспорт

	// Управленческий учет
	ЗаписатьДвиженияДокументаУпр(СтруктураПараметров, ТаблицаСписания, Замещать);
	
КонецПроцедуры

// Приведение таблицы списания к требуемому виду
//
Процедура ПодготовитьТаблицуСписания(СтруктураПараметров, ТаблицаСписания)
	
	// В таблице должна быть колонка "ВестиПартионныйУчетПоСериям", определяющая, нужно ли отбирать партии по сериям
	Если ТаблицаСписания.Колонки.Найти("ВестиПартионныйУчетПоСериям") = Неопределено Тогда
		
		// Получим данные о признаках ведения учета по сериям, установленным для номенклатуры
		МассивНоменклатуры = ТаблицаСписания.ВыгрузитьКолонку("Номенклатура");
		МассивНоменклатуры = УчетНДС.УдалитьПовторяющиесяЭлементыМассива(МассивНоменклатуры);
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Номенклатура.Ссылка,
		|	Номенклатура.ВестиПартионныйУчетПоСериям
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка В(&МассивНоменклатуры)");
		
		Запрос.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
		Выборка = Запрос.Выполнить().Выбрать();
		
		// Данные из запроса поместим в соответствие
		Соотв = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			Соотв.Вставить(Выборка.Ссылка, Выборка.ВестиПартионныйУчетПоСериям);
		КонецЦикла;
		
		// Заполним колонку "ВестиПартионныйУчетПоСериям" значениями из соответствия
		ТаблицаСписания.Колонки.Добавить("ВестиПартионныйУчетПоСериям", Новый ОписаниеТипов("Булево"));
		Для Каждого Строка Из ТаблицаСписания Цикл
			Строка.ВестиПартионныйУчетПоСериям = Соотв[Строка.Номенклатура];
		КонецЦикла;
	КонецЕсли;
	
	
	// Приведем к виду со всеми колонками
	Если ТаблицаСписания.Колонки.Найти("ОтражатьВУправленческомУчете") = Неопределено Тогда
		ТаблицаСписания.Колонки.Добавить("ОтражатьВУправленческомУчете", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	Если ТаблицаСписания.Колонки.Найти("ИсточникПоКодуОперации") <> Неопределено Тогда
		// Таблица строк получена запросом к регистру СписанныеТовары		
		СтруктураПараметров.Вставить("ЕстьНаСкладах", ТаблицаСписания.Найти("НаСкладах", "ИсточникПоКодуОперации") <> Неопределено);
		СтруктураПараметров.Вставить("ЕстьПереданные", ТаблицаСписания.Найти("Переданные", "ИсточникПоКодуОперации") <> Неопределено);
	Иначе	
		
		// Таблица строк получена из документа		
		ТаблицаСписания.Колонки.Добавить("ИсточникПоКодуОперации");
		ЕстьНаСкладах = Ложь;
		ЕстьПереданные = Ложь;		
		Для Каждого СтрокаСписания Из ТаблицаСписания Цикл
			СтрокаСписания.ИсточникПоКодуОперации = ПолучитьИсточникПоКодуОперации(СтрокаСписания.КодОперацииПартииТоваров);
			Если Не ЕстьНаСкладах И СтрокаСписания.ИсточникПоКодуОперации = "НаСкладах" Тогда
				ЕстьНаСкладах=Истина;
			ИначеЕсли НЕ ЕстьПереданные И СтрокаСписания.ИсточникПоКодуОперации = "Переданные" Тогда
				ЕстьПереданные=Истина;
			КонецЕсли;			
			
			СтрокаСписания.ОтражатьВУправленческомУчете = Истина;
			
		КонецЦикла;
		СтруктураПараметров.Вставить("ЕстьНаСкладах", ЕстьНаСкладах);
		СтруктураПараметров.Вставить("ЕстьПереданные", ЕстьПереданные);
		
	КонецЕсли;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// СПИСАНИЯ (КРЕДИТ)

// Упр учет

// Структура отбора партий по упр учету заполняется нужными значениями
//
// Параметры:
//	Нет.
//
Процедура ЗаполнитьСтруктуруОтбораУпр(СтруктураОтбора, СтрокаДокумента, РегистрУчета, СписыватьПустыеСерии)
	
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		
		СтруктураОтбора.Вставить("Номенклатура",               СтрокаДокумента.Номенклатура);
		СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", СтрокаДокумента.ХарактеристикаНоменклатуры);
		
		Если РегистрУчета="НаСкладах" Тогда
			
			// Дополнительно отбираются пустые серии на случай, если партионный учет по сериям был включен после проведения
			// документов оприходования. В этом случае серия не прописалась в регистры "ПартииТоваров"
			// Контроль остатков серии произведен по регистру "ТоварыНаСкладах"
			Если СписыватьПустыеСерии тогда
				СтруктураОтбора.Вставить("СерияНоменклатуры", Справочники.СерииНоменклатуры.ПустаяСсылка());
			Иначе
				// Серии не отбираются при резервировании т.к. в заказах нет серий
				Если СтрокаДокумента.КодОперацииПартииТоваров <> Перечисления.КодыОперацийПартииТоваров.РезервированиеПодЗаказ
				   И СтрокаДокумента.КодОперацииПартииТоваров <> Перечисления.КодыОперацийПартииТоваров.СнятиеРезерваПодЗаказ Тогда
					
					СтруктураОтбора.Вставить("СерияНоменклатуры", СтрокаДокумента.СерияНоменклатуры);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСтруктуруОтбораУпр()
// Упр учет

// Возвращает имя соответствующего регистра для строки по упр учету
//
// Параметры:
//	Нет.
//
Процедура ПолучитьПолучитьИмяРегистраУпр(ИмяРегистра, ВидРегистра, РегистрУчета, СтрокаДокумента)
	
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		Если ВидРегистра="ПартииТоваров" Тогда
			ИмяРегистра = ВидРегистра + РегистрУчета + "Упр";
		Иначе
			ИмяРегистра = ВидРегистра + "Упр";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПолучитьПолучитьИмяРегистраУпр(ИмяРегистра, "ПартииТоваров", РегистрУчета, СтрокаДокумента)

// Заполняет поля записи списания по упр учету
//
// Параметры:
//	Нет.
//
Процедура ЗаполнитьПоляЗаписиСписанияУпр(Движение, СтрокаПартии, СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоэффСписания, КоэффПоступления)
	
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		
		Движение.ХарактеристикаНоменклатуры = СтрокаПартии.ХарактеристикаНоменклатуры;
		
		Если РегистрУчета="НаСкладах" Тогда
			Движение.СерияНоменклатуры 		= СтрокаПартии.СерияНоменклатуры;
			Движение.Склад 			        = СтрокаПартии.Склад;
			Движение.Заказ  				= СтрокаПартии.Заказ;
			Движение.Качество  				= СтрокаПартии.Качество;
			
			Движение.НомерСтрокиСписанныхТоваров = СтруктураПараметров.НомерСтрокиСписанныхТоваров; 
			
		Иначе // Если РегистрУчета="Переданные" Тогда
			
			Движение.ДоговорКонтрагента	    = СтрокаПартии.ДоговорКонтрагента;
			Движение.ДокументПередачи		= СтрокаПартии.ДокументПередачи;
			
		КонецЕсли;
		
		Движение.ДокументОприходования	= СтрокаПартии.ДокументОприходования;
		
		Движение.СтатусПартии		    = СтрокаПартии.СтатусПартии;
		
		// Реквизиты
		Движение.Подразделение	        = СтрокаДокумента.Подразделение;
		
		Если РегистрУчета="Переданные" Тогда
			Движение.СтатусПередачи		= СтрокаПартии.СтатусПередачи;
		КонецЕсли;
		
		// Вспомогательные поля, не являющиеся измерениями и реквизитами, но используемые для...
		
		// Номер строки, по которой списана партия
		// Используется при возврате товаров от покупателя, ордерной схеме поступления,
		// механизмом корректировки стоимости списания 
		Движение.НомерСтрокиСписанныхТоваров = СтруктураПараметров.НомерСтрокиСписанныхТоваров;
		
		// Сумма выручки в валюте документа - для отчета комитенту
		Движение.СуммаВыручки = Окр(СтрокаДокумента.СуммаЗадолженности*КоэффПоступления, 2);
		// Валюты и курсы - для пересчета
		Движение.ВалютаДокумента = СтрокаДокумента.ВалютаДокумента;
		Движение.КурсДокумента = СтрокаДокумента.КурсДокумента;
		Движение.КратностьДокумента = СтрокаДокумента.КратностьДокумента;
		
		СтрокаДокумента.СуммаЗадолженности = СтрокаДокумента.СуммаЗадолженности - Движение.СуммаВыручки;
		
		// Количество поступления - для комплектации
		Движение.КоличествоПоступление    = Окр(СтрокаДокумента.КоличествоПоступление*КоэффПоступления, 3);
		СтрокаДокумента.КоличествоПоступление = СтрокаДокумента.КоличествоПоступление - Движение.КоличествоПоступление;
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьПоляЗаписиСписанияУпр()

// Возвращает текст содержащий вид табличной части
//
Функция ТекстСтрокиДокумента(СтрокаДокумента)

	Если ЗначениеЗаполнено(СтрокаДокумента.ВидТабличнойЧасти)Тогда
		
		ТекстСтрокиДокумента = ", табличная часть """ + Строка(СтрокаДокумента.ВидТабличнойЧасти)+"""";
		
	Иначе
		ТекстСтрокиДокумента =  " ";
		
	КонецЕсли;
	
	Возврат ТекстСтрокиДокумента;

КонецФункции // ТекстСтрокиДокумента()

// Сообщает о нехватке партии в управленческом учете
//
// Параметры:
//	СтрокаДокумента.
//
Процедура СообщитьОНехваткеПартииУпр(СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоличествоОсталосьПогасить)
	
	//Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
	//***180505
	Если СтрокаДокумента.ОтражатьВУправленческомУчете 
		И ТипЗНЧ(СтрокаДокумента.Регистратор)<>Тип("ДокументСсылка.РеализацияТоваровУслуг")  Тогда
	
		
		СтрокаСообщения = "Управленческий учет. ";
		
		Если РегистрУчета = "НаСкладах" Тогда
			СтрокаСообщения = СтрокаСообщения +
			"Документ " + СтрокаДокумента.Регистратор + ", строка :" + СтрокаДокумента.НомерСтроки + Символы.ПС
			+ "Не списано по партиям " + КоличествоОсталосьПогасить + " " + СтрокаДокумента.Номенклатура.ЕдиницаХраненияОстатков 
			+ " товара " + СтрокаДокумента.Номенклатура
			+ ?(СтрокаДокумента.Номенклатура.ВестиУчетПоХарактеристикам, ", х-ка: " + СтрокаДокумента.ХарактеристикаНоменклатуры, "")
			+ ?(СтрокаДокумента.Номенклатура.ВестиУчетПоСериям, ", серия: " + СтрокаДокумента.СерияНоменклатуры, "")
			+ ?(УчетнаяПолитика("ВестиПартионныйУчетПоСкладам", "Упр", , СтруктураПараметров), ", со склада: " + СтрокаДокумента.Склад, "");
			
		ИначеЕсли РегистрУчета = "Переданные" Тогда
			СтрокаСообщения = СтрокаСообщения +
			"Документ " + СтрокаДокумента.Регистратор + ", строка :" + СтрокаДокумента.НомерСтроки + Символы.ПС
			+ "Не списано по партиям " + КоличествоОсталосьПогасить + " " + СтрокаДокумента.Номенклатура.ЕдиницаХраненияОстатков 
			+ " товара " + СтрокаДокумента.Номенклатура
			+ ?(СтрокаДокумента.Номенклатура.ВестиУчетПоХарактеристикам, ", х-ка: " + СтрокаДокумента.ХарактеристикаНоменклатуры, "")
			+ ", по договору " + СтрокаДокумента.ДоговорКонтрагента;
		КонецЕсли;
		
		СтруктураПараметров.Вставить("Отказ", Истина);
		
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаСообщения);
				
	КонецЕсли;
	
КонецПроцедуры // СообщитьОНехваткеПартииУпр()

// Сообщает о нехватке партии в управленческом учете
//
// Параметры:
//	СтрокаДокумента.
//
Процедура СообщитьОНеполномСписанииУказаннойПартииУпр(СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоличествоОсталосьПогасить)
	
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		
		СтрокаСообщения = "Управленческий учет. ";

		СтрокаСообщения = СтрокаСообщения +
		"Документ " + СтрокаДокумента.Регистратор + ТекстСтрокиДокумента(СтрокаДокумента)+ Символы.ПС
		+ "По указанному документу поступления товаров " + СтрокаДокумента.ДокументПартии + " партия не была списанна полностью" + Символы.ПС
		+ " товар " + СтрокаДокумента.Номенклатура
		+ ?(СтрокаДокумента.Номенклатура.ВестиУчетПоХарактеристикам, ", х-ка: " + СтрокаДокумента.ХарактеристикаНоменклатуры, "")
		+ ?(СтрокаДокумента.Номенклатура.ВестиУчетПоСериям, ", серия: " + СтрокаДокумента.СерияНоменклатуры, "");
			
		Сообщить(СтрокаСообщения);
		
	КонецЕсли;
	
КонецПроцедуры // СообщитьОНехваткеПартииУпр()

// Общие

// Возвращает имя соответствующего регистра для строки
//
// Параметры:
//	Нет.
//
Функция ПолучитьИмяРегистра(ВидРегистра, РегистрУчета, СтрокаДокумента)
	
	ИмяРегистра ="";
	ПолучитьПолучитьИмяРегистраУпр(ИмяРегистра, ВидРегистра, РегистрУчета, СтрокаДокумента);
	
	Возврат ИмяРегистра;
	
КонецФункции // ПолучитьИмяРегистра("ПартииТоваров", РегистрУчета, СтрокаДокумента)

// Отбирает в партиях строки соответствующие значениям обрабатываемой строки
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Массив строк
//
Функция ОтобратьСтрокиПартий(ТаблицаПартий, СтрокаДокумента, РегистрУчета) Экспорт
	
	// Структура отбор строк партий
	СтруктураОтбора = Новый Структура;
	
	ЗаполнитьСтруктуруОтбораУпр(СтруктураОтбора, СтрокаДокумента, РегистрУчета, Ложь);
	
	МассивСтрок = ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
	
	// Для регистра "ПартииТоваровНаСкладах"...
	// Если серия заполнена - попытаемся отобрать дополнительно пустые серии
	// Если серия в документе не заполнена - пустые серии уже отобраны
	// Для переданных товаров партионный учет по сериям не ведется.
	Если РегистрУчета ="НаСкладах" тогда
		Если ЗначениеЗаполнено(СтрокаДокумента.СерияНоменклатуры) тогда
			
			// Серии не отбираются при резервировании т.к. в заказах нет серий
			Если СтрокаДокумента.КодОперацииПартииТоваров <> Перечисления.КодыОперацийПартииТоваров.РезервированиеПодЗаказ
			   И СтрокаДокумента.КодОперацииПартииТоваров <> Перечисления.КодыОперацийПартииТоваров.СнятиеРезерваПодЗаказ Тогда
			
				ЗаполнитьСтруктуруОтбораУпр(СтруктураОтбора, СтрокаДокумента, РегистрУчета, Истина);
				
				МассивСтрокСПустымиСериями = ТаблицаПартий.НайтиСтроки(СтруктураОтбора);
				
				Для каждого Элемент из МассивСтрокСПустымиСериями Цикл
					МассивСтрок.Добавить(Элемент);
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат МассивСтрок;
	
КонецФункции // ОтобратьСтрокиПартий()

// Заполняет поля записи списания
//
// Параметры:
//	Нет.
//
Процедура ЗаполнитьПоляЗаписиСписания(Движение, СтрокаПартии, СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоэффСписания, КоэффПоступления)
	
	ЗаполнитьПоляЗаписиСписанияУпр(Движение, СтрокаПартии, СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоэффСписания, КоэффПоступления);
	
КонецПроцедуры // ЗаполнитьПоляЗаписиСписания()

// Списание со склада по строке
//
// Параметры
//  СтруктураПараметров  – Структура, содержащая общие параметры
//
// Возвращаемое значение:
//   ТаблицаЗначений   – таблица списанных партий
//
Процедура СписаниеПартий(СтрокаДокумента, СтруктураПараметров, РегистрУчета)
	
	КодыОпераций = Перечисления.КодыОперацийПартииТоваров;
	
	// Партии для данного учета
	ИмяРегистра = ПолучитьИмяРегистра("ПартииТоваров", РегистрУчета, СтрокаДокумента);
	
	// Если нет регистра, по которому списываем, тогда пропускаем
	Если ИмяРегистра="" Тогда
		Возврат;
	КонецЕсли;
	
	ДеревоПартий = СтруктураПараметров["Дерево"+ИмяРегистра];
	
	СтруктураИзмерений = СтруктураПараметров[ИмяРегистра + "СтруктураИзмерений"];
	ТаблицаОстатковПартий = СтруктураПараметров[ИмяРегистра + "ТаблицаОстатков"];
	
	СтрокаДереваПартий = ДеревоПартий.Строки.Найти(СтрокаДокумента.НомерСтрокиДокумента, "НомерСтрокиДокумента");
	
	// Подлежащее погашению при списании количество
	КоличествоОсталосьПогасить = СтрокаДокумента.Количество;
	
	// В строке может быть указана стоимость поступления, если затем товар будет оприходован с другой стоимостью
	СтоимостьПоступлениеОсталосьПогасить = 0;
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		СтоимостьПоступлениеОсталосьПогасить = СтрокаДокумента.СтоимостьПоступление;
	КонецЕсли;
	
	КоличествоПоступление = СтрокаДокумента.КоличествоПоступление;
	
	КоличествоСписанноеПоДоументуПартии = 0;
	
	Если СтрокаДереваПартий <> Неопределено Тогда
		
		Для Каждого СтрокаПартииРаспределения ИЗ СтрокаДереваПартий.Строки Цикл
			
			Если КоличествоОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			СтрокаПартии = ПолучитьСтрокуОстатковПартий(СтрокаПартииРаспределения, СтруктураИзмерений, ТаблицаОстатковПартий);
			
			// Количество по строке больше 0
			Если НЕ СтрокаПартии.Количество > 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			Если СтрокаПартии.Количество >= КоличествоОсталосьПогасить Тогда
				КоэффСписания = КоличествоОсталосьПогасить/СтрокаПартии.Количество;
			Иначе
				КоэффСписания = 1;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаДокумента.ДокументПартии) И (СтрокаПартии.ДокументОприходования = СтрокаДокумента.ДокументПартии) Тогда
				КоличествоСписанноеПоДоументуПартии = КоличествоСписанноеПоДоументуПартии + Окр(СтрокаПартии.Количество * КоэффСписания,3,1);
			КонецЕсли;			
			
			// Добавим новую строку
			Движение = ДобавитьДвижениеВСтруктуруПараметров(ИмяРегистра, СтруктураПараметров);
			
			// Свойства
			Движение.Период 	 = СтрокаДокумента.Период;
			Движение.Регистратор = СтрокаДокумента.Регистратор;
			Движение.Активность  = Истина;
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			
			// Измерения
			Движение.Номенклатура = СтрокаПартии.Номенклатура;
			
			// Ресурсы 
			Движение.Количество	= Окр(СтрокаПартии.Количество * КоэффСписания,3,1);
			Движение.Стоимость	= Окр(СтрокаПартии.Стоимость  * КоэффСписания,2,1);
			Движение.НДС		= Окр(СтрокаПартии.НДС  	  * КоэффСписания,2,1);
			
			Если Движение.Количество < КоличествоОсталосьПогасить Тогда
				КоэффПоступления = Движение.Количество / КоличествоОсталосьПогасить;
			Иначе
				КоэффПоступления = 1;
			КонецЕсли;
			
			// Вспомогательное поле, не являющееся ресурсом, но используемое
			// для формирования записей по переоценке принятого на комиссию.
			Движение.СтоимостьПоступление = СтоимостьПоступлениеОсталосьПогасить * КоэффПоступления;
			СтоимостьПоступлениеОсталосьПогасить = СтоимостьПоступлениеОсталосьПогасить - Движение.СтоимостьПоступление;
			
			// Реквизиты
			Движение.КодОперации	= СтрокаДокумента.КодОперацииПартииТоваров;
			
			КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - Движение.Количество;
			
			СтрокаПартии.Количество = СтрокаПартии.Количество - Движение.Количество;
			СтрокаПартии.Стоимость  = СтрокаПартии.Стоимость  - Движение.Стоимость;
			СтрокаПартии.НДС  		= СтрокаПартии.НДС		  - Движение.НДС;
			
			// Заполнение полей, специфических для учета
			ЗаполнитьПоляЗаписиСписания(Движение, СтрокаПартии, СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоэффСписания, КоэффПоступления);
			
			// Обработка движений, связанных со списанием определенных партий по документу (специфика разных видов учета)
			ВыполнитьСвязанныеСоСписаниемДвижения(СтрокаДокумента, СтруктураПараметров, Движение, КоэффСписания, КоэффПоступления);

			// Обработка поступления
			ВыполнитьКорДвижениеУпр(РегистрУчета, ПолучитьНаправлениеСписанияПоКодуОперации(СтрокаДокумента.КодОперацииПартииТоваров, СтрокаДокумента.СтатьяЗатрат), СтрокаДокумента, СтруктураПараметров, Движение);
			
		КонецЦикла;
	КонецЕсли;
	
	// Восстановим ранее сохраненное количество поступления для движений по НДС
	СтрокаДокумента.КоличествоПоступление = КоличествоПоступление;
	
	// Товара не хватило
	Если (КоличествоОсталосьПогасить > 0) Тогда
		СообщитьОНехваткеПартии(СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоличествоОсталосьПогасить);
	Иначе
		Если ЗначениеЗаполнено(СтрокаДокумента.ДокументПартии) 
		  И (СтрокаДокумента.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.ВозвратПоставщику)
		  И (КоличествоСписанноеПоДоументуПартии < СтрокаДокумента.Количество) Тогда
		    СообщитьОНеполномСписанииУказаннойПартии(СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоличествоОсталосьПогасить);
		КонецЕсли;			
	КонецЕсли; 
	
КонецПроцедуры // СписаниеПартий()

// Сообщение о нехватке партий для списания
//
// Параметры:
//	Нет.
//
Процедура СообщитьОНехваткеПартии(СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоличествоОсталосьПогасить)
	
	СообщитьОНехваткеПартииУпр(СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоличествоОсталосьПогасить);
	
КонецПроцедуры // СообщитьОНехваткеПартии()

// Сообщение о неполном списании явно указанной партии
//
// Параметры:
//	Нет.
//
Процедура СообщитьОНеполномСписанииУказаннойПартии(СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоличествоОсталосьПогасить)
	
	СообщитьОНеполномСписанииУказаннойПартииУпр(СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоличествоОсталосьПогасить);
	
КонецПроцедуры // СообщитьОНехваткеПартии()

/////////////////////////////////////////////////////////////////////////////////
// ДВИЖЕНИЯ,ВЫПОЛНЯЕМЫЕ ПО РЕЗУЛЬТАТУ СПИСАНИЯ

// Отражение продажи комиссионного товара
//
// Параметры:
//	Нет.
//
Процедура ВыполнитьДвиженияПоРеализованнымТоварамКомитента(СтруктураПараметров, ТаблицаСписания)
	
	МассивДокументов = Новый Массив;
	
	КодОперации = Перечисления.КодыОперацийПартииТоваров;
	
	Для Сч=1 По 2 Цикл
		
		// Первый раз движения по партиям на складах, второй - по партиям на комиссии
		Если Сч=1 Тогда
			
			ТаблицаСписанныхПартий = СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Скопировать();
			СписаноИз = "НаСкладах";
			
		Иначе
			ТаблицаСписанныхПартий = СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр.Скопировать();
			СписаноИз = "Переданные";
		КонецЕсли;
		
		// Из таблицы удалим движения, не относящиеся к продаже купленных товаров
		Инд=0;
		Пока Инд<ТаблицаСписанныхПартий.Количество() Цикл
			Строка = ТаблицаСписанныхПартий[Инд];
			
			// Товар должен быть комиссионным и списываться (операции определенных типов)
			Если Строка.СтатусПартии = Перечисления.СтатусыПартийТоваров.НаКомиссию 
				
				И ( Строка.КодОперации = КодОперации.Реализация
				ИЛИ Строка.КодОперации = КодОперации.РеализацияРозница
				ИЛИ Строка.КодОперации = КодОперации.РеализацияКомиссия
				ИЛИ Строка.КодОперации = КодОперации.СписаниеНаЗатраты
				ИЛИ Строка.КодОперации = КодОперации.СписаниеПоИнвентаризации
				ИЛИ Строка.КодОперации = КодОперации.Комплектация
				ИЛИ Строка.КодОперации = КодОперации.ВозвратОтПокупателя
				ИЛИ Строка.КодОперации = КодОперации.ВозвратОтПокупателяТекущийМесяц
				
				)Тогда
				
				Инд = Инд+1;
				МассивДокументов.Добавить(Строка.ДокументОприходования);
			Иначе
				ТаблицаСписанныхПартий.Удалить(Инд);
			КонецЕсли;
		КонецЦикла;
		
		Если ТаблицаСписанныхПартий.Количество()=0 Тогда
			Продолжить;
		КонецЕсли;
		
		ТаблицаРеквизитов = ПолныеПрава.ПолучитьПараметрыДокументовОприходования(МассивДокументов, СтруктураПараметров.Период);
		
		СоотвДок = Новый Соответствие;
		Для каждого Выборка Из ТаблицаРеквизитов Цикл
			СоотвДок.Вставить(Выборка.Ссылка, Новый Структура("ДоговорПоставки, ВедениеВзаиморасчетов, Сделка, Ссылка, Валюта, Курс, Кратность", Выборка.ДоговорПоставки, Выборка.ВедениеВзаиморасчетов, Выборка.Сделка, Выборка.Ссылка, Выборка.Валюта, Выборка.Курс, Выборка.Кратность));
		КонецЦикла;	
			
		ДоговорПоставки        = Выборка.ДоговорПоставки;
		ВалютаДоговораПоставки = Выборка.Валюта;
		
		ВалютаУпр = Неопределено;
		КурсВалютыУпр = 0;
		КратностьВалютыУпр = 0;
		
		Для Каждого Движение Из ТаблицаСписанныхПартий Цикл
			
			ДвижениеРТ = ДобавитьДвижениеВСтруктуруПараметров("РеализованныеТовары", СтруктураПараметров);
			ДвижениеРТ.Период = СтруктураПараметров.Период;
			
			Если Движение.КодОперации = КодОперации.ВозвратОтПокупателя Тогда
				ДвижениеРТ.ВидДвижения = Движение.ВидДвижения;
				ДвижениеРТ.Количество  = -Движение.Количество;
				
			Иначе
				Если Движение.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
					ДвижениеРТ.ВидДвижения = ВидДвиженияНакопления.Приход;
				Иначе
					ДвижениеРТ.ВидДвижения = ВидДвиженияНакопления.Расход;
				КонецЕсли;
				ДвижениеРТ.Количество = Движение.Количество;
			КонецЕсли;
			
			ДвижениеРТ.Номенклатура = Движение.Номенклатура;
			ДвижениеРТ.ХарактеристикаНоменклатуры = Движение.ХарактеристикаНоменклатуры;
			
			Если ЗначениеЗаполнено(ТаблицаСписания[0].ПоступлениеПриходныйОрдер) Тогда
				//Это корректировка ордерной партии, возмем серию из движений
				ДвижениеРТ.СерияНоменклатуры = Движение.СерияНоменклатуры;
			Иначе	
				// серия номенклатуры в реализованных товарах должна совпадать со списанными товарами
				ДвижениеРТ.СерияНоменклатуры = ТаблицаСписания[Движение.НомерСтрокиСписанныхТоваров-1].СерияНоменклатуры;
			КонецЕсли;	

			ДвижениеРТ.ДокументПоставки	 = Движение.ДокументОприходования;

			
			СтруктураДок = СоотвДок[Движение.ДокументОприходования];
			
			Если СтруктураДок <> Неопределено Тогда
				ДвижениеРТ.ДоговорКонтрагента = СтруктураДок.ДоговорПоставки;
				
				Если ЗначениеЗаполнено(СтруктураДок.Сделка) Тогда
					ДвижениеРТ.Сделка = СтруктураДок.Сделка;
				Иначе
					ДвижениеРТ.Сделка = Неопределено;
				КонецЕсли;	
				
				Если Движение.СуммаВыручки<>0 Тогда
					// Пересчет выручки в валюту взаиморасчетов с комитентом
					ДвижениеРТ.Выручка = 
					МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Движение.СуммаВыручки, 
					Движение.ВалютаДокумента, СтруктураДок.Валюта, 
					Движение.КурсДокумента, СтруктураДок.Курс, 
					Движение.КратностьДокумента, СтруктураДок.Кратность);

				Иначе // по стоимости принятия на комиссию
					
					Если ВалютаУпр = Неопределено Тогда
						ВалютаУпр = глЗначениеПеременной("ВалютаУправленческогоУчета");
						СтруктураКурсВалюты = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Движение.Период, Новый Структура("Валюта", ВалютаУпр));
						КурсВалютыУпр = СтруктураКурсВалюты.Курс;
						КратностьВалютыУпр = СтруктураКурсВалюты.Кратность;
					КонецЕсли;
					
					ДвижениеРТ.Выручка = 
					МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Движение.Стоимость + Движение.НДС, 
					ВалютаУпр, СтруктураДок.Валюта, 
					КурсВалютыУпр, СтруктураДок.Курс, 
					КратностьВалютыУпр, СтруктураДок.Кратность);
				КонецЕсли;
				
				Если Движение.КодОперации = КодОперации.ВозвратОтПокупателя Тогда
				  //ИЛИ Движение.КодОперации = КодОперации.ВозвратОтПокупателяТекущийМесяц Тогда
					ДвижениеРТ.Выручка = -ДвижениеРТ.Выручка;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры // ВыполнитьДвиженияПоРеализованнымТоварамКомитента(СтрокаДокумента, СтруктураПараметров, Движение)()

// Связанные со списанием движения по упр учету
//
// Параметры:
//	Нет.
//
Процедура ВыполнитьСвязанныеДвиженияУпр(СтрокаДокумента, СтруктураПараметров, Движение, КоэффСписания, КоэффПоступления)
	
	Если СтрокаДокумента.ОтражатьвУправленческомУчете Тогда
		Если СтрокаДокумента.НеСписывать И НЕ СтруктураПараметров.СписыватьПартииРасходнымОрдером 
		   И ( СтрокаДокумента.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.Реализация Или
				СтрокаДокумента.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.ПередачаТарыКонтрагенту) Тогда
				
			ОприходоватьТоварПринятыйНаОтветственноеХранение(СтрокаДокумента, СтруктураПараметров, Движение);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ВыполнитьСвязанныеДвиженияУпр()

// Выполнение движений, связанных со списанием партий по документу, имеющих специфику разных в разных видах учета
// Выполняется только при первичном проведении документа, не используется при корректировке списания.
//
Процедура ВыполнитьСвязанныеСоСписаниемДвижения(СтрокаДокумента, СтруктураПараметров, Движение, КоэффСписания, КоэффПоступления)
	
	ВыполнитьСвязанныеДвиженияУпр(СтрокаДокумента, СтруктураПараметров, Движение, КоэффСписания, КоэффПоступления);
	
	// Сюда можно добавить вызов процедур для выполнения движений по другим учетам
	
КонецПроцедуры // ВыполнитьСвязанныеСоСписаниемДвижения()

/////////////////////////////////////////////////////////////////////////////////
// КОРРЕСПОНДИРУЮЩИЕ СПИСАНИЮ ДВИЖЕНИЯ (ДЕБЕТ)
// Движение по себестоимости продаж
//
// Параметры
//  СтруктураПараметров  – Структура, содержащая общие параметры
//
Процедура СписаниеНаСебестоимостьПродажУпр(СтрокаДокумента, СтруктураПараметров, Строка, ТипЗаписи = "Прямая")

	Если СтрокаДокумента.ВедениеУчетаПоПроектам Тогда
		
		// Создадим таблицу значений для передачи в ОтразитьДвиженияПоПроектам
		
		ТаблицаПоТоварам = Новый ТаблицаЗначений;
		
		Для каждого Колонка ИЗ СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр.Колонки Цикл
			
			ТаблицаПоТоварам.Колонки.Добавить(Колонка.Имя,Колонка.ТипЗначения);
			
		КонецЦикла;
		
		Если НЕ (ТипЗнч(СтрокаДокумента.Проект) = Тип("СправочникСсылка.ВидыРаспределенияПоПроектам")) Тогда
		
			ТаблицаПоТоварам.Колонки.Добавить("Проект");
			ТаблицаПоТоварам.ЗаполнитьЗначения(СтрокаДокумента.Проект,"Проект");
			
		КонецЕсли;
		
		ТаблицаПоТоварам.Колонки.Добавить("СтоимостьБезСкидок",ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
		
		НоваяСтрока = ТаблицаПоТоварам.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);
		
		ТаблицаДвижений = Новый ТаблицаЗначений;
		
		Для каждого Колонка ИЗ СтруктураПараметров.ТаблицаДвиженийПродажиСебестоимость.Колонки Цикл
			
			ТаблицаДвижений.Колонки.Добавить(Колонка.Имя,Колонка.ТипЗначения);
			
		КонецЦикла;
		
		ТаблицаДвижений.Колонки.Добавить("Заказ");
		
		ТаблицаДвижений.Колонки.Добавить("СтатусПартии");
		
		ТаблицаДвижений.Колонки.Добавить("СуммаВыручки", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
		
		ТаблицаДвижений.Колонки.Добавить("СуммаНДСВыручкиБУ", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
		
		ТаблицаДвижений.Колонки.Добавить("НомерКорСтроки", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 0));

		УправлениеПроектами.ОтразитьДвиженияПоПроектам(ТаблицаПоТоварам,ТаблицаДвижений,СтрокаДокумента.Проект,СтруктураПараметров.Период,"Продажи");
		
		НуженНомерКорСтроки = Истина;
		Для Каждого Движение из ТаблицаДвижений Цикл
			СформироватьДвиженияСписанияНаСебестоимостьПродажУпр(СтрокаДокумента, СтруктураПараметров, Движение, ТипЗаписи);
			Если НуженНомерКорСтроки тогда
				НуженНомерКорСтроки = Ложь;
				Строка.НомерКорСтроки = СтруктураПараметров.ТекНомерСтрокиПродажиСебестоимость;
			КонецЕсли;
		КонецЦикла;
		
		Возврат;

	КонецЕсли;

	СформироватьДвиженияСписанияНаСебестоимостьПродажУпр(СтрокаДокумента, СтруктураПараметров, Строка, ТипЗаписи);

КонецПроцедуры // СписаниеНаСебестоимостьПродажУпр()

Процедура СформироватьДвиженияСписанияНаСебестоимостьПродажУпр(СтрокаДокумента, СтруктураПараметров, Строка, ТипЗаписи = "Прямая")
	
	Движение = ДобавитьДвижениеВСтруктуруПараметров("ПродажиСебестоимость", СтруктураПараметров);
	
	Если ТипЗаписи = "Сторно" Тогда
		КоэффициентСторно = -1;
	Иначе
		КоэффициентСторно = 1;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Движение,Строка);
	
	Движение.Активность = Истина;
	
	Если  ЗначениеЗаполнено(СтрокаДокумента.ЗаказСписания) Тогда
		
		Движение.ЗаказПокупателя = СтрокаДокумента.ЗаказСписания;
		
	ИначеЕсли  ЗначениеЗаполнено(Строка.Заказ) Тогда
		
		Движение.ЗаказПокупателя = Строка.Заказ;
		
	Иначе
		
		Движение.ЗаказПокупателя = Неопределено;
		
	КонецЕсли;
	
	Если  ЗначениеЗаполнено(СтрокаДокумента.НоменклатураКомплекта) Тогда
	
		Движение.Номенклатура = СтрокаДокумента.НоменклатураКомплекта;
	
	КонецЕсли;
	
	Если  ЗначениеЗаполнено(СтрокаДокумента.ХарактеристикаКомплекта) Тогда
	
		Движение.ХарактеристикаНоменклатуры = СтрокаДокумента.ХарактеристикаКомплекта;
	
	КонецЕсли;	
	
	Движение.Подразделение = СтрокаДокумента.Подразделение;
	
	Если ( ЗначениеЗаполнено(СтрокаДокумента.НоменклатураКомплекта)) И (НЕ СтрокаДокумента.Количество = 0) Тогда
		Движение.Количество = КоэффициентСторно*СтрокаДокумента.КоличествоКомплекта*Строка.Количество/СтрокаДокумента.Количество;
	Иначе
		Движение.Количество = КоэффициентСторно*Строка.Количество;
	КонецЕсли;	
	
	Если НЕ Строка.СтатусПартии = Перечисления.СтатусыПартийТоваров.НаКомиссию Тогда
		Движение.Стоимость  = КоэффициентСторно*Строка.Стоимость;
		Движение.НДС        = КоэффициентСторно*Строка.НДС;
	КонецЕсли;
	              
	Строка.НомерКорСтроки = СтруктураПараметров.ТекНомерСтрокиПродажиСебестоимость;
	
КонецПроцедуры//СформироватьДвиженияСписанияНаСебестоимостьПродажУпр()

// Склад

Процедура ЗаполнитьСериюВДвижении(СтрокаДокумента, Движение, Строка, СписаноИз, СтруктураПараметров)
	
	Если СтрокаДокумента.ВестиПартионныйУчетПоСериям тогда
		
		// Если задана новая серия или установлен флаг изменить серию - подставляем ее
		Если  ЗначениеЗаполнено(СтрокаДокумента.СерияНоменклатурыНовая) Или СтрокаДокумента.ИзменитьСерию Тогда
			
			Движение.СерияНоменклатуры	= СтрокаДокумента.СерияНоменклатурыНовая;
			
		Иначе
			
			// Серия номенклатуры в движении списания есть, только если списано со склада 
			Если СписаноИз = "НаСкладах" Тогда
			
				Движение.СерияНоменклатуры	= Строка.СерияНоменклатуры;
				
			КонецЕсли;
			// При возврате товара от комиссионера или переработчика серия есть в строке документа
			Если СтрокаДокумента.КодОперацииПартииТоваров = СтруктураПараметров.КодыОпераций.ВозвратОтКомиссионера 
				ИЛИ СтрокаДокумента.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.ВозвратОтПереработчика
				Тогда
				
				Движение.СерияНоменклатуры	= СтрокаДокумента.СерияНоменклатуры;
				
			КонецЕсли;
			
		КонецЕсли;
	Иначе
		
		Движение.СерияНоменклатуры	= Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры//ЗаполнитьСериюВДвижении()

// Поступление товаров на склад
//
// Параметры
//  СтруктураПараметров  – Структура, содержащая общие параметры
//  ТаблицаПоступающихПартий  – ТаблицаЗначений – поступающие партии товаров
//
Процедура ПоступлениеНаСкладУпр(СписаноИз, СтрокаДокумента, СтруктураПараметров, Строка)
	
	Учет = "Упр";
	
	Движение = ДобавитьДвижениеВСтруктуруПараметров("ПартииТоваровНаСкладах"+Учет, СтруктураПараметров);
	
	// Свойства
	Движение.Период      = Строка.Период;
	Движение.Регистратор = Строка.Регистратор;
	Движение.Активность  = Истина;
	
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	
	// Измерения
	Если УчетнаяПолитика("ВестиПартионныйУчетПоСкладам", Учет, , СтруктураПараметров) Тогда
		
		// Склад в движении есть, только если списано со склада
		Если СписаноИз = "НаСкладах" Тогда
			Движение.Склад	= Строка.Склад;
		Иначе
			
			Движение.Склад 	= СтрокаДокумента.Склад;
			
		КонецЕсли;
		
		// Если задан новый склад поступления, подставляем его
		Если  ЗначениеЗаполнено(СтрокаДокумента.СкладПолучатель) Тогда
			Движение.Склад 	= СтрокаДокумента.СкладПолучатель;
			
		КонецЕсли;
		
	Иначе
		
		Движение.Склад      = Неопределено;
	КонецЕсли;
	
	Движение.Номенклатура 	= Строка.Номенклатура;
	
	Движение.ХарактеристикаНоменклатуры = Строка.ХарактеристикаНоменклатуры;
	
	// Если задана новая характеристика или установлен флаг изменить характеристику, подставляем ее
	Если  ЗначениеЗаполнено(СтрокаДокумента.ХарактеристикаНоменклатурыНовая) Или СтрокаДокумента.ИзменитьХарактеристику Тогда
		
		Движение.ХарактеристикаНоменклатуры = СтрокаДокумента.ХарактеристикаНоменклатурыНовая;
		
	КонецЕсли;
	
	ЗаполнитьСериюВДвижении(СтрокаДокумента, Движение, Строка, СписаноИз, СтруктураПараметров);
	
	// Качество номенклатуры в движении есть только, если списано со склада
	Если СписаноИз = "НаСкладах" Тогда
		
		Движение.Качество	= Строка.Качество;
	КонецЕсли;
	
	// Если задано новое качество, подставляем его
	Если  ЗначениеЗаполнено(СтрокаДокумента.КачествоНовое) Тогда
		
		Движение.Качество = СтрокаДокумента.КачествоНовое;
	КонецЕсли;
	
	// Вместо пустого качества - новый
	Если НЕ ЗначениеЗаполнено(Движение.Качество) Тогда
		Движение.Качество = Справочники.Качество.Новый;
	КонецЕсли;
	
	Движение.ДокументОприходования = Строка.ДокументОприходования;
	
	Если НЕ СтрокаДокумента.КодОперацииПартииТоваров = СтруктураПараметров.КодыОпераций.ВозвратОтКомиссионера И 
		НЕ СтрокаДокумента.КодОперацииПартииТоваров = СтруктураПараметров.КодыОпераций.КорректировкаСерийИХарактеристик И
		НЕ СтрокаДокумента.КодОперацииПартииТоваров = СтруктураПараметров.КодыОпераций.КорректировкаКачества 
		Тогда
	
		Движение.Заказ	= ?( ЗначениеЗаполнено(СтрокаДокумента.ЗаказСписания), СтрокаДокумента.ЗаказСписания, Неопределено);
	
	КонецЕсли;
	
	Движение.СтатусПартии = Строка.СтатусПартии;
	
	// Новая номенклатура-для комплектации: меняется все
	// Аналогично обрабатывается включение оборудования в состав МПЗ
	Если  ЗначениеЗаполнено(СтрокаДокумента.НоменклатураНовая) Тогда
		
		Движение.Номенклатура = СтрокаДокумента.НоменклатураНовая;
		Движение.ХарактеристикаНоменклатуры = СтрокаДокумента.ХарактеристикаНоменклатурыНовая;
		Движение.СерияНоменклатуры	= СтрокаДокумента.СерияНоменклатурыНовая;
		
		СпособОценкиМПЗ = ВРег(УчетнаяПолитика("СпособОценкиМПЗ", Учет, , СтруктураПараметров));
		Если СпособОценкиМПЗ <> "ПО СРЕДНЕЙ" тогда
			Движение.ДокументОприходования	= СтрокаДокумента.ДокументОприходованияНовый;
		Иначе
			Движение.ДокументОприходования	= Неопределено;
		КонецЕсли;

		// Комплект(ующее) приходуется со статусом партии, указанным в документе
		// Если значение не заполнено - статус партии "купленный"
		// Статус партии "по ордеру" не меняем
		Если СтрокаДокумента.КодОперацииПартииТоваров = СтруктураПараметров.КодыОпераций.Комплектация 
		Тогда
			Если Строка.СтатусПартии = Перечисления.СтатусыПартийТоваров.ПоОрдеру Тогда
				Движение.СтатусПартии = Строка.СтатусПартии;
			Иначе	
				Если  ЗначениеЗаполнено(СтрокаДокумента.СтатусПартииНовый) тогда
					Движение.СтатусПартии = СтрокаДокумента.СтатусПартииНовый;
				Иначе
					Движение.СтатусПартии = Перечисления.СтатусыПартийТоваров.Купленный;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	
	// Вспомогательные поля для списания
	Движение.ДокументОприходованияДата = Строка.ДокументОприходованияДата;
	
	// Ресурсы
	Движение.Количество	= Строка.Количество;
	Движение.Стоимость	= Строка.Стоимость;
	Движение.НДС		= Строка.НДС;	
	
	// Новое количество-для комплектации
	Если СтрокаДокумента.КодОперацииПартииТоваров = СтруктураПараметров.КодыОпераций.Комплектация Тогда 
		Если Строка.Количество < 0 Тогда
			Движение.Количество	= -Строка.КоличествоПоступление;
		Иначе	
			Движение.Количество	= Строка.КоличествоПоступление;
		КонецЕсли;	
	КонецЕсли;
	
	// Оприходование может быть с новой стоимостью
	Если Строка.СтоимостьПоступление<>0 Тогда
		Если Движение.Стоимость + Движение.НДС = 0 Тогда
			Движение.Стоимость = Строка.СтоимостьПоступление;
			Движение.НДС       = 0;
		Иначе
			Движение.Стоимость = Движение.Стоимость * Строка.СтоимостьПоступление / (Движение.Стоимость + Движение.НДС);
			Движение.НДС       = Строка.СтоимостьПоступление - Движение.Стоимость;
		КонецЕсли;
	КонецЕсли;

	// Реквизиты
	Движение.КодОперации  = СтрокаДокумента.КодОперацииПартииТоваров;

	Строка.НомерКорСтроки = СтруктураПараметров["ТекНомерСтрокиПартииТоваровНаСкладах"+Учет];
	
КонецПроцедуры//ПоступлениеНаСкладУпр()

// Переданные на комиссию

// Поступление товаров отданных
//
// Параметры
//  СтруктураПараметров  – Структура, содержащая общие параметры
//  ТаблицаПоступающихПартий  – ТаблицаЗначений – поступающие партии товаров
//
Процедура ПоступлениеПереданныхУпр(СтрокаДокумента, СтруктураПараметров, Строка)
	
	Учет = "Упр";
	
	Движение = ДобавитьДвижениеВСтруктуруПараметров("ПартииТоваровПереданные"+Учет, СтруктураПараметров);
	
	// Свойства
	Движение.Период                 = Строка.Период;
	Движение.Регистратор            = Строка.Регистратор;
	Движение.Активность				= Истина;
	
	Движение.ВидДвижения			= ВидДвиженияНакопления.Приход;
	
	Движение.Номенклатура 			= Строка.Номенклатура;
	Движение.ХарактеристикаНоменклатуры = Строка.ХарактеристикаНоменклатуры;
	Движение.ДокументОприходования	= Строка.ДокументОприходования;
	
	Движение.СтатусПартии	    = Строка.СтатусПартии;
	Движение.СтатусПередачи		= СтрокаДокумента.СтатусПередачи;
	
	Движение.ДоговорКонтрагента	= СтрокаДокумента.ДоговорКонтрагента;
	Движение.ДокументПередачи   	= СтрокаДокумента.ДокументПередачи;
	
	// Вспомогательные поля для списания
	Движение.ДокументОприходованияДата	= Строка.ДокументОприходованияДата;
	
	// Ресурсы
	Движение.Количество = Строка.Количество;
		
	Движение.Стоимость	= Строка.Стоимость;
	
	Движение.НДС		= Строка.НДС;
	
	// Оприходование с новой стоимостью
	Если Строка.СтоимостьПоступление<>0 Тогда
		Если Движение.Стоимость + Движение.НДС = 0 Тогда
			Движение.Стоимость = Строка.СтоимостьПоступление;
			Движение.НДС       = 0;
		Иначе
			Движение.Стоимость = Движение.Стоимость * Строка.СтоимостьПоступление / (Движение.Стоимость + Движение.НДС);
			Движение.НДС       = Строка.СтоимостьПоступление - Движение.Стоимость;
		КонецЕсли;
	КонецЕсли;
	
	// Реквизиты
	Движение.КодОперации		    = СтрокаДокумента.КодОперацииПартииТоваров;
						
	Строка.НомерКорСтроки = СтруктураПараметров["ТекНомерСтрокиПартииТоваровПереданные"+Учет];

КонецПроцедуры

// Поступление товаров на постоянные затраты
//
// Параметры
//  СтруктураПараметров  – Структура, содержащая общие параметры
//
Процедура СписаниеНаПостоянныеЗатратыУпр(СтрокаДокумента, СтруктураПараметров, Строка)
	
	СформироватьДвиженияСписаниеНаПостоянныеЗатратыУпр(СтрокаДокумента, СтруктураПараметров, Строка);

КонецПроцедуры//СписаниеНаПостоянныеЗатратыУпр

Процедура СформироватьДвиженияСписаниеНаПостоянныеЗатратыУпр(СтрокаДокумента, СтруктураПараметров, Строка)

	Учет = "Упр";

	Движение = ДобавитьДвижениеВСтруктуруПараметров("Затраты"+Учет, СтруктураПараметров);

	// Свойства
	ЗаполнитьЗначенияСвойств(Движение, СтрокаДокумента);
	
	// Свойства из основного движения
	Движение.Период      = Строка.Период;
	Движение.Регистратор = Строка.Регистратор;
	
	// Свойства кор. движения
	Движение.Активность  = Истина;

	// Заказ заполняем только для затрат на сбыт
	Если СтрокаДокумента.СтатьяЗатрат.ХарактерЗатрат = Перечисления.ХарактерЗатрат.РасходыНаСбыт Тогда
		ЗаполнитьЗаказВДвижении(Учет, Движение, СтрокаДокумента, , СтруктураПараметров);
	КонецЕсли;

	// Ресурсы
	Движение.Сумма				    = Строка.Стоимость;
	Движение.НДС 				    = Строка.НДС;

	Строка.НомерКорСтроки = СтруктураПараметров["ТекНомерСтрокиЗатраты"+Учет];

КонецПроцедуры // СформироватьДвиженияСписаниеНаПостоянныеЗатратыУпр

Процедура ЗаполнитьЗаказВДвижении(Учет, Движение, СтрокаДокумента, Организация = Неопределено, СтруктураПараметров)
	
	// Заказ списания
	ЗаказСписания = Неопределено;
	Если ЗначениеЗаполнено(СтрокаДокумента.ЗаказСписания) Тогда
		Если ТипЗнч(СтрокаДокумента.ЗаказСписания) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			ЗаказСписания = СтрокаДокумента.ЗаказСписания;
		КонецЕсли;
	КонецЕсли;
	
	Движение.Заказ = ЗаказСписания;

КонецПроцедуры // ЗаполнитьЗаказВДвижении

// Общая процедура поступления: маршрутизирует алгоритм поступления
//
Процедура ВыполнитьКорДвижениеУпр(СписаноИз, ПриходоватьВ, СтрокаДокумента, СтруктураПараметров, Движение) Экспорт
			
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		
		// Общие для всех учетов алгоритмы
		Если ПриходоватьВ = "НаСкладах" Тогда
			
			ПоступлениеНаСкладУпр(СписаноИз, СтрокаДокумента, СтруктураПараметров, Движение);
			
		ИначеЕсли ПриходоватьВ = "Переданные" Тогда
			
			ПоступлениеПереданныхУпр(СтрокаДокумента, СтруктураПараметров, Движение);
			
		ИначеЕсли ПриходоватьВ = "СебестоимостьПродаж" Тогда
			
			СписаниеНаСебестоимостьПродажУпр(СтрокаДокумента, СтруктураПараметров, Движение);
		
		ИначеЕсли ПриходоватьВ = "Затраты" Тогда
			
			СписаниеНаПостоянныеЗатратыУпр(СтрокаДокумента, СтруктураПараметров, Движение);
			
		ИначеЕсли ПриходоватьВ = "СтоимостьОСПриПринятииКУчету" Тогда
			
			СписаниеНаАмортизациюУпр(СтрокаДокумента, СтруктураПараметров, Движение);
			
		ИначеЕсли ПриходоватьВ = "ПартииМалоценкиВЭксплуатации" Тогда
			
			ПередачаМалоценкиВЭксплуатациюУпр(СтрокаДокумента, СтруктураПараметров, Движение);
			
		ИначеЕсли ПриходоватьВ = "ВложенияВоВнеоборотныеАктивы" Тогда
			
			ВложенияВоВнеоборотныеАктивыУпр(СтрокаДокумента, СтруктураПараметров, Движение);
			
		ИначеЕсли ПриходоватьВ = "ПроизводственныеРасходы" Тогда
			
			ПоступлениеВПроизводствоУпр(СписаноИз, СтрокаДокумента, СтруктураПараметров, Движение);
			
		ИначеЕсли ПриходоватьВ = "БракВПроизводстве" Тогда
			
			СписаниеНаБракУпр(СтрокаДокумента, СтруктураПараметров, Движение);
			
		ИначеЕсли ПриходоватьВ = "ОбщепроизводственныеРасходы"
			ИЛИ ПриходоватьВ = "АдминистративныеРасходы" 
			ИЛИ ПриходоватьВ = "РасходыНаСбыт"
			ИЛИ ПриходоватьВ = "ПрочиеОперационныеРасходы"
			ИЛИ ПриходоватьВ = "ПрочиеРасходы" 
			ИЛИ ПриходоватьВ = "ТранспортноЗаготовительныеРасходы" Тогда
			
			СписаниеНаПостоянныеЗатратыУпр(СтрокаДокумента, СтруктураПараметров, Движение);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры
// Общие

////////////////////////////////////////////////////////////////////////////////
// СПЕЦИФИЧЕСКИЕ ПРОЦЕДУРЫ ДВИЖЕНИЯ ПАРТИЙ ДЛЯ ОПРЕДЕЛЕННЫХ ВИДОВ ДОКУМЕНТОВ

// Процедуры, обслуживающие возврат товара от покупателя
// Возврат товара от комиссионера обслуживается общим случаем списания партий товаров
Процедура ПолучитьТаблицуПартийПринятыхНаОтветственноеХранение(СтруктураПараметров, ДокументПартии, СтрУчет)
	
	Запрос = Новый Запрос;
	Если СтрУчет = "Управленческий" Тогда
	
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПартииТоваровНаСкладах.Номенклатура КАК Номенклатура,
		               |	ПартииТоваровНаСкладах.Склад КАК Склад,
		               |	ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	ПартииТоваровНаСкладах.СерияНоменклатуры КАК СерияНоменклатуры,
		               |	ПартииТоваровНаСкладах.ДокументОприходования КАК ДокументОприходования,
		               |	ПартииТоваровНаСкладах.СтатусПартии КАК СтатусПартии,
		               |	ПартииТоваровНаСкладах.Заказ КАК Заказ,
		               |	ПартииТоваровНаСкладах.Качество КАК Качество,
		               |	СУММА(ВЫБОР
		               |			КОГДА ПартииТоваровНаСкладах.ВидДвижения = &ВидДвиженияПриход
		               |				ТОГДА ПартииТоваровНаСкладах.Количество
		               |			ИНАЧЕ -ПартииТоваровНаСкладах.Количество
		               |		КОНЕЦ) КАК Количество,
		               |	СУММА(ВЫБОР
		               |			КОГДА ПартииТоваровНаСкладах.ВидДвижения = &ВидДвиженияПриход
		               |				ТОГДА ПартииТоваровНаСкладах.Стоимость
		               |			ИНАЧЕ -ПартииТоваровНаСкладах.Стоимость
		               |		КОНЕЦ) КАК Стоимость
		               |ИЗ
		               |	РегистрНакопления.ПартииТоваровНаСкладах КАК ПартииТоваровНаСкладах
		               |ГДЕ
		               |	ПартииТоваровНаСкладах.КодОперации = &КодОперации
		               |	И (ПартииТоваровНаСкладах.ДокументДвижения = &ДокументПартии
		               |			ИЛИ ПартииТоваровНаСкладах.Регистратор = &ДокументПартии)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ПартииТоваровНаСкладах.Номенклатура,
		               |	ПартииТоваровНаСкладах.Склад,
		               |	ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры,
		               |	ПартииТоваровНаСкладах.СерияНоменклатуры,
		               |	ПартииТоваровНаСкладах.ДокументОприходования,
		               |	ПартииТоваровНаСкладах.СтатусПартии,
		               |	ПартииТоваровНаСкладах.Заказ,
		               |	ПартииТоваровНаСкладах.Качество";
					   
	Иначе
		Возврат;
	
	КонецЕсли; 
				   
	Запрос.УстановитьПараметр("КодОперации", Перечисления.КодыОперацийПартииТоваров.ОтложеннаяОтгрузка);
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ДокументПартии", ДокументПартии);
	
	СтруктураПараметров.Вставить("ТаблицаПартийПринятыхНаОтветственноеХранение"+СтрУчет,Запрос.Выполнить().Выгрузить());


КонецПроцедуры//ПолучитьТаблицуПартийПринятыхНаОтветственноеХранение()

// Возвращает предыдущий документ, которым реализовался тот же товар
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Предыдущий документ реализации
//
Функция ПредыдущийДокументРеализации(СтрокаДокумента)
	
	
	// Ищем документ того же типа, выписанный с того же склада
	ТипДок = ТипЗнч(СтрокаДокумента.Регистратор);
	
	МетаДок = Метаданные.НайтиПоТипу(ТипДок);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СписанныеТовары.Регистратор
	|ИЗ
	|	РегистрСведений.СписанныеТовары КАК СписанныеТовары
	|
	|ГДЕ
	|	СписанныеТовары.Номенклатура = &Номенклатура И
	|	СписанныеТовары.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры И
	|	СписанныеТовары.СерияНоменклатуры = &СерияНоменклатуры И
	|	СписанныеТовары.Склад = &Склад И
	|	СписанныеТовары.Количество >= &Количество И
	|	СписанныеТовары.КодОперацииПартииТоваров = ЗНАЧЕНИЕ(Перечисление.КодыОперацийПартииТоваров.РеализацияРозница) И 
	|	СписанныеТовары.Период < &Период И
	|	(СписанныеТовары.Регистратор ССЫЛКА Документ."+МетаДок.Имя+")
	|
	|УПОРЯДОЧИТЬ ПО
	|	СписанныеТовары.Период УБЫВ");
	
	Запрос.УстановитьПараметр("Номенклатура", СтрокаДокумента.Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", СтрокаДокумента.ХарактеристикаНоменклатуры);
	Запрос.УстановитьПараметр("СерияНоменклатуры", СтрокаДокумента.СерияНоменклатуры);
	Запрос.УстановитьПараметр("Склад", СтрокаДокумента.Склад);
	Запрос.УстановитьПараметр("Количество", СтрокаДокумента.Количество);
	Запрос.УстановитьПараметр("Период", СтрокаДокумента.Период);
	Запрос.УстановитьПараметр("Регистратор", СтрокаДокумента.Регистратор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Регистратор;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции // ПредыдущийДокументРеализации()

// Определяет параметры возврата 
//
Процедура ПолучитьПараметрыВозвратаОтПокупателя(СтрокаДокумента, СтруктураПараметров, ИмяРегистраПартии, СтрУчет, ВестиПУПоСкладам, НаборОснование)
	
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		
		ИмяРегистраПартии = "ПартииТоваровНаСкладахУпр";
		ВестиПУПоСкладам  =УчетнаяПолитика("ВестиПартионныйУчетПоСкладам", "Упр", , СтруктураПараметров);
		СтрУчет			  ="Управленческий";
		НаборОснование	  = РегистрыНакопления.ПартииТоваровНаСкладах.СоздатьНаборЗаписей();
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает таблицу партий товаров, которую можно оприходовать документом "Возврат товаров от покупателя"
//
Функция ПолучитьТаблицуЗаписейОснованияВозврата(СтрокаДокумента, СтруктураПараметров, ДокументПартии, НаборОснование, СтрУчет, ИмяРегистраПартии)
	
	// Партии, которые можно вернуть, получаем при первом вызове и помещаем в структуру параметров
	// Затем до конца обработки документа используем таблицы значений из структуры параметров
	ТаблицаЗаписейОснования = Неопределено;
	ТаблицыПартийВозврата = Неопределено;
	
	// В табличной части возврата от покупателя может быть указано произвольное количество документов партии
	Если СтруктураПараметров.Свойство("ТаблицыПартийВозврата"+СтрУчет,ТаблицыПартийВозврата) тогда
		ТаблицаЗаписейОснования = ТаблицыПартийВозврата.Получить(ДокументПартии.УникальныйИдентификатор());
		Если ТаблицаЗаписейОснования <> Неопределено тогда
			Возврат ТаблицаЗаписейОснования;
		КонецЕсли;
	КонецЕсли;
	
	КодыОпераций = СтруктураПараметров.КодыОпераций;
	
	// Если указан партиеобразующий документ, данные берутся из него
	Если Метаданные.РегистрыНакопления.ПартииТоваровНаСкладах.Измерения.ДокументОприходования.Тип.СодержитТип(ТипЗнч(ДокументПартии)) Тогда
		
		// Для сообщения о нехватке партии
		
		СтруктураПараметров.Вставить("СтрокаСообщенияНеНайдено", "Не найдено поступление ");
		СтруктураПараметров.Вставить("СтрокаСообщенияДокументПартии", ", по документу партии ");
		// 
		Если СтрокаДокумента.ОтражатьВУправленческомУчете 
		   И ТипЗнч(ДокументПартии)=Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		   Тогда
			// выберем движения документа партии по регистру "ПартииТоваров" за исключением сторнирующих движений
			ЗапросПоДокументуПартии = Новый Запрос;
			ЗапросПоДокументуПартии.Текст =  "ВЫБРАТЬ
			|	ПартииТоваровНаСкладах.Период,
			|	ПартииТоваровНаСкладах.Регистратор,
			|	ПартииТоваровНаСкладах.Номенклатура,
			|	ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры,
			|	ПартииТоваровНаСкладах.СерияНоменклатуры,
			|	ПартииТоваровНаСкладах.ДокументОприходования,
			|	ПартииТоваровНаСкладах.СтатусПартии,
			|	ПартииТоваровНаСкладах.Заказ,
			|	ПартииТоваровНаСкладах.Качество,
			|	ПартииТоваровНаСкладах.Количество,
			|	ПартииТоваровНаСкладах.НДС,
			|	ПартииТоваровНаСкладах.Стоимость
			|ИЗ
			|	РегистрНакопления.ПартииТоваровНаСкладах КАК ПартииТоваровНаСкладах
			|ГДЕ
			|	ПартииТоваровНаСкладах.СтатусПартии <> &СтатусПартии
			|	И ПартииТоваровНаСкладах.Регистратор = &Регистратор";
			
			ЗапросПоДокументуПартии.УстановитьПараметр("СтатусПартии",Перечисления.СтатусыПартийТоваров.ПоОрдеру);
			ЗапросПоДокументуПартии.УстановитьПараметр("Регистратор",ДокументПартии);
			ТаблицаЗаписейОснования = ЗапросПоДокументуПартии.Выполнить().Выгрузить();
		Иначе
			НаборОснование.Отбор.Регистратор.Установить(ДокументПартии);
			НаборОснование.Прочитать();
			ТаблицаЗаписейОснования = НаборОснование.Выгрузить();
		КонецЕсли;
		
		// Если указан документ списания, данные о партиях берутся из него
	Иначе
		// Для сообщения о нехватке партии
		СтруктураПараметров.Вставить("СтрокаСообщенияНеНайдено", "Не найдено ");
		СтруктураПараметров.Вставить("СтрокаСообщенияДокументПартии",  ", списанного документом ");
		
		// Движения реализации могут быть выполнены с неточной или нерассчитанной себестоимостью
		// Кроме того в управленческом учете при ордерной схеме движения реализации может выполнять поступление
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ВидДвиженияРасход",ВидДвиженияНакопления.Расход);
		МассивРеализация = Новый Массив;
		МассивРеализация.Добавить(КодыОпераций.ПередачаТарыКонтрагенту);
		МассивРеализация.Добавить(КодыОпераций.Реализация);
		МассивРеализация.Добавить(КодыОпераций.РеализацияКомиссия);
		МассивРеализация.Добавить(КодыОпераций.РеализацияРозница);
		
		Запрос.УстановитьПараметр("КодыСписание",МассивРеализация);
		Запрос.УстановитьПараметр("Регистратор",ДокументПартии);
		// Возврат не может быть раньше реализации
		Запрос.УстановитьПараметр("Период",СтруктураПараметров.Период);
		
		Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
			ЗаполнитьЗапросПоВозвратуУпр(Запрос);
		КонецЕсли;	
			
		ТаблицаЗаписейОснования = Запрос.Выполнить().Выгрузить();
		
		ПолучитьТаблицуПартийПринятыхНаОтветственноеХранение(СтруктураПараметров, ДокументПартии, СтрУчет);
		
	КонецЕсли;//Если Метаданные.РегистрыНакопления.ПартииТоваровНаСкладах.Измерения.ДокументОприходования.Тип.СодержитТип(ТипЗнч(ДокументПартии)) Тогда
	
	// Добавим вспомогательное поле ДокументОприходованияДата
	ТаблицаЗаписейОснования.Колонки.Добавить("ДокументОприходованияДата", ОбщегоНазначения.ПолучитьОписаниеТиповДаты(ЧастиДаты.ДатаВремя));
	
	// В полученной таблице найдем партии, которые уже возвращались другими возвратами
	ТаблицаЗаписейОснования = ПодготовитьТаблицуЗаписейОснования(ТаблицаЗаписейОснования, СтруктураПараметров, ДокументПартии, СтрУчет);
	
	Если НЕ СтруктураПараметров.Свойство("ТаблицыПартийВозврата"+СтрУчет,ТаблицыПартийВозврата) тогда
		ТаблицыПартийВозврата = Новый Соответствие;
	КонецЕсли;
	
	ТаблицыПартийВозврата.Вставить(ДокументПартии.УникальныйИдентификатор(),ТаблицаЗаписейОснования);
	
	СтруктураПараметров.Вставить("ТаблицыПартийВозврата"+СтрУчет,ТаблицыПартийВозврата);
	
	Возврат ТаблицаЗаписейОснования;
	
КонецФункции// ПолучитьТаблицуЗаписейОснованияВозврата()

Процедура ЗаполнитьЗапросПоВозвратуУпр(Запрос)

Запрос.Текст = "ВЫБРАТЬ
               |	Подзапрос.Номенклатура,
               |	Подзапрос.ХарактеристикаНоменклатуры,
               |	Подзапрос.СерияНоменклатуры,
               |	Подзапрос.ДокументОприходования,
               |	Подзапрос.СтатусПартии,
               |	Подзапрос.Заказ,
               |	СУММА(Подзапрос.Количество) КАК Количество,
               |	СУММА(Подзапрос.Стоимость) КАК Стоимость,
			   |	СУММА(Подзапрос.НДС) КАК НДС,
               |	Подзапрос.СтоимостьПоступление,
               |	Подзапрос.КодОперацииПартииТоваров,
               |	Подзапрос.Регистратор,
               |	Подзапрос.Период,
               |	Подзапрос.Качество,
               |	Подзапрос.ВестиПартионныйУчетПоСериям
               |ИЗ
               |	(ВЫБРАТЬ
               |		ПартииТоваровНаСкладах.Номенклатура КАК Номенклатура,
               |		ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
               |		ПартииТоваровНаСкладах.СерияНоменклатуры КАК СерияНоменклатуры,
               |		ПартииТоваровНаСкладах.ДокументОприходования КАК ДокументОприходования,
               |		ПартииТоваровНаСкладах.СтатусПартии КАК СтатусПартии,
               |		ПартииТоваровНаСкладах.Заказ КАК Заказ,
               |		ПартииТоваровНаСкладах.Количество КАК Количество,
               |		ПартииТоваровНаСкладах.Стоимость КАК Стоимость,
               |		ПартииТоваровНаСкладах.НДС КАК НДС,
               |		0 КАК СтоимостьПоступление,
               |		ПартииТоваровНаСкладах.КодОперации КАК КодОперацииПартииТоваров,
               |		ПартииТоваровНаСкладах.Регистратор КАК Регистратор,
               |		ПартииТоваровНаСкладах.Период КАК Период,
               |		ПартииТоваровНаСкладах.Качество КАК Качество,
               |		ПартииТоваровНаСкладах.Номенклатура.ВестиПартионныйУчетПоСериям КАК ВестиПартионныйУчетПоСериям
               |	ИЗ
               |		РегистрНакопления.ПартииТоваровНаСкладах КАК ПартииТоваровНаСкладах
               |	ГДЕ
               |		ПартииТоваровНаСкладах.ВидДвижения = &ВидДвиженияРасход
               |		И ПартииТоваровНаСкладах.КодОперации В(&КодыСписание)
               |		И ПартииТоваровНаСкладах.Регистратор = &Регистратор
               |		И ПартииТоваровНаСкладах.Период < &Период
               |	
               |	ОБЪЕДИНИТЬ ВСЕ
               |	
               |	ВЫБРАТЬ
               |		ПартииТоваровНаСкладах.Номенклатура,
               |		ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры,
               |		ПартииТоваровНаСкладах.СерияНоменклатуры,
               |		ПартииТоваровНаСкладах.ДокументОприходования,
               |		ПартииТоваровНаСкладах.СтатусПартии,
               |		ПартииТоваровНаСкладах.Заказ,
               |		ПартииТоваровНаСкладах.Количество,
               |		ПартииТоваровНаСкладах.Стоимость,
               |		ПартииТоваровНаСкладах.НДС,
               |		0,
               |		ПартииТоваровНаСкладах.КодОперации,
               |		ПартииТоваровНаСкладах.ДокументДвижения,
               |		ПартииТоваровНаСкладах.ДокументДвиженияПериод,
               |		ПартииТоваровНаСкладах.Качество,
               |		ПартииТоваровНаСкладах.Номенклатура.ВестиПартионныйУчетПоСериям
               |	ИЗ
               |		РегистрНакопления.ПартииТоваровНаСкладах КАК ПартииТоваровНаСкладах
               |	ГДЕ
               |		ПартииТоваровНаСкладах.ВидДвижения = &ВидДвиженияРасход
               |		И ПартииТоваровНаСкладах.КодОперации В(&КодыСписание)
               |		И ПартииТоваровНаСкладах.ДокументДвижения = &Регистратор
               |		И ПартииТоваровНаСкладах.Период < &Период) КАК Подзапрос
               |
               |СГРУППИРОВАТЬ ПО
               |	Подзапрос.Номенклатура,
               |	Подзапрос.ХарактеристикаНоменклатуры,
               |	Подзапрос.СерияНоменклатуры,
               |	Подзапрос.ДокументОприходования,
               |	Подзапрос.СтатусПартии,
               |	Подзапрос.Заказ,
               |	Подзапрос.СтоимостьПоступление,
               |	Подзапрос.КодОперацииПартииТоваров,
               |	Подзапрос.Регистратор,
               |	Подзапрос.Период,
               |	Подзапрос.Качество,
               |	Подзапрос.ВестиПартионныйУчетПоСериям"

КонецПроцедуры // ЗаполнитьЗапросПоВозвратуУпр(Запрос)()

// Корректирует таблицу основания с учетом ранее проведенных возвратов
// Таблица основания корректируется либо по документу поступления, либо по документу реализации
// Ситуация, когда есть возвраты и по документу поступления и по документу реализации не обслуживается
//
Функция ПодготовитьТаблицуЗаписейОснования(ТаблицаЗаписейОснования, СтруктураШапкиДокумента, ДокументПартии, СтрУчет)
	
	ЗапросПоВозвратам = Новый Запрос;
	
	ЗапросПоВозвратам.Текст = "ВЫБРАТЬ
	                          |	ПартииТоваровНаСкладах.Номенклатура КАК Номенклатура,
	                          |	ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	                          |	ПартииТоваровНаСкладах.СерияНоменклатуры КАК СерияНоменклатуры,
	                          |	ПартииТоваровНаСкладах.ДокументОприходования КАК ДокументОприходования,
	                          |	ВЫБОР
	                          |		КОГДА ПартииТоваровНаСкладах.КодОперации = &ВозвратТекущегоМесяца
	                          |			ТОГДА -ПартииТоваровНаСкладах.Количество
	                          |		ИНАЧЕ ПартииТоваровНаСкладах.Количество
	                          |	КОНЕЦ КАК Количество,
	                          |	ВЫБОР
	                          |		КОГДА ПартииТоваровНаСкладах.КодОперации = &ВозвратТекущегоМесяца
	                          |			ТОГДА -ПартииТоваровНаСкладах.Стоимость
	                          |		ИНАЧЕ ПартииТоваровНаСкладах.Стоимость
	                          |	КОНЕЦ КАК Стоимость,
	                          |	ВЫБОР
	                          |		КОГДА ПартииТоваровНаСкладах.КодОперации = &ВозвратТекущегоМесяца
	                          |			ТОГДА -ПартииТоваровНаСкладах.НДС
	                          |		ИНАЧЕ ПартииТоваровНаСкладах.НДС
	                          |	КОНЕЦ КАК НДС,
	                          |	ПартииТоваровНаСкладах.НомерСтрокиСписанныхТоваров
	                          |ИЗ
	                          |	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	                          |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах КАК ПартииТоваровНаСкладах
	                          |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СписанныеТовары КАК СписанныеТовары
	                          |			ПО (СписанныеТовары.Регистратор = ПартииТоваровНаСкладах.Регистратор)
	                          |				И (СписанныеТовары.ДокументПартии = &ДокументПартии)
	                          |				И (СписанныеТовары.НомерСтроки = ПартииТоваровНаСкладах.НомерСтрокиСписанныхТоваров)
	                          |		ПО (ПартииТоваровНаСкладах.Регистратор = ВозвратТоваровОтПокупателя.Ссылка)
	                          |ГДЕ
	                          |	ВозвратТоваровОтПокупателя.Товары.ДокументПартии = &ДокументПартии
	                          |	И ВозвратТоваровОтПокупателя.МоментВремени > &МоментНач
	                          |	И ВозвратТоваровОтПокупателя.МоментВремени < &МоментКон";
	
	ЗапросПоВозвратам.УстановитьПараметр("ДокументПартии",ДокументПартии);
	
	СтруктураРеквизитовДокумента = Новый Структура("Дата");
	ПолучитьРеквизитыОбъекта(ДокументПартии, СтруктураРеквизитовДокумента);
	МоментНач = Новый МоментВремени(СтруктураРеквизитовДокумента.Дата,ДокументПартии);
	МоментКон = Новый МоментВремени(СтруктураШапкиДокумента.Период,СтруктураШапкиДокумента.Регистратор);
	
	ЗапросПоВозвратам.УстановитьПараметр("МоментНач",МоментНач);
	ЗапросПоВозвратам.УстановитьПараметр("МоментКон",МоментКон);
	ЗапросПоВозвратам.УстановитьПараметр("ВозвратТекущегоМесяца", Перечисления.КодыОперацийПартииТоваров.ВозвратОтПокупателяТекущийМесяц);
	
	// Изменим текст запроса, если учет отличается от управленческого
	Если СтрУчет <> "Управленческий" тогда
		ЗапросПоВозвратам.Текст = СтрЗаменить(ЗапросПоВозвратам.Текст,".ПартииТоваровНаСкладах",".ПартииТоваровНаСкладах"+СтрУчет+"Учет");
	КонецЕсли;
	
	РезультатЗапроса = ЗапросПоВозвратам.Выполнить();
	Если Не РезультатЗапроса.Пустой() тогда
		
		ТаблицаВозвратов = РезультатЗапроса.Выгрузить();
		// Уменьшим количество и стоимость в таблице основания
		Для каждого Строка Из ТаблицаЗаписейОснования цикл
			
			// Отберем строки
			СтруктураОтбора = Новый Структура;

			СтруктураОтбора.Вставить("Номенклатура",               Строка.Номенклатура);
			СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", Строка.ХарактеристикаНоменклатуры);
			СтруктураОтбора.Вставить("СерияНоменклатуры", 		   Строка.СерияНоменклатуры);
			СтруктураОтбора.Вставить("ДокументОприходования",	   Строка.ДокументОприходования);
						
			НайденныеСтроки = ТаблицаВозвратов.НайтиСтроки(СтруктураОтбора);
			
			// Дополнительно отберем строки с пустыми сериями 
			Если Строка.СерияНоменклатуры <> Справочники.СерииНоменклатуры.ПустаяСсылка() тогда
				
				СтруктураОтбора.Вставить("СерияНоменклатуры", Справочники.СерииНоменклатуры.ПустаяСсылка());
					
				НайденныеСтрокиСПустымиСериями = ТаблицаВозвратов.НайтиСтроки(СтруктураОтбора);
						
				Для каждого Элемент из НайденныеСтрокиСПустымиСериями Цикл
					НайденныеСтроки.Добавить(Элемент);
				КонецЦикла;
			КонецЕсли;

			Для каждого НайденнаяСтрока из НайденныеСтроки цикл
				Строка.Количество = Строка.Количество - НайденнаяСтрока.Количество;
				Строка.Стоимость  = Строка.Стоимость - НайденнаяСтрока.Стоимость;
				Строка.НДС		  = Строка.НДС - НайденнаяСтрока.НДС;
				
				НайденнаяСтрока.Количество = 0;
				НайденнаяСтрока.Стоимость = 0;
				НайденнаяСтрока.НДС = 0;
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТаблицаЗаписейОснования;
	
КонецФункции // ПодготовитьТаблицуЗаписейОснования

// Выполняет движения документа возврат товаров от покупателя
//
Процедура ВыполнитьДвиженияВозврата(СтруктураПараметров, СтрокаДокумента, Строка, ИмяРегистраПартии, 
									ВестиПУПоСкладам, ДокументПартии, КоличествоОсталосьПогасить, КоэффСписания)
	
	Движение = ДобавитьДвижениеВСтруктуруПараметров(ИмяРегистраПартии, СтруктураПараметров);
	
	КачествоНовый = Справочники.Качество.Новый;

	// Свойства
	Движение.Период      = СтрокаДокумента.Период;
	Движение.Регистратор = СтрокаДокумента.Регистратор;
	Движение.Активность = Истина;
	
	// Возврат текущего месяца может быть с неизвестной стоимостью
	// например, если возвращается продукция
	// в этом случае он учитывается как сторно расхода 
	// и обрабатывается корректировкой стоимости списания как расход
	ВозвратТекущегоМесяца = КонецМесяца(ДокументПартии.Дата) = КонецМесяца(СтруктураПараметров.Период);
	Если ВозвратТекущегоМесяца тогда
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		КоэффициентСторно    = -1;
		ТипЗаписи = "Прямая";
	Иначе	
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		КоэффициентСторно    = 1;
		ТипЗаписи = "Сторно";
	КонецЕсли;
	
	// Измерения
	Если ВестиПУПоСкладам Тогда
		
		Движение.Склад	= СтрокаДокумента.Склад;
		
		// Если задан новый склад поступления, подставляем его
		Если  ЗначениеЗаполнено(СтрокаДокумента.СкладПолучатель) Тогда
			Движение.Склад	= СтрокаДокумента.СкладПолучатель;
			
		КонецЕсли;
		
	Иначе
		
		Движение.Склад      = Неопределено;
	КонецЕсли;
	
	Движение.Номенклатура	= СтрокаДокумента.Номенклатура;
	
	Движение.СтатусПартии = Строка.СтатусПартии;
	
	Движение.ХарактеристикаНоменклатуры = СтрокаДокумента.ХарактеристикаНоменклатуры;
	
	// Если партионный учет по сериям не ведется - серии в регистре "партии товаров на складах" нет
	Если СтрокаДокумента.ВестиПартионныйУчетПоСериям тогда
		Движение.СерияНоменклатуры	= СтрокаДокумента.СерияНоменклатуры;
	КонецЕсли;
	
	Движение.ДокументОприходования	= Строка.ДокументОприходования;
	
	Движение.Качество = СтрокаДокумента.Качество;
	
	Если НЕ ЗначениеЗаполнено(Движение.Качество) Тогда
		Движение.Качество = КачествоНовый;
	КонецЕсли;
	
	// Ресурсы
	ПогашаемоеКоличество = Окр(Строка.Количество * КоэффСписания,3,1);
	ПогашаемаяСтоимость  = Окр(Строка.Стоимость * КоэффСписания,2,1);
	ПогашаемыйНДС 		 = Окр(Строка.НДС * КоэффСписания,2,1);
	Движение.Количество  = КоэффициентСторно * ПогашаемоеКоличество;
	Движение.Стоимость   = КоэффициентСторно * ПогашаемаяСтоимость;
	Движение.НДС  		 = КоэффициентСторно * ПогашаемыйНДС;
	
	// Реквизиты
	Если ВозвратТекущегоМесяца тогда
		Движение.КодОперации = Перечисления.КодыОперацийПартииТоваров.ВозвратОтПокупателяТекущийМесяц;
	Иначе
		Движение.КодОперации = СтрокаДокумента.КодОперацииПартииТоваров;
	КонецЕсли;
	
	Если ПогашаемоеКоличество < КоличествоОсталосьПогасить Тогда
		КоэффПоступления = ПогашаемоеКоличество / КоличествоОсталосьПогасить;
	Иначе
		КоэффПоступления = 1;
	КонецЕсли;
	
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		Движение.НомерСтрокиСписанныхТоваров = СтруктураПараметров.НомерСтрокиСписанныхТоваров;
		// Сумма выручки в валюте документа - для отчета комитенту
		Движение.СуммаВыручки = КоэффициентСторно*Окр(СтрокаДокумента.СуммаЗадолженности*КоэффПоступления, 2);
		// Валюты и курсы - для пересчета
		Движение.ВалютаДокумента = СтрокаДокумента.ВалютаДокумента;
		Движение.КурсДокумента = СтрокаДокумента.КурсДокумента;
		Движение.КратностьДокумента = СтрокаДокумента.КратностьДокумента;
	КонецЕсли;
	
	СтрокаДокумента.СуммаЗадолженности = СтрокаДокумента.СуммаЗадолженности - Движение.СуммаВыручки;
	
	КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - ПогашаемоеКоличество;
	
	Строка.Количество = Строка.Количество - ПогашаемоеКоличество;
	Строка.Стоимость  = Строка.Стоимость  - ПогашаемаяСтоимость;
	Строка.НДС		  = Строка.НДС		  - ПогашаемыйНДС;
	
	// Номер строки, по которой списана партия
	// Используется при возврате товаров от покупателя, ордерной схеме поступления,
	// механизмом корректировки стоимости списания 
	Движение.НомерСтрокиСписанныхТоваров = СтруктураПараметров.НомерСтрокиСписанныхТоваров;
	
	// Списание на себестоимость продаж (сторно)
	Если ПолучитьНаправлениеСписанияПоКодуОперации(СтрокаДокумента.КодОперацииПартииТоваров) = "СебестоимостьПродаж" Тогда
		
		Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
			СписаниеНаСебестоимостьПродажУпр(СтрокаДокумента, СтруктураПараметров, Движение, ТипЗаписи);
		КонецЕсли;
		
	КонецЕсли;
	
	ВыполнитьСвязанныеСоСписаниемДвижения(СтрокаДокумента, СтруктураПараметров, Движение, КоэффСписания, КоэффПоступления);
	
	СписаниеПринятыхНаОтветственноеХранение(СтрокаДокумента, СтруктураПараметров, Движение, ИмяРегистраПартии);
	
Конецпроцедуры//ВыполнитьДвиженияВозврата

Процедура СписаниеПринятыхНаОтветственноеХранение(СтрокаДокумента, СтруктураПараметров, Движение, ИмяРегистраПартии)

	// Спишем партии, находящиеся на ответственном хранении
	Если СтрокаДокумента.ОтражатьВУправленческомУчете тогда
		
		СтрУчет = "Управленческий";
		
	Иначе
		Возврат;
		
	КонецЕсли; 
	
	ТаблицаПартийПринятыхНаОтветственноеХранение = Неопределено;
	
	Если СтруктураПараметров.Свойство("ТаблицаПартийПринятыхНаОтветственноеХранение"+СтрУчет,ТаблицаПартийПринятыхНаОтветственноеХранение) Тогда
		
		СтруктураИзмерений = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры, Заказ, Качество");
		ЗаполнитьЗначенияСвойств(СтруктураИзмерений, Движение);
		СтруктураИзмерений.Вставить("СтатусПартии");
		Если Движение.СтатусПартии = Перечисления.СтатусыПартийТоваров.ВозвратнаяТара Тогда
			СтруктураИзмерений.СтатусПартии = Перечисления.СтатусыПартийТоваров.ВозвратнаяТараОтложеннаяОтгрузка;
		Иначе
			СтруктураИзмерений.СтатусПартии = Перечисления.СтатусыПартийТоваров.КупленныйОтложеннаяОтгрузка;
		КонецЕсли;
		
		МассивСтрок = ТаблицаПартийПринятыхНаОтветственноеХранение.НайтиСтроки(СтруктураИзмерений);
		
		КоличествоОсталосьПогасить = Движение.Количество;
		Для Каждого Строка Из МассивСтрок Цикл
			Если КоличествоОсталосьПогасить <=0 Тогда
				Прервать;
			КонецЕсли;
			
			Если Строка.Количество <=0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если Строка.Количество >= КоличествоОсталосьПогасить Тогда
				КоэффСписания = КоличествоОсталосьПогасить/Строка.Количество;
			Иначе
				КоэффСписания = 1;
			КонецЕсли;
			
			Движение = ДобавитьДвижениеВСтруктуруПараметров(ИмяРегистраПартии, СтруктураПараметров);

			ЗаполнитьЗначенияСвойств(Движение,Строка);
			
			Движение.Период 		= СтрокаДокумента.Период;
			Движение.Регистратор 	= СтрокаДокумента.Регистратор;
			Движение.Активность 	= Истина;
			Движение.ВидДвижения 	= ВидДвиженияНакопления.Расход;
			
			Движение.Количество	= Окр(Строка.Количество * КоэффСписания,3,1);
			Движение.Стоимость	= Окр(Строка.Стоимость  * КоэффСписания,2,1);
			Движение.НДС		= Окр(Строка.НДС  		* КоэффСписания,2,1);
			
		КонецЦикла;

	КонецЕсли; 

КонецПроцедуры // СписаниеПринятыхНаОтветственноеХраниние()

// Поступление товаров по возврату от покупателя
//
Процедура ПоступлениеВозвратОтПокупателя(СтрокаДокумента, СтруктураПараметров)
	
	ДокументПартии = СтрокаДокумента.ДокументПартии;
	
	// Возврат без указания документа партии обрабатывается в модуле документа
	Если НЕ ЗначениеЗаполнено(ДокументПартии) Тогда
		
		// Если документ - отчет о розничных продажах, то в нем не указывается исх документ продажи, а выбирается ближайший предыдущий:
		
		Если ТипЗнч(СтрокаДокумента.Регистратор) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
			
			ДокументПартии = ПредыдущийДокументРеализации(СтрокаДокумента);
			
		Иначе
			Возврат;
		КонецЕсли;

	КонецЕсли;

	ИмяРегистраПартии = Неопределено;
	СтрУчет			  = Неопределено;
	ВестиПУПоСкладам  = Неопределено;
	НаборОснование	  = Неопределено;
	
	ПолучитьПараметрыВозвратаОтПокупателя(СтрокаДокумента, СтруктураПараметров, ИмяРегистраПартии, СтрУчет, ВестиПУПоСкладам, НаборОснование);
	
	ТаблицаЗаписейОснования = ПолучитьТаблицуЗаписейОснованияВозврата (СтрокаДокумента,
																	  СтруктураПараметров,
																	  ДокументПартии,
																	  НаборОснование,
																	  СтрУчет,
																	  ИмяРегистраПартии);
	
	НайденныеСтроки = ОтобратьСтрокиПартий(ТаблицаЗаписейОснования, СтрокаДокумента, "НаСкладах");
			
	КоличествоОсталосьПогасить = СтрокаДокумента.Количество;
	Для Каждого Строка Из НайденныеСтроки Цикл
		Если КоличествоОсталосьПогасить <=0 Тогда
			Прервать;
		КонецЕсли;
			
		Если Строка.Количество <=0 Тогда
			Продолжить;
		КонецЕсли;
			
		Если Строка.Количество >= КоличествоОсталосьПогасить Тогда
			КоэффСписания = КоличествоОсталосьПогасить/Строка.Количество;
		Иначе
			КоэффСписания = 1;
		КонецЕсли;
		
		ВыполнитьДвиженияВозврата(СтруктураПараметров, СтрокаДокумента, Строка, ИмяРегистраПартии, 
									ВестиПУПоСкладам, ДокументПартии, КоличествоОсталосьПогасить, КоэффСписания);
			
	КонецЦикла;


	// Товара не хватило
	Если (КоличествоОсталосьПогасить > 0) Тогда
		
		ОбщегоНазначения.СообщитьОбОшибке(СтрУчет + " учет. Документ " + СтрокаДокумента.Регистратор + ", строка :" + СтрокаДокумента.НомерСтроки + Символы.ПС
		+ СтруктураПараметров.СтрокаСообщенияНеНайдено + КоличествоОсталосьПогасить + " " + СтрокаДокумента.Номенклатура.ЕдиницаХраненияОстатков 
		+ " товара " + СтрокаДокумента.Номенклатура
		+ ?(СтрокаДокумента.Номенклатура.ВестиУчетПоХарактеристикам, ", х-ка: " + СтрокаДокумента.ХарактеристикаНоменклатуры, "")
		+ ?(СтрокаДокумента.Номенклатура.ВестиУчетПоСериям, ", серия: " + СтрокаДокумента.СерияНоменклатуры, "")
		+ СтруктураПараметров.СтрокаСообщенияДокументПартии + ДокументПартии);
			
		СтруктураПараметров.Вставить("Отказ", Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Снятие резерва по заказам покупателей при закрытии заказов
//
Процедура СнятиеРезервовПоЗаказамПокупателей(СтрокаДокумента, СтруктураПараметров)
	
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		
		ИмяРегистраПартии = "ПартииТоваровНаСкладахУпр";
		ВестиПУПоСкладам =УчетнаяПолитика("ВестиПартионныйУчетПоСкладам", "Упр", , СтруктураПараметров);
		СтрУчет="Управленческий";
		
	КонецЕсли;
	
	// Партии для данного учета
	ДеревоПартий = СтруктураПараметров["Дерево"+ИмяРегистраПартии];
	ТаблицаОстатковПартий = СтруктураПараметров[ИмяРегистраПартии + "ТаблицаОстатков"];
	СтрокаДереваПартий = ДеревоПартий.Строки.Найти(СтрокаДокумента.НомерСтрокиДокумента, "НомерСтрокиДокумента");
	СтруктураИзмерений = СтруктураПараметров[ИмяРегистраПартии + "СтруктураИзмерений"];
	
	Если СтрокаДереваПартий <> Неопределено Тогда
		
		Для Каждого СтрокаПартииРаспределения ИЗ СтрокаДереваПартий.Строки Цикл
			
			СтрокаПартии = ПолучитьСтрокуОстатковПартий(СтрокаПартииРаспределения, СтруктураИзмерений, ТаблицаОстатковПартий);
			
			// 1. Списание
			Движение = ДобавитьДвижениеВСтруктуруПараметров(ИмяРегистраПартии, СтруктураПараметров);
			
			ЗаполнитьЗначенияСвойств(Движение,СтрокаПартии);
			
			// Свойства
			Движение.Период			= СтрокаДокумента.Период;
			Движение.Регистратор	= СтрокаДокумента.Регистратор;
			Движение.Активность		= Истина;
			Движение.ВидДвижения	= ВидДвиженияНакопления.Расход;
			
			// Реквизиты
			// Специфические разрезы для разных видов учета
			Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
				Движение.Подразделение = СтрокаДокумента.Подразделение;
			КонецЕсли;
			
			Движение.КодОперации	= СтрокаДокумента.КодОперацииПартииТоваров;
			
			СтрокаПартии.Количество = 0;
			СтрокаПартии.Стоимость  = 0;
			СтрокаПартии.НДС  		= 0;
			
			Строка   = Движение;
			
			// 2. Поступление
			Движение = ДобавитьДвижениеВСтруктуруПараметров(ИмяРегистраПартии, СтруктураПараметров);
			
			ЗаполнитьЗначенияСвойств(Движение,Строка);
			
			// Свойства
			Движение.ВидДвижения  = ВидДвиженияНакопления.Приход;
			
			// Измерения
			Движение.Заказ		  = Неопределено;
			
			Строка.НомерКорСтроки = СтруктураПараметров["ТекНомерСтроки"+ИмяРегистраПартии];
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // СнятиеРезервовПоЗаказамПокупателей()

Функция ПолучитьТаблицуСписанияРасходнымОрдером(СтрокаДокумента, СтруктураПараметров) Экспорт

	ТаблицыСписанияРасходнымОрдером = Неопределено;
	ТаблицаСписанияРасходнымОрдером = Неопределено;
	
	// Отборы по видам учета
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		СтрУчет = "Упр";
	Иначе
		// Строка документа не отражается ни в одном учете
		Возврат Неопределено;
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("ТаблицыСписанияРасходнымОрдером",ТаблицыСписанияРасходнымОрдером) тогда
		ТаблицаСписанияРасходнымОрдером = ТаблицыСписанияРасходнымОрдером.Получить(СтрУчет);
		Если ТаблицаСписанияРасходнымОрдером <> Неопределено тогда
			Возврат ТаблицаСписанияРасходнымОрдером;
		КонецЕсли;
	КонецЕсли;
	
	// Сначала получим таблицу исходного документа
	Запрос = Новый Запрос;

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	""СписаниеПартийРасходнымОрдером1"" КАК QuieryId ";
	
	Ресурсы = Метаданные.РегистрыСведений.СписанныеТовары.Ресурсы;
	
	Для каждого Ресурс Из Ресурсы Цикл
		
		Запрос.Текст = 	Запрос.Текст + ",
		|	"+ Ресурс.Имя;

	КонецЦикла;
	
	Запрос.Текст = 	Запрос.Текст + ",
	|	Номенклатура.ВестиПартионныйУчетПоСериям КАК ВестиПартионныйУчетПоСериям,
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	Период,
	|	НомерСтроки
	|ИЗ
	|	РегистрСведений.СписанныеТовары КАК Строки
	|
	|ГДЕ
	|	Регистратор=&Регистратор
	|";
	
	// Отборы по видам учета
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		Запрос.Текст = 	Запрос.Текст + " И (ОтражатьВУправленческомУчете)";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Регистратор", СтрокаДокумента.ОсновнойДокумент);
	Запрос.УстановитьПараметр("Номенклатура", СтрокаДокумента.Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", СтрокаДокумента.ХарактеристикаНоменклатуры);
	
	ТаблицаСписанияРасходнымОрдером=Запрос.Выполнить().Выгрузить();
    	
	ТаблицаСписанияРасходнымОрдером.ЗаполнитьЗначения(СтрокаДокумента.ОтражатьВУправленческомУчете,"ОтражатьВУправленческомУчете");
	
	СтруктураПараметров.Вставить("ОсновнойДокумент",СтрокаДокумента.ОсновнойДокумент);
	
	// Если записей по исх. документу нет
	Если ТаблицаСписанияРасходнымОрдером.Количество()=0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	""СписаниеПартийРасходнымОрдером2"" КАК QuieryId,
	|	Строки.Регистратор,
	|	Строки.Период,
	|	СУММА(Строки.Количество) КАК Количество,
	|	Строки.Номенклатура,
	|	Строки.СерияНоменклатуры,
	|	Строки.ХарактеристикаНоменклатуры,
	|	Строки.Склад
	|ИЗ
	|	РегистрСведений.СписанныеТовары КАК Строки
	|ГДЕ
	|	Строки.Период МЕЖДУ &ДатаНач И &ДатаКон
	|	И Строки.ОсновнойДокумент = &ОсновнойДокумент
	|	И Строки.Регистратор <> &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.Регистратор,
	|	Строки.Период,
	|	Строки.НомерСтроки,
	|	Строки.Номенклатура,
	|	Строки.СерияНоменклатуры,
	|	Строки.ХарактеристикаНоменклатуры,
	|	Строки.Склад";
	
	// Отборы по видам учета
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		Запрос.Текст = 	СтрЗаменить(Запрос.Текст, "ГДЕ", "ГДЕ (ОтражатьВУправленческомУчете) И ");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаНач", ТаблицаСписанияРасходнымОрдером[0].Период);
	Запрос.УстановитьПараметр("ДатаКон", СтрокаДокумента.Период);
	Запрос.УстановитьПараметр("ОсновнойДокумент", СтрокаДокумента.ОсновнойДокумент);
	Запрос.УстановитьПараметр("Регистратор", СтрокаДокумента.Регистратор);
	
	ТаблицаОрдеров = Запрос.Выполнить().Выгрузить();
	
	ТаблицаСписанияРасходнымОрдером.ЗаполнитьЗначения(СтрокаДокумента.Период, "Период");
	
	// Пересчет сумм, уже списанных другими расходными ордерами
	Для каждого СтрокаСписания Из ТаблицаСписанияРасходнымОрдером Цикл
		
		СтруктураПоиска = Новый Структура("Номенклатура, СерияНоменклатуры, ХарактеристикаНоменклатуры, Склад",
										СтрокаСписания.Номенклатура,
										СтрокаСписания.СерияНоменклатуры,
										СтрокаСписания.ХарактеристикаНоменклатуры,
										СтрокаСписания.Склад
										);
										
		МассивСтрокОрдеров = ТаблицаОрдеров.НайтиСтроки(СтруктураПоиска);
			
		// Серия может быть указана в документе реализации и в ордере или только в ордере
		Если СтрокаДокумента.ВестиПартионныйУчетПоСериям И ЗначениеЗаполнено(СтрокаДокумента.СерияНоменклатуры)Тогда
				
			СтруктураПоиска.Вставить("СерияНоменклатуры", Справочники.СерииНоменклатуры.ПустаяСсылка());
			МассивСтрокСПустойСерией = ТаблицаОрдеров.НайтиСтроки(СтруктураПоиска);
				
			Для каждого Элемент Из МассивСтрокСПустойСерией Цикл
				
				МассивСтрокОрдеров.Добавить(Элемент);
				
			КонецЦикла;
			
		КонецЕсли;
		
		Для каждого СтрокаОрдера Из МассивСтрокОрдеров Цикл
			
			Если СтрокаОрдера.Количество <=0  Тогда
			
				Продолжить;
			
			КонецЕсли; 
			
			// В накладной и в ордере один и тот же товар может быть в разных строках
			Если СтрокаСписания.Количество > СтрокаОрдера.Количество Тогда
				// полностью списали количество по строке ордера
				КоэффСписания = СтрокаОрдера.Количество/СтрокаСписания.Количество;
				КоэффСписанияОрдера = 1;
			Иначе
				// полностью списали количество по строке накладной
				КоэффСписанияОрдера = СтрокаСписания.Количество/СтрокаОрдера.Количество;
				КоэффСписания = 1;
			КонецЕсли;
			
			КоличествоСписанияОрдера = Окр(КоэффСписанияОрдера * СтрокаОрдера.Количество,3);
			
			СтрокаСписания.Количество = СтрокаСписания.Количество - КоличествоСписанияОрдера;
			СтрокаОрдера.Количество   = СтрокаОрдера.Количество - КоличествоСписанияОрдера;
	
			СтрокаСписания.СуммаЗадолженности       = СтрокаСписания.СуммаЗадолженности -Окр(СтрокаСписания.СуммаЗадолженности *КоэффСписания,2);
			СтрокаСписания.СтоимостьПоступление   = СтрокаСписания.СтоимостьПоступление - Окр(КоэффСписания * СтрокаСписания.СтоимостьПоступление, 2);
					
		КонецЦикла;
	
	КонецЦикла;
	
	Если НЕ СтруктураПараметров.Свойство("ТаблицыСписанияРасходнымОрдером",ТаблицыСписанияРасходнымОрдером) тогда
		ТаблицыСписанияРасходнымОрдером = Новый Соответствие;
	КонецЕсли;
	
	ТаблицыСписанияРасходнымОрдером.Вставить(СтрУчет,ТаблицаСписанияРасходнымОрдером);
	
	СтруктураПараметров.Вставить("ТаблицыСписанияРасходнымОрдером",ТаблицыСписанияРасходнымОрдером);
	
	Возврат ТаблицаСписанияРасходнымОрдером;

КонецФункции // ПолучитьТаблицуСписанияРасходнымОрдером()

// Списание партий ордерного склада
// Обслуживаются документы:
// 1.Расходный ордер на товары
// 2 Приходный ордер на товары с видом операции "перемещение"
// 3 Поступление товаров и услуг в НТТ с видом операции "перемещение"
// Параметры СтрокаДокумента - 
//			 СтруктураПараметров - структура хранящая основные переменные
Процедура СписаниеПартийОрдерныйСклад(СтрокаДокумента, СтруктураПараметров)
	
	// Отложенная отгрузка отражается только в бухгалтерском и управленческом учете
	Если СтруктураПараметров.ТипЗначенияРегистратора = Тип("ДокументСсылка.РасходныйОрдерНаТовары")И
	   НЕ СтруктураПараметров.СписыватьПартииРасходнымОрдером И
	   НЕ (СтрокаДокумента.ОтражатьВУправленческомУчете ИЛИ СтрокаДокумента.ОтражатьВБухгалтерскомУчете) Тогда
	   
	   Возврат;
	КонецЕсли;
	
	ТаблицаСписанияРасходнымОрдером = ПолучитьТаблицуСписанияРасходнымОрдером(СтрокаДокумента, СтруктураПараметров);
	
	// Если записей по исх. документу нет
	Если ТаблицаСписанияРасходнымОрдером = Неопределено Или ТаблицаСписанияРасходнымОрдером.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("Номенклатура, СерияНоменклатуры, ХарактеристикаНоменклатуры",
									СтрокаДокумента.Номенклатура,
									СтрокаДокумента.СерияНоменклатуры,
									СтрокаДокумента.ХарактеристикаНоменклатуры
									);
									
	// В расходном ордере склад должен совпадать со складом в табличной части основного документа
	Если СтруктураПараметров.ТипЗначенияРегистратора = Тип("ДокументСсылка.РасходныйОрдерНаТовары") тогда

		СтруктураПоиска.Вставить("Склад", СтрокаДокумента.Склад);

	КонецЕсли;

	МассивСтрокСписания = ТаблицаСписанияРасходнымОрдером.НайтиСтроки(СтруктураПоиска);
	
	// Серия может быть указана в документе реализации или в ордере
	Если СтрокаДокумента.ВестиПартионныйУчетПоСериям И ЗначениеЗаполнено(СтрокаДокумента.СерияНоменклатуры)Тогда
		
		СтруктураПоиска.Вставить("СерияНоменклатуры", Справочники.СерииНоменклатуры.ПустаяСсылка());
		МассивСтрокСПустойСерией = ТаблицаСписанияРасходнымОрдером.НайтиСтроки(СтруктураПоиска);
		
		Для каждого Элемент Из МассивСтрокСПустойСерией Цикл
		
			МассивСтрокСписания.Добавить(Элемент);
		
		КонецЦикла;
	
	КонецЕсли;
	
	КоличествоОсталосьПогасить = СтрокаДокумента.Количество;
	
	Для Каждого Строка Из МассивСтрокСписания Цикл
		
		Если КоличествоОсталосьПогасить <=0 Тогда
			Прервать;
		КонецЕсли;
		
		Если Строка.Количество <=0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Строка.Количество >= КоличествоОсталосьПогасить Тогда
			КоэффСписания = КоличествоОсталосьПогасить/Строка.Количество;
		Иначе
			КоэффСписания = 1;
		КонецЕсли;

		// Заполнение параметров
		// Список свойств, не заполняющихся в строке ордера по строке реализации
		СписокИсключаемыхСвойств = "СерияНоменклатуры, Количество, ОсновнойДокумент, ВидТабличнойЧасти,
									  |НомерСтрокиДокумента, Период, Регистратор";

		Если СтруктураПараметров.ТипЗначенияРегистратора = Тип("ДокументСсылка.РасходныйОрдерНаТовары") тогда
			СписокИсключаемыхСвойств = СписокИсключаемыхСвойств +",Склад";
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаДокумента, Строка,,СписокИсключаемыхСвойств);
		
		ПогашаемоеКоличество = Окр(КоэффСписания * Строка.Количество, 3);
		
		СтрокаДокумента.СуммаЗадолженности     = Окр(КоэффСписания * Строка.СуммаЗадолженности, 2);
		СтрокаДокумента.СтоимостьПоступление   = Окр(КоэффСписания * Строка.СтоимостьПоступление, 2);
		СтрокаДокумента.Количество = ПогашаемоеКоличество;
		
		Если НЕ СтруктураПараметров.СписыватьПартииРасходнымОрдером 
		   И ( СтрокаДокумента.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.Реализация Или
				СтрокаДокумента.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.ПередачаТарыКонтрагенту) Тогда
			
			СтрокаДокумента.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.ОтложеннаяОтгрузка;
			
			Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
				
				Для СчетчикСтатуса =1 По 4 Цикл
					Если СтрокаДокумента["ДопустимыйСтатус"+СчетчикСтатуса] = Перечисления.СтатусыПартийТоваров.ВозвратнаяТара Тогда
						СтрокаДокумента["ДопустимыйСтатус"+СчетчикСтатуса] = Перечисления.СтатусыПартийТоваров.ВозвратнаяТараОтложеннаяОтгрузка;
					Иначе
						СтрокаДокумента["ДопустимыйСтатус"+СчетчикСтатуса] = Перечисления.СтатусыПартийТоваров.КупленныйОтложеннаяОтгрузка;
					КонецЕсли; 
				КонецЦикла; 
				
			КонецЕсли; 
		КонецЕсли; 
		
		
		Если     СтрокаДокумента.СкладПолучатель.СуммовойУчет 
			И НЕ СтрокаДокумента.ВидТабличнойЧасти = Перечисления.ВидыТабличныхЧастей.БланкиСтрогогоУчета Тогда
			ПеремещениеВСНТТ(СтрокаДокумента, СтруктураПараметров, "НаСкладах");
		Иначе
			СписаниеПартий(СтрокаДокумента, СтруктураПараметров, "НаСкладах");
		КонецЕсли;
		
		КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - ПогашаемоеКоличество;
		
		Строка.Количество             = Строка.Количество - ПогашаемоеКоличество;
		Строка.СуммаЗадолженности     = Строка.СуммаЗадолженности - Окр(КоэффСписания * Строка.СуммаЗадолженности, 2);
		Строка.СтоимостьПоступление   = Строка.СтоимостьПоступление - Окр(КоэффСписания * Строка.СтоимостьПоступление, 2);
		
	КонецЦикла;
	
	// Товара не хватило
	Если (КоличествоОсталосьПогасить > 0) Тогда
		СтрУчет = "Упр";
		
		ОбщегоНазначения.СообщитьОбОшибке(СтрУчет + " учет. Документ " + СтрокаДокумента.Регистратор + Символы.ПС
		+ "Не найдено в документе списания "+ СтрокаДокумента.ОсновнойДокумент+ " "+ КоличествоОсталосьПогасить + " " + СтрокаДокумента.Номенклатура.ЕдиницаХраненияОстатков 
		+ " товара " + СтрокаДокумента.Номенклатура
		+ ?(СтрокаДокумента.Номенклатура.ВестиУчетПоХарактеристикам, ", х-ка: " + СтрокаДокумента.ХарактеристикаНоменклатуры, "")
		+ ?(СтрокаДокумента.Номенклатура.ВестиУчетПоСериям, ", серия: " + СтрокаДокумента.СерияНоменклатуры, ""));
			
		СтруктураПараметров.Вставить("Отказ", Истина);
		
	КонецЕсли;
	
	// Теперь отметим все выполненные движения как принадлежащие документу реализации:
	СтруктураРеквизитовДокумента = Новый Структура("Дата");
	ПолучитьРеквизитыОбъекта(СтрокаДокумента.ОсновнойДокумент, СтруктураРеквизитовДокумента);
		
	Для Каждого Элемент Из СтруктураПараметров Цикл
				
		// В каждой таблице движений заполним реквизиты ДокументДвижения и ДокументДвиженияПериод
		Если Лев(Элемент.Ключ,15) = "ТаблицаДвижений" Тогда
			Если ТипЗнч(Элемент.Значение) = Тип("ТаблицаЗначений") Тогда
				Если Элемент.Значение.Колонки.Найти("ДокументДвижения")<>Неопределено Тогда
					Элемент.Значение.ЗаполнитьЗначения(СтрокаДокумента.ОсновнойДокумент, "ДокументДвижения");
				КонецЕсли;
				Если Элемент.Значение.Колонки.Найти("ДокументДвиженияПериод")<>Неопределено Тогда
					Элемент.Значение.ЗаполнитьЗначения(СтруктураРеквизитовДокумента.Дата, "ДокументДвиженияПериод");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
				
	КонецЦикла;

КонецПроцедуры // СписаниеПартийОрдерныйСклад()

// Общие

// Возвращает таблицу документов
//
// Параметры
//ДокументСписания 
//Упр 
//Бух 
//Нал 
// Возвращаемое значение:
//   Таблица строк документа
//
Функция ПолучитьТаблицуСтрокДокументов(ДокументСписания=Неопределено) Экспорт
	
	// Общая часть запроса
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСписания", ДокументСписания);
		
	Запрос.УстановитьПараметр("ВозвратОтКомиссионера", Перечисления.КодыОперацийПартииТоваров.ВозвратОтКомиссионера);
	Запрос.УстановитьПараметр("РеализацияКомиссия", Перечисления.КодыОперацийПартииТоваров.РеализацияКомиссия);
	

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	""ПолучитьТаблицуСтрокДокументов"" КАК QuieryId ";
	
	Ресурсы = Метаданные.РегистрыСведений.СписанныеТовары.Ресурсы;
	
	Для каждого Ресурс Из Ресурсы Цикл
		
		Запрос.Текст = 	Запрос.Текст + ",
		|	"+ Ресурс.Имя;
		
	КонецЦикла; 
	
	Запрос.Текст = 	Запрос.Текст + ",
	|	Регистратор,
	|	Период,
	|	НомерСтроки,
	|	НомерСтрокиДокумента,
	|	Номенклатура.ВестиПартионныйУчетПоСериям КАК ВестиПартионныйУчетПоСериям,
	|	ВЫБОР
	|	КОГДА
	|	КодОперацииПартииТоваров = &ВозвратОтКомиссионера
	|	ИЛИ КодОперацииПартииТоваров = &РеализацияКомиссия
	|	ТОГДА ""Переданные""
	|	ИНАЧЕ
	|	""НаСкладах""
	|	КОНЕЦ КАК ИсточникПоКодуОперации
	|ИЗ
	|	РегистрСведений.СписанныеТовары КАК Строки";
	
	Если ДокументСписания <> Неопределено Тогда
	
		Запрос.Текст = Запрос.Текст+ "
		|ГДЕ
		|	Строки.Регистратор = &ДокументСписания";
	
	КонецЕсли; 
	
	
	Результат = Запрос.Выполнить();

	Возврат Результат.Выгрузить();

КонецФункции//ПолучитьТаблицуСтрокДокументов

// Добавление остатков по партионному и остальным видам учетов
//
// Параметры:
//	Нет.
//
Процедура ПолучитьОстатки(СтруктураПараметров, ТаблицаСписания, МоментКон)
	
	ПолучитьОстаткиУпр(СтруктураПараметров, ТаблицаСписания, МоментКон)
	
КонецПроцедуры // ПолучитьОстаткиОстальные()

/////////////////////////////////////////////////////////////////////////////////
// СПИСАНИЕ ПАРТИЙ ТОВАРОВ ПО ЗАДАННОЙ ТАБЛИЦЕ СПИСАНИЯ

// Дополнительные движения по результату списания партий
//
// Параметры:
//	Нет.
//
Процедура ВыполнитьДопДвиженияПоРезультатуСписания(СтруктураПараметров, ТаблицаСписания)

	ВыполнитьДопДвиженияПоРезультатуСписанияУпр(СтруктураПараметров, ТаблицаСписания);
	
КонецПроцедуры // ВыполнитьДопДвиженияПоРезультатуСписания()

// Запускает формирование движений по результату списания партий в упр учете
//
// Параметры:
//	Нет.
//
Процедура ВыполнитьДопДвиженияПоРезультатуСписанияУпр(СтруктураПараметров, ТаблицаСписания)
	
	Если СтруктураПараметров.ЕстьСтрокиОтражатьВУправленческомУчете Тогда
		
		// Запись данных о реализованных товаррах
		ВыполнитьДвиженияПоРеализованнымТоварамКомитента(СтруктураПараметров, ТаблицаСписания);
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьДопДвиженияПоРезультатуСписанияУпр()

// СтруктураПараметров - основная переменная модуля, передается в бОльшую часть процедур и функций,
// содержит бОльшую часть параметров, нужных для проведения по партиям
Процедура ДополнитьСтруктуруПараметров(СтруктураПараметров, ТаблицаСписания)
	
	СтруктураПараметров.Вставить("СпособОценкиМПЗУпр", Строка(УчетнаяПолитика("СпособОценкиМПЗ", "Упр", , СтруктураПараметров)));		
	СтруктураПараметров.Вставить("СтратегияСтатусПартииУпр",  УчетнаяПолитика("СтратегияСписанияПоСтатусам", "Упр", , СтруктураПараметров));
	СтруктураПараметров.Вставить("ВестиПартионныйУчетПоСкладамУпр", УчетнаяПолитика("ВестиПартионныйУчетПоСкладам", "Упр", , СтруктураПараметров));
	
	СтруктураПараметров.Вставить("СписыватьПартииРасходнымОрдером",Константы.СписыватьПартииРасходнымОрдером.Получить());
	СтруктураПараметров.Вставить("ИспользоватьУказаниеСерийНоменклатурыПриРезервировании",Константы.ИспользоватьУказаниеСерийНоменклатурыПриРезервировании.Получить());

КонецПроцедуры//ДополнитьСтруктуруПараметров(СтруктураПараметров)

Процедура ВыполнитьСписание(СтруктураПараметров, ТаблицаСписания, МоментКон,  Останавливаться = Ложь, ПроведениеОстановлено = Ложь, ОтражатьВУправленческомУчете, ОтражатьВБухгалтерскомУчете, Отказ, Партия, Содержание) Экспорт
	
	Если ТаблицаСписания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если ОтражатьВУправленческомУчете Тогда
				
		ДополнитьСтруктуруПараметров(СтруктураПараметров, ТаблицаСписания);
		
		СтруктураПараметров.Вставить("КодыОпераций",Перечисления.КодыОперацийПартииТоваров);
		
		Если ЗначениеЗаполнено(ТаблицаСписания[0].ОсновнойДокумент) Тогда
			
			СтруктураПараметров.Вставить("ОсновнойДокумент",ТаблицаСписания[0].ОсновнойДокумент);
			
		КонецЕсли;
		
		СтруктураПараметров.Вставить("ЕстьСтрокиОтражатьВУправленческомУчете", Истина);
		
		// Подготовка наборов записей
		ПодготовитьНаборыЗаписей(СтруктураПараметров, ТаблицаСписания, ТаблицаСписания[0].Период, ТаблицаСписания[0].Регистратор, Истина);
		
		СтруктураПараметров.Вставить("ТипЗначенияРегистратора", ТипЗнч(ТаблицаСписания[0].Регистратор));
		
		// Для закрытия заказов покупателей дерево остатков партий должно получаться особым способом
		Если СтруктураПараметров.ТипЗначенияРегистратора =Тип("ДокументСсылка.ЗакрытиеЗаказовПокупателей") тогда
			СтруктураПараметров.Вставить("ЗакрытиеЗаказовПокупателей");
		КонецЕсли;
		
		// В некоторых случаях получать остатки не нужно
		ТребуетсяПолучитьОстатки = Истина;
		
		// Ордерная схема поступления
		Если СтруктураПараметров.ТипЗначенияРегистратора =Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
		   Или СтруктураПараметров.ТипЗначенияРегистратора =Тип("ДокументСсылка.АвансовыйОтчет")  тогда
			
			ТребуетсяПолучитьОстатки = Ложь;
			
		// Возврат товаров от покупателя (возврат от комиссионера обрабатывается основным алгоритмом списания
		// фактически являясь частным случаем перемещения товаров)
		ИначеЕсли СтруктураПараметров.ТипЗначенияРегистратора =Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
			
			ТребуетсяПолучитьОстатки = НЕ ТаблицаСписания[0].КодОперацииПартииТоваров = СтруктураПараметров.КодыОпераций.ВозвратОтПокупателя;
		
		КонецЕсли;

		Если ТребуетсяПолучитьОстатки Тогда
		
			// Добавление в структуру остатков по партионному учету
			ПолучитьОстатки(СтруктураПараметров, ТаблицаСписания, МоментКон);
		
		КонецЕсли;
		
		ОбрабатываемыйДокумент = Неопределено;
		
		СтрокаДокумента = Новый Структура;
		Для Каждого Колонка ИЗ ТаблицаСписания.Колонки Цикл
			СтрокаДокумента.Вставить(Колонка.Имя);
		КонецЦикла;
		
		Для Каждого СтрокаТаблицыСписания Из ТаблицаСписания Цикл
			
			ОбрабатываемыйДокумент = СтрокаТаблицыСписания.Регистратор;
			
			// Строка не обрабатывается при списании
			Если НЕ СтруктураПараметров.СписыватьПартииРасходнымОрдером И 
				( СтрокаТаблицыСписания.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.Реализация Или
					СтрокаТаблицыСписания.КодОперацииПартииТоваров = Перечисления.КодыОперацийПартииТоваров.ПередачаТарыКонтрагенту)
			   Тогда
				// Для документа "Реализация товаров и услуг" реализована возможность списывать партии при 
				// проведении документа, даже если вид передачи = "по ордеру"
				// В этом случае партия списывается за баланс и окончательное списание осуществляется ордером
				НеСписывать = Ложь;
			Иначе
				НеСписывать = СтрокаТаблицыСписания.НеСписывать;
			КонецЕсли;
			
			Если НеСписывать Тогда
				Продолжить;
			КонецЕсли;
			
			// Номер строки, по которой списана партия
			// Используется при возврате товаров от покупателя, ордерной схеме поступления,
			// механизмом корректировки стоимости списания 
			СтруктураПараметров.Вставить("НомерСтрокиСписанныхТоваров",СтрокаТаблицыСписания.НомерСтроки);
			
			ЗаполнитьЗначенияСвойств(СтрокаДокумента, СтрокаТаблицыСписания);
			
			// Специфические случаи списания :
			
			// 1. Возврат от покупателя
			Если СтрокаДокумента.КодОперацииПартииТоваров = СтруктураПараметров.КодыОпераций.ВозвратОтПокупателя Тогда
				
				ПоступлениеВозвратОтПокупателя(СтрокаДокумента, СтруктураПараметров);
				
				// 2. Поступление товаров по ордеру: корректировка предыдущих списаний
			ИначеЕсли  ЗначениеЗаполнено(СтрокаДокумента.ПоступлениеПриходныйОрдер) Тогда
				
				Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
					ПоступлениеТоваровПоОрдеруУпр(СтрокаДокумента, СтруктураПараметров);
				КонецЕсли;
				
				// 3. Закрытие заказов
			ИначеЕсли ТипЗнч(СтрокаДокумента.Регистратор)=Тип("ДокументСсылка.ЗакрытиеЗаказовПокупателей") Тогда
				
				СнятиеРезервовПоЗаказамПокупателей(СтрокаДокумента, СтруктураПараметров);
				
			ИначеЕсли (ТипЗнч(СтрокаДокумента.Регистратор)=Тип("ДокументСсылка.ПеремещениеТоваров")
				     И СтрокаДокумента.СкладПолучатель.СуммовойУчет) 
					 И НЕ СтрокаДокумента.ВидТабличнойЧасти = Перечисления.ВидыТабличныхЧастей.БланкиСтрогогоУчета Тогда
				
				// 4. Перемещение в НТТ с суммовым учетом
				ПеремещениеВСНТТ(СтрокаДокумента, СтруктураПараметров, ПолучитьИсточникПоКодуОперации(СтрокаДокумента.КодОперацииПартииТоваров));
				
				// 5. Документ, в котором указываются данные, которых нет в документе списания:
				// - Списание партий по расходному ордеру
				// - Перемещение партий по приходному ордеру
			ИначеЕсли  ЗначениеЗаполнено(СтрокаДокумента.ОсновнойДокумент) Тогда
				
				СписаниеПартийОрдерныйСклад(СтрокаДокумента, СтруктураПараметров);
				
				// Общий случай списания
			Иначе
				
				// Сначала обрабатывается списание
				СписаниеПартий(СтрокаДокумента, СтруктураПараметров, СтрокаДокумента.ИсточникПоКодуОперации);
				
			КонецЕсли;
			
		КонецЦикла; 
		
		Если СтруктураПараметров.Свойство("ТаблицыСписанияРасходнымОрдером") тогда
			СтруктураПараметров.Удалить("ТаблицыСписанияРасходнымОрдером");
		КонецЕсли;
		
		Если Не ОбрабатываемыйДокумент = Неопределено Тогда
			
			// Дополнительные движения по результату списания документом определенных партий
			ВыполнитьДопДвиженияПоРезультатуСписания(СтруктураПараметров, ТаблицаСписания);
			
			// Если задан параметр останавливаться при нехватке партий и партий не хватило - прекратим проведение
			
			Если Останавливаться И СтруктураПараметров.Отказ тогда
				
				ОбщегоНазначения.СообщитьОбОшибке("Проведение по партиям остановлено ",СтруктураПараметров.Отказ);
				ПроведениеОстановлено = Истина;
				Возврат;
				
			КонецЕсли;
			
			ЗаписатьДвиженияДокумента(СтруктураПараметров, ТаблицаСписания, Истина);
			
			#Если Клиент Тогда
				Состояние("Проведен документ " + ОбрабатываемыйДокумент);
			#КонецЕсли
		КонецЕсли;
		
	Иначе
		
		Бух = ОтражатьВБухгалтерскомУчете;
		
		СтруктураПараметров.Вставить("МоментКон", МоментКон);
		СтруктураПараметров.Вставить("Организация", ТаблицаСписания[0].Организация);                           	
		СтруктураПараметров.Вставить("УчетнаяПолитика", ПолучитьУчетнуюПолитику(СтруктураПараметров.МоментКон, СтруктураПараметров.Организация));	
		СтруктураПараметров.Вставить("ПроводитьДокументПоРазделуУчета", ОбщегоНазначения.ПроводитьДокументПоРазделуУчета(СтруктураПараметров.Организация, Перечисления.РазделыУчета.ОценкаМПЗ, СтруктураПараметров.МоментКон.Дата));
		ВестиУчетПоДопРазрезамРегл  = ?(ПланыСчетов.Хозрасчетный.ТоварыНаСкладе.ПолучитьОбъект().ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеПозиции, "ВидСубконто") = Неопределено, Ложь, Истина);
		СтруктураПараметров.Вставить("ВестиУчетПоДопРазрезамРегл", ВестиУчетПоДопРазрезамРегл);
		
		СтруктураПараметров.Вставить("СпособОценкиБух", УчетнаяПолитика("СпособОценкиМПЗ", "Бух", СтруктураПараметров.Организация, СтруктураПараметров));
		СтруктураПараметров.Вставить("ЕстьНалогНаПрибыльНал", УчетнаяПолитика("ЕстьНалогНаПрибыль", "Нал", СтруктураПараметров.Организация, СтруктураПараметров));
		
		// Сформируем структура отбора данных при формировании запроса по номенклатуре
		МассивНоменклатуры = ТаблицаСписания.ВыгрузитьКолонку("Номенклатура");
		ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивНоменклатуры);

		МассивСчетовУчетаБУ = ТаблицаСписания.ВыгрузитьКолонку("СчетУчетаБУ");
		ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСчетовУчетаБУ);
		
		МассивСкладов = ТаблицаСписания.ВыгрузитьКолонку("Склад");
		ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСкладов);

		МассивОрганизаций = ТаблицаСписания.ВыгрузитьКолонку("Организация");
		ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивОрганизаций);
		
		Если ТаблицаСписания.Колонки.Найти("НалоговоеНазначение") <> Неопределено Тогда
			МассивНалоговыхНазначений = ТаблицаСписания.ВыгрузитьКолонку("НалоговоеНазначение");
			ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивНалоговыхНазначений);
		Иначе
			МассивНалоговыхНазначений = Новый Массив;
		КонецЕсли;

		ДоговорКонтрагент = ТаблицаСписания[0].ДоговорКонтрагента;
		Если НЕ ЗначениеЗаполнено(ДоговорКонтрагент) Тогда
			Контрагент = Неопределено;
			СтрокаТаблицыСписания = ТаблицаСписания[0];
			Для Индекс = 1 По 4 Цикл
				Если НЕ ТаблицаСписания.Колонки.Найти("КорСубконтоСписанияБУ" + Индекс) = Неопределено Тогда
					Если ТипЗнч(СтрокаТаблицыСписания[("КорСубконтоСписанияБУ" + Индекс)]) = Тип("СправочникСсылка.Контрагенты") 
						И ЗначениеЗаполнено(СтрокаТаблицыСписания[("КорСубконтоСписанияБУ" + Индекс)]) Тогда
						Контрагент = СтрокаТаблицыСписания[("КорСубконтоСписанияБУ" + Индекс)];
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Контрагент = ДоговорКонтрагент.Владелец;
		КонецЕсли; 

		Если глЗначениеПеременной("ИспользоватьУправляемыеБлокировки") Тогда
			
			ТаблицаСписания.Индексы.Добавить("СчетУчетаБУ");
			ТаблицаСчетов = ТаблицаСписания.Скопировать(, "СчетУчетаБУ");
			ТаблицаСчетов.Свернуть("СчетУчетаБУ");

			Для Каждого СтрокаСчета Из ТаблицаСчетов Цикл
				
				ОтборПоСчету = Новый Структура("СчетУчетаБУ", СтрокаСчета.СчетУчетаБУ);
				ТаблицаСписанияПоСчету = ТаблицаСписания.Скопировать(ОтборПоСчету, "Номенклатура,Склад,Организация,ДокументОприходования");
					
				СтруктураПараметровБлокировки = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрБухгалтерии", "Хозрасчетный");
				СтруктураПараметровБлокировки.Вставить("ИсточникДанных", ТаблицаСписанияПоСчету);
				
				ЗначенияБлокировки = Новый Соответствие;
				ЗначенияБлокировки.Вставить("Период", Новый Диапазон(, СтруктураПараметров.МоментКон.Дата)); 
				ЗначенияБлокировки.Вставить("Счет", СтрокаСчета.СчетУчетаБУ);
								
				СведенияОСчетах = Новый Соответствие;
				СведенияОСчете = ОбщегоНазначения.ПолучитьСведенияОСчете(СведенияОСчетах, СтрокаСчета.СчетУчетаБУ);
					
				ВидСубконтоНоменклатура = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура;
				ВидСубконтоСклады = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады;
				ВидСубконтоКонтрагенты = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты;
				ВидСубконтоДоговоры = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры;
				ВидСубконтоПартии = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии;
				
				ЕстьНоменклатураНаСчете = (СведенияОСчете.ВидСубконто1 = ВидСубконтоНоменклатура)
					ИЛИ (СведенияОСчете.ВидСубконто2 = ВидСубконтоНоменклатура)
					ИЛИ (СведенияОСчете.ВидСубконто3 = ВидСубконтоНоменклатура);
					
				ЕстьСкладыНаСчете = (СведенияОСчете.ВидСубконто1 = ВидСубконтоСклады)
					ИЛИ (СведенияОСчете.ВидСубконто2 = ВидСубконтоСклады)
					ИЛИ (СведенияОСчете.ВидСубконто3 = ВидСубконтоСклады);
					
				ЕстьКонтрагентыНаСчете = (СведенияОСчете.ВидСубконто1 = ВидСубконтоКонтрагенты)
					ИЛИ (СведенияОСчете.ВидСубконто2 = ВидСубконтоКонтрагенты)
					ИЛИ (СведенияОСчете.ВидСубконто3 = ВидСубконтоКонтрагенты);
				
				ЕстьДоговорыНаСчете = (СведенияОСчете.ВидСубконто1 = ВидСубконтоДоговоры)
					ИЛИ (СведенияОСчете.ВидСубконто2 = ВидСубконтоДоговоры)
					ИЛИ (СведенияОСчете.ВидСубконто3 = ВидСубконтоДоговоры);
					
				ЕстьПартияНаСчете = (СведенияОСчете.ВидСубконто1 = ВидСубконтоПартии)
					ИЛИ (СведенияОСчете.ВидСубконто2 = ВидСубконтоПартии)
					ИЛИ (СведенияОСчете.ВидСубконто3 = ВидСубконтоПартии);					
					
				Если ЕстьКонтрагентыНаСчете Тогда					
					ЗначенияБлокировки.Вставить(ВидСубконтоКонтрагенты, Контрагент); 
				КонецЕсли;	
					
				Если ЕстьДоговорыНаСчете Тогда					
					ЗначенияБлокировки.Вставить(ВидСубконтоДоговоры, ДоговорКонтрагент);
				КонецЕсли;	
							
				ОписаниеИсточника = Новый Соответствие;
				ОписаниеИсточника.Вставить("Организация", "Организация");
				
				Если ЗначениеЗаполнено(Партия) И ЕстьПартияНаСчете Тогда
					ОписаниеИсточника.Вставить(ВидСубконтоПартии, "ДокументОприходования");				
				КонецЕсли;
								
				Если ЕстьНоменклатураНаСчете Тогда
					ОписаниеИсточника.Вставить(ВидСубконтоНоменклатура, "Номенклатура");
				КонецЕсли;
				
				Если ЕстьСкладыНаСчете Тогда
					ОписаниеИсточника.Вставить(ВидСубконтоСклады, "Склад");
				КонецЕсли;
				
				ОбщегоНазначения.УстановитьУправляемуюБлокировку(СтруктураПараметровБлокировки, ЗначенияБлокировки, ОписаниеИсточника);
				
			КонецЦикла;	
				
		КонецЕсли;
		
		СтруктураПараметров.Вставить("ДеревоПартийТоваровНаСкладахБух", ПолучитьТаблицуПартийНаСкладах(СтруктураПараметров, "Бух", МассивОрганизаций, МассивНалоговыхНазначений, МассивСчетовУчетаБУ, МассивНоменклатуры, МассивСкладов, Контрагент, Партия, ДоговорКонтрагент));
			
		// Добавим колонку в которой будут отражаться списанные по БУ партии
		Если ТаблицаСписания.Колонки.Найти("СписанныеПартииБУ") = Неопределено Тогда
			ТаблицаСписания.Колонки.Добавить("СписанныеПартииБУ");
		КонецЕсли;
		
		// Добавим колонку с договором поставщика
		Если ТаблицаСписания.Колонки.Найти("ДоговорПоставщика") = Неопределено Тогда
			ТаблицаСписания.Колонки.Добавить("ДоговорПоставщика");
		КонецЕсли;
		
		// Добавим колонку с номенклатурной группой
		Если ТаблицаСписания.Колонки.Найти("НоменклатурнаяГруппа") = Неопределено Тогда
			ТаблицаСписания.Колонки.Добавить("НоменклатурнаяГруппа");
		КонецЕсли;
		
		Если ТаблицаСписания.Колонки.Найти("СкладНовый") = Неопределено Тогда
			ТаблицаСписания.Колонки.Добавить("СкладНовый");
		КонецЕсли;
		
		Если ТаблицаСписания.Колонки.Найти("НалоговоеНазначениеНовое") = Неопределено Тогда
			ТаблицаСписания.Колонки.Добавить("НалоговоеНазначениеНовое");
		КонецЕсли;
		
		Если ТаблицаСписания.Колонки.Найти("НоменклатураНовая") = Неопределено Тогда
			ТаблицаСписания.Колонки.Добавить("НоменклатураНовая");
		КонецЕсли;
		
		Если ВестиУчетПоДопРазрезамРегл Тогда
			
			Если ТаблицаСписания.Колонки.Найти("ХарактеристикаНоменклатуры") = Неопределено Тогда
				ТаблицаСписания.Колонки.Добавить("ХарактеристикаНоменклатуры");
			КонецЕсли;
		
			Если ТаблицаСписания.Колонки.Найти("СерияНоменклатуры") = Неопределено Тогда
				ТаблицаСписания.Колонки.Добавить("СерияНоменклатуры");
			КонецЕсли;
		
			Если ТаблицаСписания.Колонки.Найти("ЗаказПокупателя") = Неопределено Тогда
				ТаблицаСписания.Колонки.Добавить("ЗаказПокупателя");
			КонецЕсли;
		
			Если ТаблицаСписания.Колонки.Найти("Качество") = Неопределено Тогда
				ТаблицаСписания.Колонки.Добавить("Качество");
			КонецЕсли;
			
			Если ТаблицаСписания.Колонки.Найти("СписыватьТолькоПоЗаказуРегл") = Неопределено Тогда
				ТаблицаСписания.Колонки.Добавить("СписыватьТолькоПоЗаказуРегл");
				ТаблицаСписания.ЗаполнитьЗначения(Ложь, "СписыватьТолькоПоЗаказуРегл");
			КонецЕсли;
			
			Если ТаблицаСписания.Колонки.Найти("ХарактеристикаНоменклатурыНовая") = Неопределено Тогда
				ТаблицаСписания.Колонки.Добавить("ХарактеристикаНоменклатурыНовая");
			КонецЕсли;
			
			Если ТаблицаСписания.Колонки.Найти("СерияНоменклатурыНовая") = Неопределено Тогда
				ТаблицаСписания.Колонки.Добавить("СерияНоменклатурыНовая");
			КонецЕсли;
			
			Если ТаблицаСписания.Колонки.Найти("КачествоНовое") = Неопределено Тогда
				ТаблицаСписания.Колонки.Добавить("КачествоНовое");
			КонецЕсли;
			
			Если ТаблицаСписания.Колонки.Найти("ЗаказПокупателяНовый") = Неопределено Тогда
				ТаблицаСписания.Колонки.Добавить("ЗаказПокупателяНовый");
			КонецЕсли;
			
		КонецЕсли;
		
		СтруктураПараметров.Вставить("УчетнаяПолитика",    ПолучитьУчетнуюПолитику(МоментКон, ТаблицаСписания[0].Организация));
		СтруктураПараметров.Вставить("Контрагент",         Контрагент);
		СтруктураПараметров.Вставить("СодержаниеПроводки", Содержание);
		СтруктураПараметров.Вставить("ДоговорКонтрагента", ТаблицаСписания[0].ДоговорКонтрагента);
		СтруктураПараметров.Вставить("ОтключитьКонтрольОстатков",   Константы.ОтключитьКонтрольОтрицательныхОстатков.Получить());

		// Проверим - все ли нужные нам колонки есть на месте.
		Для Индекс = 1 По 4 Цикл

			Если ТаблицаСписания.Колонки.Найти("КорСубконтоСписанияБУ" + Индекс) = Неопределено Тогда
				ТаблицаСписания.Колонки.Добавить("КорСубконтоСписанияБУ" + Индекс);
			КонецЕсли;

		КонецЦикла;
		
		Если ТаблицаСписания.Колонки.Найти("СписыватьПоУказаннойСтоимости") = Неопределено Тогда
			ТаблицаСписания.Колонки.Добавить("СписыватьПоУказаннойСтоимости", Новый ОписаниеТипов("Булево"));
		КонецЕсли;
		
		Если ТаблицаСписания.Колонки.Найти("ИзменятьКоличество") = Неопределено Тогда
			ТаблицаСписания.Колонки.Добавить("ИзменятьКоличество", Новый ОписаниеТипов("Булево"));
			ТаблицаСписания.ЗаполнитьЗначения(Ложь, "ИзменятьКоличество");
		КонецЕсли;

		Если ТаблицаСписания.Колонки.Найти("СодержаниеПроводки") = Неопределено Тогда
			ТаблицаСписания.Колонки.Добавить("СодержаниеПроводки");
			ТаблицаСписания.ЗаполнитьЗначения(Содержание, "СодержаниеПроводки");
		КонецЕсли;

		Для Каждого СтрокаДокумента Из ТаблицаСписания Цикл
			
			СписаниеСоСклада(СтрокаДокумента, СтруктураПараметров, Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Сдвиг ГП партионного учета назад, используемый в неоперативных документах.
//
Процедура СдвигГраницыПоследовательностиПартионногоУчетаНазад(Дата, Ссылка, Организация = Неопределено) Экспорт
	
	МассивУчетов = Новый Массив;
	МассивУчетов.Добавить("Упр");
	
	Для каждого Учет Из МассивУчетов Цикл
		
		ИмяПоследовательности = "ПартионныйУчет";
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПартионныйУчетГраницы.МоментВремени
		|ИЗ
		|	Последовательность."+ИмяПоследовательности+".Границы КАК ПартионныйУчетГраницы
		| ";
		
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		МоментВремениДокумента = Новый МоментВремени(Ссылка.Дата, Ссылка);
		
		Если Выборка.Следующий() Тогда
			
			// Граница переносится назад, если документ проводится задним числом
			Если МоментВремениДокумента.Сравнить(Выборка.МоментВремени) = -1 Тогда
				
				Последовательности[ИмяПоследовательности].УстановитьГраницу(МоментВремениДокумента);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры//СдвигГраницыПоследовательностиПартионногоУчетаНазад

// Устанавливает ГП на последний документ в последней секунде месяца
// (при необходимости)
// Параметры 
// Дата - Последняя секунда месяца
// Учет - Строка вида учета
// Организация 
// Вызывается из модулей объектов документов "Коректировка стоимости списания",
// "Расчет себестоимости выпуска"
Процедура РегламентнаяУстановкаГП(Дата, Учет, Организация = Неопределено) Экспорт
	
	// При записи набора записей регламентного документа граница последовательности 
	// может быть смещена назад
	// В последней секунде месяца кроме регламентных корректировки стоимости списания и расчета 
	// себестоимости выпуска могут находиться движения по регистрам партий и других документов
	// и их моменты времени могут быть больше моментов времени регламентных документов
		
	// Сдвиг ГП вперед нужен только если ГП находится в последней секунде месяца и не стоит на
	// последнем документе в этой секунде
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПартионныйУчетГраницы.МоментВремени
	               |ИЗ
	               |	Последовательность.ПартионныйУчет.Границы КАК ПартионныйУчетГраницы
	               |ГДЕ
	               |	ПартионныйУчетГраницы.Период >= &ПериодНач
	               |	И ПартионныйУчетГраницы.Период < &ПериодКон";
	
	Запрос.УстановитьПараметр("ПериодНач", Дата);
	Запрос.УстановитьПараметр("ПериодКон", Дата+1);
	
	ИмяПоследовательности = "ПартионныйУчет";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
	
		Возврат;
	
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	МоментВремениГП = Выборка.МоментВремени;
	
	Запрос.Текст ="ВЫБРАТЬ ПЕРВЫЕ 1
	              |	ПартионныйУчет.МоментВремени КАК МоментВремени
	              |ИЗ
	              |	Последовательность.ПартионныйУчет КАК ПартионныйУчет
	              |ГДЕ
	              |	ПартионныйУчет.Период < &ПериодКон
	              |	И ПартионныйУчет.Период >= &ПериодНач
	              |
	              |УПОРЯДОЧИТЬ ПО
	              |	МоментВремени УБЫВ";

	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
	
		Возврат;
	
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	МоментВремениПоследнегоДокумента = Выборка.МоментВремени;
	
	Если МоментВремениПоследнегоДокумента.Сравнить(МоментВремениГП) = 1 Тогда
		
		Последовательности[ИмяПоследовательности].УстановитьГраницу(МоментВремениПоследнегоДокумента);
	
	КонецЕсли;

КонецПроцедуры//РегламентнаяУстановкаГП

// 
//
Функция ОпределитьНеобходимостьСдвигаГраницы(ДокументМоментВремени, Учет, Отбор = Неопределено) Экспорт
	
	// Границу последовательности можно сдвигать вперед только если между границей
	// и документом нет других документов в последовательности
	// Исключения - регламентные документы
	// Регламентные документы могут располагаться в последней секунде месяца
	// одновременно с другими документами и иметь момент времени меньше,
	// чем документы списания, находящиеся в той же секунде
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	Последовательность.Регистратор
	               |ИЗ
	               |	Последовательность.ПартионныйУчет КАК Последовательность
	               |ГДЕ
	               |	Последовательность.МоментВремени > &Граница
	               |	И Последовательность.МоментВремени < &Документ";
	
	ГраницаМоментВремени = Последовательности.ПартионныйУчет.ПолучитьГраницу();

	Запрос.УстановитьПараметр("Граница", ГраницаМоментВремени);
	Запрос.УстановитьПараметр("Документ", ДокументМоментВремени);
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции 

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА ПОСТУПЛЕНИЯ НЕОТФАКТУРОВАННЫХ ТОВАРОВ

Процедура ПоступлениеТоваровПоОрдеруУпр(СтрокаДокумента, СтруктураПараметров) Экспорт
			
	Если НЕ СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		Возврат;
	КонецЕсли;
	
	КодыОпераций = Перечисления.КодыОперацийПартииТоваров;
	
	Ссылка = СтрокаДокумента.Регистратор;
	
	ПриходныйОрдер = СтрокаДокумента.ПоступлениеПриходныйОрдер;
	
	Если Не ТипЗнч(ПриходныйОрдер) = Тип("ДокументСсылка.ПриходныйОрдерНаТовары") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросДатаОрдера = Новый Запрос("ВЫБРАТЬ Дата ИЗ Документ.ПриходныйОрдерНаТовары ГДЕ Ссылка = &Ссылка");
	ЗапросДатаОрдера.УстановитьПараметр("Ссылка", ПриходныйОрдер);
	Выборка = ЗапросДатаОрдера.Выполнить().Выбрать();
	Выборка.Следующий();
	
	// Дата начала обработки
	ДатаНач = Выборка.Дата;
	
	// Дата конца обработки
	ДатаКон = СтрокаДокумента.Период;
	
	// Партии, поступившие по накладной
	ЗапросПартии = Новый Запрос;
	
	ЗапросПартии.УстановитьПараметр("Регистратор", Ссылка);
	ЗапросПартии.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	ЗапросПартии.УстановитьПараметр("ПоОрдеру", Перечисления.СтатусыПартийТоваров.ПоОрдеру);
	
	ЗапросПартии.Текст = 
	"ВЫБРАТЬ
	|	ПартииТоваровНаСкладах.Номенклатура,
	|	ПартииТоваровНаСкладах.Склад,
	|	ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры,
	|	ПартииТоваровНаСкладах.СерияНоменклатуры,
	|	ПартииТоваровНаСкладах.ДокументОприходования,
	|	ПартииТоваровНаСкладах.ДокументОприходования.Дата КАК ДокументОприходованияДата,
	|	ПартииТоваровНаСкладах.СтатусПартии,
	|	ПартииТоваровНаСкладах.Заказ,
	|	СУММА(ПартииТоваровНаСкладах.Количество)	КАК Количество,
	|	СУММА(ПартииТоваровНаСкладах.Стоимость) 	КАК Стоимость,
	|	СУММА(ПартииТоваровНаСкладах.НДС) 			КАК НДС,
	|	СУММА(ПартииТоваровНаСкладах.Количество) 	КАК КоличествоДляВозврата,
	|	СУММА(ПартииТоваровНаСкладах.Стоимость) 	КАК СтоимостьДляВозврата,
	|	СУММА(ПартииТоваровНаСкладах.НДС) 			КАК НДСДляВозврата,
	|	ПартииТоваровНаСкладах.Качество
	|ИЗ
	|	РегистрНакопления.ПартииТоваровНаСкладах КАК ПартииТоваровНаСкладах
	|ГДЕ
	|	ПартииТоваровНаСкладах.ВидДвижения = &ВидДвиженияПриход
	|	И ПартииТоваровНаСкладах.СтатусПартии <> &ПоОрдеру
	|	И ПартииТоваровНаСкладах.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ПартииТоваровНаСкладах.Номенклатура,
	|	ПартииТоваровНаСкладах.Склад,
	|	ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры,
	|	ПартииТоваровНаСкладах.СерияНоменклатуры,
	|	ПартииТоваровНаСкладах.ДокументОприходования,
	|	ПартииТоваровНаСкладах.СтатусПартии,
	|	ПартииТоваровНаСкладах.Заказ,
	|	ПартииТоваровНаСкладах.Качество,
	|	ПартииТоваровНаСкладах.ДокументОприходования.Дата";
	
	ТабПартий = ЗапросПартии.Выполнить().Выгрузить();
	
	МассивНоменклатуры = ТабПартий.ВыгрузитьКолонку("Номенклатура");
	
	МассивНоменклатуры = УчетНДС.УдалитьПовторяющиесяЭлементыМассива(МассивНоменклатуры);
	
	// Пустая таблица
	ТабПартийПеред = Новый ТаблицаЗначений;
	ТабПартийПеред.Колонки.Добавить("Номенклатура");
	ТабПартийПеред.Колонки.Добавить("ХарактеристикаНоменклатуры");
	ТабПартийПеред.Колонки.Добавить("ДокументОприходования");
	ТабПартийПеред.Колонки.Добавить("ДокументОприходованияДата");
	ТабПартийПеред.Колонки.Добавить("ДоговорКонтрагента");
	ТабПартийПеред.Колонки.Добавить("ДокументПередачи");
	ТабПартийПеред.Колонки.Добавить("СтатусПартии");
	ТабПартийПеред.Колонки.Добавить("СтатусПередачи");
	ТабПартийПеред.Колонки.Добавить("Количество");
	ТабПартийПеред.Колонки.Добавить("Стоимость");
	ТабПартийПеред.Колонки.Добавить("НДС");
	
	// Запрос для определения количества строк в регистре партий:
	
	Если СтруктураПараметров.ТекНомерСтрокиПартииТоваровНаСкладахУпр = 0 Тогда
	
		Запрос = Новый Запрос;
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	МАКСИМУМ(ПартииТоваровНаСкладах.НомерСтроки) КАК НомерСтроки
		               |ИЗ
		               |	РегистрНакопления.ПартииТоваровНаСкладах КАК ПартииТоваровНаСкладах
		               |ГДЕ
		               |	ПартииТоваровНаСкладах.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			СтруктураПараметров.ТекНомерСтрокиПартииТоваровНаСкладахУпр = Выборка.НомерСтроки;
		КонецЕсли;
	КонецЕсли;
	
	// Запрос по списанию партий ордеров:
	
	Запрос = Новый Запрос;
	
	МассивРеализация = Новый Массив;
	МассивРеализация.Добавить(КодыОпераций.Реализация);
	МассивРеализация.Добавить(КодыОпераций.РеализацияРозница);
	МассивРеализация.Добавить(КодыОпераций.РеализацияКомиссия);
	МассивРеализация.Добавить(КодыОпераций.ВозвратОтПокупателяТекущийМесяц);
	Запрос.УстановитьПараметр("КодыСписаниеНаСебестоимость", МассивРеализация);
	
	МассивСписаниеНаПереданные = Новый Массив;
	МассивСписаниеНаПереданные.Добавить(КодыОпераций.ПередачаНаКомиссию);
	МассивСписаниеНаПереданные.Добавить(КодыОпераций.ПередачаВПереработку);
	Запрос.УстановитьПараметр("КодыСписаниеНаПереданные",МассивСписаниеНаПереданные);
	
	Запрос.УстановитьПараметр("КодыСписаниеНаЗатраты", КодыОпераций.СписаниеНаЗатраты);
	
	МассивПеремещениеСклад= Новый Массив;
	МассивПеремещениеСклад.Добавить(КодыОпераций.ПеремещениеМеждуСкладами);
	МассивПеремещениеСклад.Добавить(КодыОпераций.ПереоценкаПринятыхНаКомиссию);
	МассивПеремещениеСклад.Добавить(КодыОпераций.КорректировкаСерийИХарактеристик);
	МассивПеремещениеСклад.Добавить(КодыОпераций.КорректировкаКачества);
	МассивПеремещениеСклад.Добавить(КодыОпераций.РезервированиеПодЗаказ);
	МассивПеремещениеСклад.Добавить(КодыОпераций.СнятиеРезерваПодЗаказ);
	
	Запрос.УстановитьПараметр("КодыПеремещениеСклад", МассивПеремещениеСклад);
	МассивСписаниеНаСклад = Новый Массив;
	МассивСписаниеНаСклад.Добавить(КодыОпераций.ВозвратОтКомиссионера);
	МассивСписаниеНаСклад.Добавить(КодыОпераций.ВозвратОтПереработчика);
	Запрос.УстановитьПараметр("КодыСписаниеНаСклад", МассивСписаниеНаСклад); 
		
	Запрос.УстановитьПараметр("МассивДокументовОприходования", ПриходныйОрдер);
	Запрос.УстановитьПараметр("ДатаНач", ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Запрос.УстановитьПараметр("ВидДвиженияРасход", ВидДвиженияНакопления.Расход);
	
	Запрос.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
	Запрос.УстановитьПараметр("КодОперацииСписаниеПоОрдеру", Перечисления.КодыОперацийПартииТоваров.СписаниеПоОрдеру);
	
	Запрос.УстановитьПараметр("КодКомплектация", КодыОпераций.Комплектация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	""НаСкладах"" КАК СписаноИз,
	|	Источник.Период КАК Период,
	|	Источник.Регистратор КАК Регистратор,
	|	Источник.Номенклатура,
	|	Источник.Склад,
	|	Источник.ХарактеристикаНоменклатуры,
	|	Источник.СерияНоменклатуры,
	|	Источник.ДокументОприходования,
	|	Источник.ДокументОприходования.Дата КАК ДокументОприходованияДата,
	|	Источник.СтатусПартии,
	|	Источник.Заказ,
	|	СУММА(Источник.Количество) 	КАК Количество,
	|	СУММА(Источник.Стоимость) 	КАК Стоимость,
	|	СУММА(Источник.НДС) 		КАК НДС,
	|	Источник.КодОперации,
	|	Переданные.СтатусПередачи,
	|	Переданные.ДокументПередачи,
	|	Переданные.ДоговорКонтрагента,
	|	Переданные.КодОперации КАК КодОперацииПартииТоваровПереданные,
	|	НаСкладахПоступление.Склад КАК СкладПолучатель,
	|	НаСкладахПоступление.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатурыНовая,
	|	НаСкладахПоступление.СерияНоменклатуры КАК СерияНоменклатурыНовая,
	|	Источник.КодОперации КАК КодОперацииПартииТоваров,
	|	Источник.НомерКорСтроки,
	|	ВЫБОР
	|		КОГДА (НЕ Затраты.СтатьяЗатрат ЕСТЬ NULL )
	|			ТОГДА Затраты.СтатьяЗатрат
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяЗатрат,
	|	ВЫБОР
	|		КОГДА (НЕ Затраты.НоменклатурнаяГруппа ЕСТЬ NULL )
	|			ТОГДА Затраты.НоменклатурнаяГруппа
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	ВЫБОР
	|		КОГДА (НЕ Затраты.Подразделение ЕСТЬ NULL )
	|			ТОГДА Затраты.Подразделение
	|		КОГДА (НЕ Себестоимость.Подразделение ЕСТЬ NULL )
	|			ТОГДА Себестоимость.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА (НЕ Себестоимость.ЗаказПокупателя ЕСТЬ NULL )
	|			ТОГДА Себестоимость.ЗаказПокупателя
	|		КОГДА (НЕ НаСкладахПоступление.Заказ ЕСТЬ NULL )
	|			ТОГДА НаСкладахПоступление.Заказ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ЗаказСписания,
	|	0 КАК СтоимостьПоступление,
	|	Источник.Качество,
	|	НаСкладахПоступление.Качество КАК КачествоНовое,
	|	Источник.Номенклатура.ВестиПартионныйУчетПоСериям КАК ВестиПартионныйУчетПоСериям,
	|	ВЫБОР
	|		КОГДА (НЕ ИсточникДляКомплектации.Номенклатура ЕСТЬ NULL )
	|			ТОГДА ИсточникДляКомплектации.Номенклатура
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НоменклатураНовая,
	|	ВЫБОР
	|		КОГДА (НЕ ИсточникДляКомплектации.ДокументОприходования ЕСТЬ NULL )
	|			ТОГДА ИсточникДляКомплектации.ДокументОприходования
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументОприходованияНовый,
	|	ЕСТЬNULL(ИсточникДляКомплектации.Количество, 0) КАК КоличествоПоступление,
	|	Источник.НомерСтрокиСписанныхТоваров КАК НомерСтрокиСписанныхТоваров,
	|	СписанныеТовары.НоменклатураКомплекта КАК НоменклатураКомплекта,
	|	СписанныеТовары.ВалютаДокумента КАК ВалютаДокумента,
	|	СписанныеТовары.КурсДокумента КАК КурсДокумента,
	|	СписанныеТовары.КратностьДокумента КАК КратностьДокумента,
	|	СписанныеТовары.ХарактеристикаКомплекта КАК ХарактеристикаКомплекта,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ СписанныеТовары.Количество = 0
	|			ТОГДА СписанныеТовары.КоличествоКомплекта*Источник.Количество/СписанныеТовары.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК КоличествоКомплекта
	|ИЗ
	|	РегистрНакопления.ПартииТоваровНаСкладах КАК Источник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровПереданные КАК Переданные
	|		ПО Источник.Регистратор = Переданные.Регистратор
	|			И Источник.НомерКорСтроки = Переданные.НомерСтроки
	|			И (Источник.КодОперации В (&КодыСписаниеНаПереданные))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Затраты КАК Затраты
	|		ПО Источник.Регистратор = Затраты.Регистратор
	|			И Источник.НомерКорСтроки = Затраты.НомерСтроки
	|			И (Источник.КодОперации В (&КодыСписаниеНаЗатраты))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПродажиСебестоимость КАК Себестоимость
	|		ПО Источник.Регистратор = Себестоимость.Регистратор
	|			И Источник.НомерКорСтроки = Себестоимость.НомерСтроки
	|			И (Источник.КодОперации В (&КодыСписаниеНаСебестоимость))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах КАК НаСкладахПоступление
	|		ПО Источник.Регистратор = НаСкладахПоступление.Регистратор
	|			И Источник.НомерКорСтроки = НаСкладахПоступление.НомерСтроки
	|			И (Источник.КодОперации В (&КодыПеремещениеСклад))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах КАК ИсточникДляКомплектации
	|		ПО Источник.Регистратор = ИсточникДляКомплектации.Регистратор
	|			И Источник.НомерКорСтроки = ИсточникДляКомплектации.НомерСтроки
	|			И (Источник.КодОперации В (&КодКомплектация))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СписанныеТовары КАК СписанныеТовары
	|		ПО Источник.Регистратор = СписанныеТовары.Регистратор
	|			И (Источник.НомерСтрокиСписанныхТоваров = СписанныеТовары.НомерСтроки)
	|ГДЕ
	|	Источник.Период МЕЖДУ &ДатаНач И &ДатаКон
	|	И Источник.ВидДвижения = &ВидДвиженияРасход
	|	И Источник.КодОперации <> &КодОперацииСписаниеПоОрдеру
	|	И Источник.Регистратор <> &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Источник.Период,
	|	Источник.Регистратор,
	|	Источник.Номенклатура,
	|	Источник.Склад,
	|	Источник.ХарактеристикаНоменклатуры,
	|	Источник.СерияНоменклатуры,
	|	Источник.ДокументОприходования,
	|	Источник.СтатусПартии,
	|	Источник.Заказ,
	|	Источник.КодОперации,
	|	Переданные.СтатусПередачи,
	|	Переданные.ДокументПередачи,
	|	Переданные.ДоговорКонтрагента,
	|	Переданные.КодОперации,
	|	НаСкладахПоступление.Склад,
	|	НаСкладахПоступление.ХарактеристикаНоменклатуры,
	|	НаСкладахПоступление.СерияНоменклатуры,
	|	Источник.НомерКорСтроки,
	|	ВЫБОР
	|		КОГДА (НЕ Затраты.СтатьяЗатрат ЕСТЬ NULL )
	|			ТОГДА Затраты.СтатьяЗатрат
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА (НЕ Затраты.НоменклатурнаяГруппа ЕСТЬ NULL )
	|			ТОГДА Затраты.НоменклатурнаяГруппа
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА (НЕ Затраты.Подразделение ЕСТЬ NULL )
	|			ТОГДА Затраты.Подразделение
	|		КОГДА (НЕ Себестоимость.Подразделение ЕСТЬ NULL )
	|			ТОГДА Себестоимость.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА (НЕ Себестоимость.ЗаказПокупателя ЕСТЬ NULL )
	|			ТОГДА Себестоимость.ЗаказПокупателя
	|		КОГДА (НЕ НаСкладахПоступление.Заказ ЕСТЬ NULL )
	|			ТОГДА НаСкладахПоступление.Заказ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	Источник.Качество,
	|	НаСкладахПоступление.Качество,
	|	Источник.ДокументОприходования.Дата,
	|	Источник.Номенклатура.ВестиПартионныйУчетПоСериям,
	|	ВЫБОР
	|		КОГДА (НЕ ИсточникДляКомплектации.Номенклатура ЕСТЬ NULL )
	|			ТОГДА ИсточникДляКомплектации.Номенклатура
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА (НЕ ИсточникДляКомплектации.ДокументОприходования ЕСТЬ NULL )
	|			ТОГДА ИсточникДляКомплектации.ДокументОприходования
    |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ЕСТЬNULL(ИсточникДляКомплектации.Количество, 0),
	|	Источник.НомерСтрокиСписанныхТоваров,
	|	СписанныеТовары.НоменклатураКомплекта,
	|	СписанныеТовары.ХарактеристикаКомплекта,
	|	СписанныеТовары.ВалютаДокумента,
	|	СписанныеТовары.КурсДокумента,
	|	СписанныеТовары.КратностьДокумента,	
	|	Источник.КодОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Переданные"",
	|	Источник.Период,
	|	Источник.Регистратор,
	|	Источник.Номенклатура,
	|	НаСкладах.Склад,
	|	Источник.ХарактеристикаНоменклатуры,
	|	НаСкладах.СерияНоменклатуры,
	|	Источник.ДокументОприходования,
	|	Источник.ДокументОприходования.Дата,
	|	Источник.СтатусПартии,
	|	НаСкладах.Заказ,
	|	СУММА(Источник.Количество),
	|	СУММА(Источник.Стоимость),
	|	СУММА(Источник.НДС),
	|	Источник.КодОперации,
	|	Источник.СтатусПередачи,
	|	Источник.ДокументПередачи,
	|	Источник.ДоговорКонтрагента,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	НаСкладах.КодОперации,
	|	Источник.НомерКорСтроки,
	|	NULL,
	|	NULL,
	|	Себестоимость.Подразделение,
	|	ВЫБОР
	|		КОГДА (НЕ Себестоимость.ЗаказПокупателя ЕСТЬ NULL )
	|			ТОГДА Себестоимость.ЗаказПокупателя
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	NULL,
	|	NULL,
	|	Источник.Номенклатура.ВестиПартионныйУчетПоСериям,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,     	
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL
	|ИЗ
	|	РегистрНакопления.ПартииТоваровПереданные КАК Источник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах КАК НаСкладах
	|		ПО Источник.Регистратор = НаСкладах.Регистратор
	|			И Источник.НомерКорСтроки = НаСкладах.НомерСтроки
	|			И (Источник.КодОперации В (&КодыСписаниеНаСклад))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПродажиСебестоимость КАК Себестоимость
	|		ПО Источник.Регистратор = Себестоимость.Регистратор
	|			И Источник.НомерКорСтроки = Себестоимость.НомерСтроки
	|			И (Источник.КодОперации В (&КодыСписаниеНаСебестоимость))
	|ГДЕ
	|	Источник.Период МЕЖДУ &ДатаНач И &ДатаКон
	|	И Источник.ВидДвижения = &ВидДвиженияРасход
	|	И Источник.Регистратор <> &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Источник.Номенклатура,
	|	Источник.ДоговорКонтрагента,
	|	Источник.ДокументПередачи,
	|	Источник.ХарактеристикаНоменклатуры,
	|	Источник.ДокументОприходования,
	|	Источник.СтатусПартии,
	|	Источник.СтатусПередачи,
	|	НаСкладах.Склад,
	|	НаСкладах.СерияНоменклатуры,
	|	НаСкладах.Заказ,
	|	Источник.Период,
	|	Источник.Регистратор,
	|	Источник.КодОперации,
	|	НаСкладах.КодОперации,
	|	Источник.НомерКорСтроки,
	|	ВЫБОР
	|		КОГДА (НЕ Себестоимость.ЗаказПокупателя ЕСТЬ NULL )
	|			ТОГДА Себестоимость.ЗаказПокупателя
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	Себестоимость.Подразделение,
	|	Источник.ДокументОприходования.Дата,
	|	Источник.Номенклатура.ВестиПартионныйУчетПоСериям
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Регистратор";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ", "ГДЕ Источник.Номенклатура В (&МассивНоменклатуры)
	|	И Источник.ДокументОприходования В (&МассивДокументовОприходования) И ");
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	// Пустые колонки для совместимости со структурой регистра СписанныеТовары
	ТаблицаЗапроса.Колонки.Добавить("Организация");
	ТаблицаЗапроса.Колонки.Добавить("Проект");
	ТаблицаЗапроса.Колонки.Добавить("СтатусПартииНовый");
	ТаблицаЗапроса.Колонки.Добавить("ВедениеУчетаПоПроектам",       Новый ОписаниеТипов("Булево"));
	ТаблицаЗапроса.Колонки.Добавить("ОтражатьВУправленческомУчете", Новый ОписаниеТипов("Булево"));
	ТаблицаЗапроса.Колонки.Добавить("ВидТабличнойЧасти");
	ТаблицаЗапроса.Колонки.Добавить("ЗаказПартии");
	
	ТаблицаЗапроса.ЗаполнитьЗначения(Истина, "ОтражатьВУправленческомУчете");
	
	ТаблицаЗапроса.Колонки.Добавить("ИзменитьСерию", Новый ОписаниеТипов("Булево"));
	
	ТаблицаЗапроса.Колонки.Добавить("ИзменитьХарактеристику", Новый ОписаниеТипов("Булево"));
		
	Если ТаблицаЗапроса.Количество() = 0 тогда
		Возврат;
	КонецЕсли;

	// Таблицы остатков партий
	СтруктураПараметров.Вставить("ТаблицаПартииТоваровНаСкладах"+"Упр", ТабПартий);
	СтруктураПараметров.Вставить("ТаблицаПартииТоваровПереданные"+"Упр", ТабПартийПеред);
	
	НачНомерНаСкладах = 0;
	Если Не СтруктураПараметров.Свойство("МинНомерСтрокиПартииТоваровНаСкладахУпр",НачНомерНаСкладах) тогда
		НачНомерНаСкладах = СтруктураПараметров.ТекНомерСтрокиПартииТоваровНаСкладахУпр;
		СтруктураПараметров.Вставить("МинНомерСтрокиПартииТоваровНаСкладахУпр", НачНомерНаСкладах);
	КонецЕсли;
	
	НачНомерПереданные = 0;
	Если Не СтруктураПараметров.Свойство("МинНомерСтрокиПартииТоваровПереданныеУпр",НачНомерПереданные) тогда
		НачНомерПереданные = СтруктураПараметров.ТекНомерСтрокиПартииТоваровПереданныеУпр;
		СтруктураПараметров.Вставить("МинНомерСтрокиПартииТоваровПереданныеУпр", НачНомерПереданные);
	КонецЕсли;
	
	Для Каждого ВыборкаДвижений ИЗ ТаблицаЗапроса Цикл
	
		// Сторно списания по ордеру
		Если ВыборкаДвижений.СписаноИз = "НаСкладах" Тогда
			
			СтруктураПоиска = Новый Структура(
			"Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры, Заказ, Качество",
			ВыборкаДвижений.Номенклатура,
			ВыборкаДвижений.ХарактеристикаНоменклатуры,
			ВыборкаДвижений.СерияНоменклатуры,
			ВыборкаДвижений.Заказ,
			ВыборкаДвижений.Качество
			);
			
			Если УчетнаяПолитика("ВестиПартионныйУчетПоСкладам", "Упр", , СтруктураПараметров) Тогда
				СтруктураПоиска.Вставить("Склад",ВыборкаДвижений.Склад);
			КонецЕсли;
			
			НайденныеСтроки = СтруктураПараметров["ТаблицаПартииТоваровНаСкладах"+"Упр"].НайтиСтроки(СтруктураПоиска);
			
			КоличествоОсталосьПогасить = ВыборкаДвижений.Количество;
			
			Для каждого Строка Из НайденныеСтроки Цикл
				
				Если КоличествоОсталосьПогасить = 0 Тогда
					Прервать;
				КонецЕсли;
				
				Если ВыборкаДвижений.КодОперации = Перечисления.КодыОперацийПартииТоваров.ВозвратОтПокупателяТекущийМесяц тогда
					
					Количество = Строка.КоличествоДляВозврата;
					Стоимость  = Строка.СтоимостьДляВозврата;
					НДС		   = Строка.НДСДляВозврата;

				Иначе

					Количество = Строка.Количество;
					Стоимость  = Строка.Стоимость;
					НДС		   = Строка.НДС;

				КонецЕсли;
				
				Если Количество = 0 Тогда
					Продолжить;
				КонецЕсли;
		
				Если Количество >= КоличествоОсталосьПогасить Тогда
					КоэффСписания = КоличествоОсталосьПогасить/Количество;
				Иначе
					КоэффСписания = 1;
				КонецЕсли;
				
				// Ресурсы 
				Количество	= Окр(Количество * КоэффСписания,3,1);
				Стоимость	= Окр(Стоимость  * КоэффСписания,2,1);
				НДС			= Окр(НДС  		 * КоэффСписания,2,1);
				
				// Сторно старого движения
				Движение              = ДобавитьДвижениеВСтруктуруПараметров("ПартииТоваровНаСкладах"+"Упр", СтруктураПараметров);
				
				ЗаполнитьЗначенияСвойств(Движение, ВыборкаДвижений);
				
				Движение.Период       = СтрокаДокумента.Период;
				
				Движение.ВидДвижения  = ВидДвиженияНакопления.Расход;
				
				// Сведения о документе списания
				Движение.ДокументДвижения        = ВыборкаДвижений.Регистратор;
				Движение.ДокументДвиженияПериод  = ВыборкаДвижений.Период;
				
				Движение.Количество   = -Количество;
				Движение.Стоимость    = 0;
				Движение.НДС    	  = 0;
				
				НаправлениеСписания = ПолучитьНаправлениеСписанияПоКодуОперации(ВыборкаДвижений.КодОперации, ВыборкаДвижений.СтатьяЗатрат);
				
				// Аналогичную корректировку выполним для корреспондирующего движения
				ВыполнитьКорДвижениеУпр("НаСкладах", НаправлениеСписания, ВыборкаДвижений, СтруктураПараметров, Движение);
				
				// Заполним в КорДвижении дополнительные поля
				Если НаправлениеСписания = "Переданные" тогда
					КорДвижение = СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр[Движение.НомерКорСтроки -1- НачНомерПереданные];
					КорДвижение.ДокументДвижения        = ВыборкаДвижений.Регистратор;
					КорДвижение.ДокументДвиженияПериод  = ВыборкаДвижений.Период;
				КонецЕсли;
				
				// Новое движение
				Движение              = ДобавитьДвижениеВСтруктуруПараметров("ПартииТоваровНаСкладах"+"Упр", СтруктураПараметров);
				ЗаполнитьЗначенияСвойств(Движение, ВыборкаДвижений);
				Движение.ВидДвижения  = ВидДвиженияНакопления.Расход;
				
				Движение.Период       = СтрокаДокумента.Период;
				
				Если НЕ УчетнаяПолитика("ВестиПартионныйУчетПоСкладам", "Упр", , СтруктураПараметров) Тогда
					Движение.Склад = Неопределено;
				КонецЕсли;				
				
				// Сведения о документе списания
				Движение.ДокументДвижения        = ВыборкаДвижений.Регистратор;
				Движение.ДокументДвиженияПериод  = ВыборкаДвижений.Период;
				
				Движение.ДокументОприходования = Строка.ДокументОприходования;
				
				Движение.СтатусПартии = Строка.СтатусПартии;
				
				Движение.Количество   = Количество;
				Движение.Стоимость    = Стоимость;
				Движение.НДС    	  = НДС;
				
				КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - Движение.Количество;
				
				Если ВыборкаДвижений.КодОперации = Перечисления.КодыОперацийПартииТоваров.ВозвратОтПокупателяТекущийМесяц тогда

					Строка.КоличествоДляВозврата = Строка.КоличествоДляВозврата - Движение.Количество;
					Строка.СтоимостьДляВозврата  = Строка.СтоимостьДляВозврата  - Движение.Стоимость;

				Иначе
				
				Строка.Количество 	= Строка.Количество - Движение.Количество;
				Строка.Стоимость  	= Строка.Стоимость  - Движение.Стоимость;
				Строка.НДС  		= Строка.НДС  		- Движение.НДС;
				
				КонецЕсли;
				
				// Аналогичную корректировку выполним для корреспондирующего движения
				ВыполнитьКорДвижениеУпр("НаСкладах", НаправлениеСписания, ВыборкаДвижений, СтруктураПараметров, Движение);
				
				// Заполним в КорДвижении дополнительные поля
				Если НаправлениеСписания = "Переданные" тогда
					КорДвижение = СтруктураПараметров.ТаблицаДвиженийПартииТоваровПереданныеУпр[Движение.НомерКорСтроки -1- НачНомерПереданные];
					КорДвижение.ДокументДвижения        = ВыборкаДвижений.Регистратор;
					КорДвижение.ДокументДвиженияПериод  = ВыборкаДвижений.Период;
				
					// Если движение привело к поступлению в партии товаров переданные - надо увеличить остаток с соответствующим статусом партии
					НоваяСтрокаПереданных = СтруктураПараметров["ТаблицаПартииТоваровПереданные"+"Упр"].Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаПереданных,ВыборкаДвижений);
					ЗаполнитьЗначенияСвойств(НоваяСтрокаПереданных,Движение);
				КонецЕсли;

				
			КонецЦикла; 
			
		ИначеЕсли ВыборкаДвижений.СписаноИз = "Переданные" Тогда	
			
			СтруктураПоиска = Новый Структура(
			"Номенклатура, ХарактеристикаНоменклатуры, ДоговорКонтрагента, ДокументПередачи, СтатусПередачи",
			ВыборкаДвижений.Номенклатура,
			ВыборкаДвижений.ХарактеристикаНоменклатуры,
			ВыборкаДвижений.ДоговорКонтрагента,
			ВыборкаДвижений.ДокументПередачи,
			ВыборкаДвижений.СтатусПередачи,
			);
			
			СтруктураПоиска.Вставить("СтатусПередачи", ВыборкаДвижений.СтатусПередачи);
			
			НайденныеСтроки = СтруктураПараметров["ТаблицаПартииТоваровПереданные"+"Упр"].НайтиСтроки(СтруктураПоиска);
			
			КоличествоОсталосьПогасить = ВыборкаДвижений.Количество;
			
			Для каждого Строка Из НайденныеСтроки Цикл
				
				Если КоличествоОсталосьПогасить = 0 Тогда
					Прервать;
				КонецЕсли;
				
				Если Строка.Количество = 0 Тогда
					Продолжить;
				КонецЕсли;
		
				Если Строка.Количество >= КоличествоОсталосьПогасить Тогда
					КоэффСписания = КоличествоОсталосьПогасить/Строка.Количество;
				Иначе
					КоэффСписания = 1;
				КонецЕсли;
				
				// Ресурсы 
				Количество	= Окр(Строка.Количество * КоэффСписания,3,1);
				Стоимость	= Окр(Строка.Стоимость  * КоэффСписания,2,1);
				НДС			= Окр(Строка.НДС  		* КоэффСписания,2,1);
				
				// Сторно старого движения
				Движение              = ДобавитьДвижениеВСтруктуруПараметров("ПартииТоваровПереданные"+"Упр", СтруктураПараметров);
				
				ЗаполнитьЗначенияСвойств(Движение,ВыборкаДвижений);
				
				Движение.Период       = СтрокаДокумента.Период;
				Движение.ВидДвижения  = ВидДвиженияНакопления.Расход;
				
				// Сведения о документе списания
				Движение.ДокументДвижения        = ВыборкаДвижений.Регистратор;
				Движение.ДокументДвиженияПериод  = ВыборкаДвижений.Период;
				
				Движение.Количество   = -Количество;
				Движение.Стоимость    = 0;
				Движение.НДС    	  = 0;
				
				// Код операции партии товаров должен совпадать с кодом операции
				ВыборкаДвижений.КодОперацииПартииТоваров = ВыборкаДвижений.КодОперации;
                			
				// Аналогичную корректировку выполним для корреспондирующего движения
				НаправлениеСписания = ПолучитьНаправлениеСписанияПоКодуОперации(ВыборкаДвижений.КодОперации, ВыборкаДвижений.СтатьяЗатрат);
				ВыполнитьКорДвижениеУпр("Переданные",НаправлениеСписания, ВыборкаДвижений, СтруктураПараметров, Движение);
				
				// Заполним в КорДвижении дополнительные поля
				Если НаправлениеСписания = "НаСкладах" тогда
					КорДвижение = СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр[Движение.НомерКорСтроки -1- НачНомерНаСкладах];
					КорДвижение.ДокументДвижения        = ВыборкаДвижений.Регистратор;
					КорДвижение.ДокументДвиженияПериод  = ВыборкаДвижений.Период;
				КонецЕсли;
				
				// Новое движение
				Движение              = ДобавитьДвижениеВСтруктуруПараметров("ПартииТоваровПереданные"+"Упр", СтруктураПараметров);
				
				ЗаполнитьЗначенияСвойств(Движение,ВыборкаДвижений);
				
				Движение.Период       = СтрокаДокумента.Период;

				Движение.ВидДвижения  = ВидДвиженияНакопления.Расход;
				
				// Сведения о документе списания
				Движение.ДокументДвижения        = ВыборкаДвижений.Регистратор;
				Движение.ДокументДвиженияПериод  = ВыборкаДвижений.Период;
				
				Движение.ДокументОприходования = Строка.ДокументОприходования;

				Движение.СтатусПартии  = Строка.СтатусПартии;

				Движение.Количество   = Количество;
				Движение.Стоимость    = Стоимость;
				Движение.НДС    	  = НДС;
				
				// Уменьшение погашаемого количества
				КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - Движение.Количество;
				
				Строка.Количество 	= Строка.Количество - Движение.Количество;
				Строка.Стоимость  	= Строка.Стоимость 	- Движение.Стоимость;
				Строка.НДС  		= Строка.НДС 		- Движение.НДС;
				
				// Аналогичную корректировку выполним для корреспондирующего движения
				ВыполнитьКорДвижениеУпр("Переданные", НаправлениеСписания, ВыборкаДвижений, СтруктураПараметров, Движение);
            				
				// Заполним в КорДвижении дополнительные поля
				Если НаправлениеСписания = "НаСкладах" тогда
					КорДвижение = СтруктураПараметров.ТаблицаДвиженийПартииТоваровНаСкладахУпр[Движение.НомерКорСтроки -1- НачНомерНаСкладах];
					КорДвижение.ДокументДвижения        = ВыборкаДвижений.Регистратор;
					КорДвижение.ДокументДвиженияПериод  = ВыборкаДвижений.Период;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ПоступлениеТоваровПоОрдеруУпр

// Процедура-вход для всех документов. Проводит переданный документ по учету партий товаров
//
// Параметры
//  ОбрабатываемыйДокумент - проводимый документ
//  ТаблицаДокумента – таблица движений документа из регистра СписанныеТовары
//  Останавливаться - флаг остановки проведения по партиям на первом документе, который по партиям не провелся
Процедура ДвижениеПартийТоваров(ОбрабатываемыйДокумент, ТаблицаДокумента = Неопределено,
								КоличествоСтрокВДокументе=0, ОтражатьВУправленческомУчете=Истина,
								ОтражатьВБухгалтерскомУчете=Ложь,
								Останавливаться = Ложь, ПроведениеОстановлено = Ложь, Отказ = Ложь, Партия = Неопределено, Содержание = "",
								ДокументМоментВремени = Неопределено, СписаниеВыполняетсяОбработкой = Ложь,	СтруктураНаборовЗаписей = Неопределено) Экспорт
								
	Если Содержание = "" Тогда
		Содержание = "Списаны ТМЦ";
	КонецЕсли;
	
	// Если не переданы строки документа, выбираем из базы.
	Если ТаблицаДокумента = Неопределено Тогда
		
		// Таблица строк документов списания
		ТаблицаДокумента = ПолучитьТаблицуСтрокДокументов(ОбрабатываемыйДокумент);
		
	КонецЕсли;
	
	ТаблицаСписания = ТаблицаДокумента;
	
	КоличествоСтрокВДокументе = ТаблицаСписания.Количество();
	
	СтруктураПараметров = Новый Структура;
	
	СтруктураДата = Новый Структура("Дата");
	ПолучитьРеквизитыОбъекта(ОбрабатываемыйДокумент, СтруктураДата);
	
	Если ДокументМоментВремени = Неопределено Тогда
		ДокументМоментВремени = Новый МоментВремени(СтруктураДата.Дата, ОбрабатываемыйДокумент);
	КонецЕсли; 
	
	СтруктураПараметров.Вставить("УчетнаяПолитика", ПолучитьУчетнуюПолитику(ДокументМоментВремени));
	
	Если КоличествоСтрокВДокументе>0 Тогда
		
		СтруктураПараметров.Вставить("ТекстСообщений", 	"");
		СтруктураПараметров.Вставить("Отказ", 			Отказ);
		СтруктураПараметров.Вставить("ВестиПартионныйУчетПоСкладам", УчетнаяПолитика("ВестиПартионныйУчетПоСкладам","Упр",,СтруктураПараметров));
		
		
		Если ОтражатьВУправленческомУчете Тогда
			
			Если СтруктураНаборовЗаписей = Неопределено Тогда
			
				// Движения - наборы записей по регистрам
				СоздатьНаборыЗаписей(СтруктураПараметров);
				
			Иначе
				
				Для каждого Элемент Из СтруктураНаборовЗаписей Цикл

					СтруктураПараметров.Вставить(Элемент.Ключ, Элемент.Значение);
		
				КонецЦикла;
			
			КонецЕсли; 
			
			ПодготовитьТаблицуСписания(СтруктураПараметров, ТаблицаСписания);
			
			Строка0 = ТаблицаСписания[0];
			
			Если НЕ ЗначениеЗаполнено(Строка0.Регистратор) Тогда
				ТаблицаСписания.ЗаполнитьЗначения(ОбрабатываемыйДокумент, "Регистратор");
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Строка0.Период) Тогда
				ТаблицаСписания.ЗаполнитьЗначения(СтруктураДата.Дата, "Период");
			КонецЕсли;
			
		КонецЕсли;
		
		ВыполнитьСписание(СтруктураПараметров, ТаблицаСписания, ДокументМоментВремени, Останавливаться, ПроведениеОстановлено, ОтражатьВУправленческомУчете, ОтражатьВБухгалтерскомУчете, Отказ, Партия, Содержание);
		
		Если ОтражатьВУправленческомУчете Тогда
			
			// Сдвиг границы последовательности осуществляется только при онлайновом списании партий
			// либо из обработки проведение по партиям.
			ЗакрытиеЗаказовПокупателей = ТипЗнч(ОбрабатываемыйДокумент) = Тип("ДокументСсылка.ЗакрытиеЗаказовПокупателей");
				
			Если СписаниеВыполняетсяОбработкой Или СтруктураПараметров.УчетнаяПолитика[0].СписыватьПартииПриПроведенииДокументовУпр Тогда
				
				СдвигатьГраницу = ОпределитьНеобходимостьСдвигаГраницы(ДокументМоментВремени,"Упр");
				
				Если СдвигатьГраницу Тогда
					Последовательности.ПартионныйУчет.УстановитьГраницу(ДокументМоментВремени);
				КонецЕсли;
					
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;

КонецПроцедуры // ДвижениеПартийТоваров()

Функция ПолучитьСписокДокументовПоПоследовательности(ИмяПоследовательности,МоментВремениНачало, ДатаОкончания, МаксимальноеКоличествоДокументовВВыборке=1000, Организация=Неопределено) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ " + Формат(МаксимальноеКоличествоДокументовВВыборке,"ЧГ=") + "
	                      |	ТаблицаПоследовательности.Период КАК Дата,
						  |	ТаблицаПоследовательности.Регистратор КАК Ссылка
	                      |ИЗ
	                      |	Последовательность." + ИмяПоследовательности + " КАК ТаблицаПоследовательности
						  |ГДЕ
						  |	ТаблицаПоследовательности.Период <= &ДатаКон
						  |	И ТаблицаПоследовательности.Период > &Период
						  |	ИЛИ ((ТаблицаПоследовательности.Период = &Период) И (ТаблицаПоследовательности.Регистратор > &Регистратор))");
	Если  ЗначениеЗаполнено(Организация) Тогда 					  
		Запрос.Текст = Запрос.Текст + "
	                      |	И ТаблицаПоследовательности.Организация = &Организация";
		Запрос.УстановитьПараметр("Организация",Организация);
	КонецЕсли;	
	Запрос.Текст = Запрос.Текст + "						  
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ТаблицаПоследовательности.Период,
						  |	ТаблицаПоследовательности.Регистратор";
	Запрос.УстановитьПараметр("ДатаКон",ДатаОкончания);					  
	Запрос.УстановитьПараметр("Период",МоментВремениНачало.Дата);					  
	Запрос.УстановитьПараметр("Регистратор",МоментВремениНачало.Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции	

// Возвращает список документов, которые нужно проводить по партиям
//
Функция ПолучитьСписокДокументов(ПоследнийОбработанныйДокумент, ДатаНач, СсылкаНач, ДатаКон) Экспорт
	
	МоментНач = Новый МоментВремени(ДатаНач, СсылкаНач);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаПоследнего",   ПоследнийОбработанныйДокумент.Дата);
	Запрос.УстановитьПараметр("СсылкаПоследнего", ПоследнийОбработанныйДокумент);
	Запрос.УстановитьПараметр("МоментНач", МоментНач);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКон);
	
	МассивТаблиц = Новый Массив;
	
	КоличествоСтрокЗаПроход = 1000;

	Для каждого МетаданныеДокумент Из Метаданные.Последовательности.ПартионныйУчет.Документы Цикл
		
		ТекстФлаги = "";
		Если МетаданныеДокумент.Реквизиты.Найти("ОтражатьВУправленческомУчете")<>Неопределено Тогда
			ТекстФлаги = ТекстФлаги + ", ОтражатьВУправленческомУчете КАК ОтражатьВУправленческомУчете";
		Иначе			
			ТекстФлаги = ТекстФлаги + ", Ложь КАК ОтражатьВУправленческомУчете";
		КонецЕсли;
		Если МетаданныеДокумент.Реквизиты.Найти("ОтражатьВБухгалтерскомУчете")<>Неопределено Тогда
			ТекстФлаги = ТекстФлаги + ", ОтражатьВБухгалтерскомУчете КАК ОтражатьВБухгалтерскомУчете";
		Иначе			
			ТекстФлаги = ТекстФлаги + ", Ложь КАК ОтражатьВБухгалтерскомУчете";
		КонецЕсли;
		
		ТекстОрганизация = "";
		Если МетаданныеДокумент.Реквизиты.Найти("Организация")<>Неопределено Тогда
			ТекстОрганизация = ТекстОрганизация + ", Организация КАК Организация";
		Иначе			
			ТекстОрганизация = ТекстОрганизация + ", NULL КАК Организация";
		КонецЕсли;
		
		Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ "+Формат(КоличествоСтрокЗаПроход, "ЧГ=0")+" 
		|   Док.Дата, Док.Ссылка  " + ТекстФлаги + ТекстОрганизация + "
		|ИЗ Документ."+МетаданныеДокумент.Имя + " Док"; 
	
		// Документы в этой дате
		Запрос.Текст = Текст + "
		|ГДЕ Док.МоментВремени>&МоментНач
		|	И Док.Дата = &ДатаПоследнего И Док.Ссылка > &СсылкаПоследнего 
		|УПОРЯДОЧИТЬ ПО Дата, Ссылка";
		
		МассивТаблиц.Добавить(Запрос.Выполнить().Выгрузить());
	
		// Документы в следующей дате
		Запрос.Текст = Текст + " 
		|ГДЕ  Док.МоментВремени>=&МоментНач
		|	И Док.Дата > &ДатаПоследнего И Док.Дата <= &ДатаКон 
		|УПОРЯДОЧИТЬ ПО Дата, Ссылка";
		
		МассивТаблиц.Добавить(Запрос.Выполнить().Выгрузить());
		
	КонецЦикла;
	
	
	Таблица = Новый ТаблицаЗначений;
	
	// После упорядочивания по датам строки должны располагаться в том же порядке, что и в исходном запросе
	// для этого в служебной колонке запомним порядок запроса (таблицы в массиве) и положение строки в запросе в виде 
	// ПорядокТаблицыВМассиве*10^N + ПорядокВТаблице, где N - количество десятичных разрядов в числе КоличествоСтрокЗаПроход.
	СдвигРазряда = Pow(10,Цел(Log10(КоличествоСтрокЗаПроход)+1));
	
	Таблица.Колонки.Добавить("Ключ"); // Исходный индекс строки
	
	// Создаем колонки
	Для Каждого Колонка Из МассивТаблиц[0].Колонки Цикл
		Таблица.Колонки.Добавить(Колонка.Имя);
	КонецЦикла;
	
	// Загрузим значения в одну таблицу
	Сч=0;
	Для Каждого Элемент Из МассивТаблиц Цикл
		
		Сч=Сч+1;
		
		Для Каждого Строка Из Элемент Цикл
			
			НоваяСтрока = Таблица.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);
			
			НоваяСтрока.Ключ=Сч*СдвигРазряда+Элемент.Индекс(Строка);
		КонецЦикла;
	КонецЦикла; 
	
	Таблица.Сортировать("Дата Возр, Ссылка Возр, Ключ Возр");
	Таблица.Свернуть("Дата, Ссылка, ОтражатьВУправленческомУчете, ОтражатьВБухгалтерскомУчете, Организация");
	
	// От таблицы оставим только первые КоличествоСтрокЗаПроход, таким образом получим ограничение
	// обрабатываемого количества строк числом КоличествоСтрокЗаПроход и ограничение выбираемых из  
	// базы данных строк (не более КоличествоСтрокЗаПроход*МассивТаблиц.Количество())
	
	КоличествоЛишнихСтрокСнизу = Таблица.Количество()-КоличествоСтрокЗаПроход;
	
	Если КоличествоЛишнихСтрокСнизу>0 Тогда
		Для Сч=1 По КоличествоЛишнихСтрокСнизу Цикл
			Таблица.Удалить(Таблица[КоличествоСтрокЗаПроход]);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Таблица;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ТЕХНОЛОГИЧЕСКИЕ

Процедура ЗаписатьДокументНаСервере(ДокументСсылка, РежимЗаписи) Экспорт

	ДокументСсылка.ПолучитьОбъект().Записать(РежимЗаписи);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОВОДКИ ПО БУХ УЧЕТУ, ЗАВИСЯЩИЕ ОТ ТОГО, КАКИЕ ПАРТИИ СПИСАНЫ

Процедура ОприходоватьТоварПринятыйНаОтветственноеХранение(СтрокаДокумента, СтруктураПараметров, Строка)

	ОприходоватьТоварПринятыйНаОтветственноеХранениеУпр(СтрокаДокумента, СтруктураПараметров, Строка);
	
КонецПроцедуры//ОприходоватьТоварПринятыйНаОтветственноеХранение

Процедура ОприходоватьТоварПринятыйНаОтветственноеХранениеУпр(СтрокаДокумента, СтруктураПараметров, Строка)

	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		
		Учет = "Упр";
		
	Иначе
		Возврат;
	КонецЕсли;
	
	Движение = ДобавитьДвижениеВСтруктуруПараметров("ПартииТоваровНаСкладах"+Учет, СтруктураПараметров);
	
	ЗаполнитьЗначенияСвойств(Движение,Строка);
	
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	
	Движение.КодОперации = Перечисления.КодыОперацийПартииТоваров.ОтложеннаяОтгрузка;
	
	Если Строка.СтатусПартии = Перечисления.СтатусыПартийТоваров.ВозвратнаяТара Тогда
		
		Движение.СтатусПартии = Перечисления.СтатусыПартийТоваров.ВозвратнаяТараОтложеннаяОтгрузка;
	Иначе
			
		Движение.СтатусПартии = Перечисления.СтатусыПартийТоваров.КупленныйОтложеннаяОтгрузка;
	КонецЕсли;

КонецПроцедуры//ОприходоватьТоварПринятыйНаОтветственноеХранение

////////////////////////////////////////////////////////////////////////////////
// ПОЛУЧЕНИЕ ОСТАТКОВ ИЗ РЕГИСТРОВ ПАРТИЙ

// Упр

Процедура ЗаполнитьЗапросПартийНаСкладахУпр(Запрос, ВестиПартионныйУчетПоСкладам, СтратегияСтатусПартии, СпособОценкиМПЗ)

	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписанныеТовары.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ПартииТоваровНаСкладах.Номенклатура,
	|	ПартииТоваровНаСкладах.ДокументОприходования КАК ДокументОприходования,
	|	ПартииТоваровНаСкладах.ДокументОприходования.Дата КАК ДокументОприходованияДата,
	|	ПартииТоваровНаСкладах.ДокументОприходования.МоментВремени КАК ДокументОприходованияМоментВремени,
	|	ПартииТоваровНаСкладах.Склад,
	|	ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры,
	|	ПартииТоваровНаСкладах.СерияНоменклатуры,
	|	ПартииТоваровНаСкладах.Качество,
	|	ПартииТоваровНаСкладах.Заказ,
	|	ПартииТоваровНаСкладах.КоличествоОстаток КАК Количество,
	|	ПартииТоваровНаСкладах.СтоимостьОстаток КАК Стоимость,
	|	ПартииТоваровНаСкладах.НДСОстаток КАК НДС,
	|	ПартииТоваровНаСкладах.СтатусПартии,
	|	ВЫБОР
	|		КОГДА СписанныеТовары.СерияНоменклатуры = ПартииТоваровНаСкладах.СерияНоменклатуры
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЧислоСерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА СписанныеТовары.ДокументПартии = НЕОПРЕДЕЛЕНО
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СписанныеТовары.ДокументПартии = ПартииТоваровНаСкладах.ДокументОприходования
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧислоДокументОприходования,
	|	ВЫБОР
	|		КОГДА СписанныеТовары.ЗаказПартии = НЕОПРЕДЕЛЕНО
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПартииТоваровНаСкладах.Заказ = &ПустойЗаказ
	|					ТОГДА 1
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧислоЗаказ,
	|	ВЫБОР
	|		КОГДА ПартииТоваровНаСкладах.СтатусПартии = &НаКомиссию
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЧислоСтатусПартии
	|ИЗ
	|	РегистрСведений.СписанныеТовары КАК СписанныеТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах.Остатки(
	|		&Дат,
	|		Номенклатура В
	|		    (ВЫБРАТЬ
	|		        РегистрСведений.СписанныеТовары.Номенклатура
	|		    ИЗ
	|		        РегистрСведений.СписанныеТовары
	|		    ГДЕ
	|		        РегистрСведений.СписанныеТовары.Регистратор = &Ссылка)" + ?(ВестиПартионныйУчетПоСкладам, "
	|		И (Склад В 
	|		    (ВЫБРАТЬ
	|		        РегистрСведений.СписанныеТовары.Склад
	|		    ИЗ
	|		        РегистрСведений.СписанныеТовары
	|		    ГДЕ
	|		        РегистрСведений.СписанныеТовары.Регистратор = &Ссылка) ИЛИ Склад = &ПустойСклад)", "") + ") КАК ПартииТоваровНаСкладах
	|		ПО СписанныеТовары.Номенклатура = ПартииТоваровНаСкладах.Номенклатура
	|			И СписанныеТовары.ХарактеристикаНоменклатуры = ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры
	|			И (ВЫБОР
	|				КОГДА ПартииТоваровНаСкладах.Качество = &ПустоеКачество
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВЫБОР
	|						КОГДА СписанныеТовары.Качество = &ПустоеКачество
	|							ТОГДА ПартииТоваровНаСкладах.Качество = &КачествоНовый
	|						ИНАЧЕ ПартииТоваровНаСкладах.Качество = СписанныеТовары.Качество
	|					КОНЕЦ
	|			КОНЕЦ)
	|			" + ?(ВестиПартионныйУчетПоСкладам, "И (ПартииТоваровНаСкладах.Склад = СписанныеТовары.Склад ИЛИ ПартииТоваровНаСкладах.Склад = &ПустойСклад)", "") + "
	|			И (ВЫБОР
	|				КОГДА СписанныеТовары.ДопустимыйСтатус1 <> &ПустойСтатус
	|						ИЛИ СписанныеТовары.ДопустимыйСтатус2 <> &ПустойСтатус
	|						ИЛИ СписанныеТовары.ДопустимыйСтатус3 <> &ПустойСтатус
	|						ИЛИ СписанныеТовары.ДопустимыйСтатус4 <> &ПустойСтатус
	|					ТОГДА ПартииТоваровНаСкладах.СтатусПартии = &ПустойСтатус
	|							ИЛИ ПартииТоваровНаСкладах.СтатусПартии = &СтатусПартииПоОрдеру
	|							ИЛИ ПартииТоваровНаСкладах.СтатусПартии = СписанныеТовары.ДопустимыйСтатус1
	|							ИЛИ ПартииТоваровНаСкладах.СтатусПартии = СписанныеТовары.ДопустимыйСтатус2
	|							ИЛИ ПартииТоваровНаСкладах.СтатусПартии = СписанныеТовары.ДопустимыйСтатус3
	|							ИЛИ ПартииТоваровНаСкладах.СтатусПартии = СписанныеТовары.ДопустимыйСтатус4
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|	
	|		И (ВЫБОР
	|			КОГДА СписанныеТовары.СписыватьТолькоПоЗаказу = ИСТИНА
	|				ТОГДА ВЫБОР
	|						КОГДА ПартииТоваровНаСкладах.Заказ <> СписанныеТовары.ЗаказПартии
	|							ТОГДА ВЫБОР
	|									КОГДА (НЕ СписанныеТовары.ЗаказПартии = НЕОПРЕДЕЛЕНО)
	|										ТОГДА ЛОЖЬ
	|									ИНАЧЕ ПартииТоваровНаСкладах.Заказ = &ПустойЗаказ
	|								КОНЕЦ
	|						ИНАЧЕ ИСТИНА
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ПартииТоваровНаСкладах.Заказ <> СписанныеТовары.ЗаказПартии
	|						ТОГДА ПартииТоваровНаСкладах.Заказ = &ПустойЗаказ
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|		КОНЕЦ)
	|		И (СписанныеТовары.СерияНоменклатуры = ПартииТоваровНаСкладах.СерияНоменклатуры
	|			ИЛИ ПартииТоваровНаСкладах.СерияНоменклатуры = &ПустаяСерияНоменклатуры)
	|ГДЕ
	|	СписанныеТовары.Регистратор = &ОсновнойДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЧислоСерияНоменклатуры,
	|	ЧислоДокументОприходования,
	|	ЧислоЗаказ,
	|	ЧислоСтатусПартии" + ?(СтратегияСтатусПартии = Перечисления.СтретегииСписанияПартийТоваровПоСтатусам.СначалаПринятыеПотомСобственные, " Убыв", "") + ",
	|	ДокументОприходованияДата" + ?(СпособОценкиМПЗ = "ЛИФО", " Убыв","") + ",
	|	ДокументОприходованияМоментВремени" + ?(СпособОценкиМПЗ = "ЛИФО", " Убыв","") + ",
	|	ДокументОприходования" + ?(СпособОценкиМПЗ = "ЛИФО", " Убыв","") + "
	|ИТОГИ ПО
	|	НомерСтрокиДокумента";
	

КонецПроцедуры // ЗаполнитьЗапросПартийНаСкладахУпр()

Процедура ЗаполнитьЗапросПартийНаСкладахДляЗакрытияЗаказовПокупателей(Запрос)

	Запрос.Текст ="ВЫБРАТЬ
	              |	СписанныеТовары.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	              |	ПартииТоваровНаСкладах.Номенклатура,
	              |	ПартииТоваровНаСкладах.ДокументОприходования КАК ДокументОприходования,
	              |	ПартииТоваровНаСкладах.ДокументОприходования.Дата КАК ДокументОприходованияДата,
	              |	ПартииТоваровНаСкладах.Склад,
	              |	ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры,
	              |	ПартииТоваровНаСкладах.СерияНоменклатуры,
	              |	ПартииТоваровНаСкладах.Качество,
	              |	ПартииТоваровНаСкладах.Заказ,
	              |	ПартииТоваровНаСкладах.КоличествоОстаток КАК Количество,
	              |	ПартииТоваровНаСкладах.СтоимостьОстаток КАК Стоимость,
	              |	ПартииТоваровНаСкладах.НДСОстаток КАК НДС,
	              |	ПартииТоваровНаСкладах.СтатусПартии
	              |ИЗ
	              |	РегистрСведений.СписанныеТовары КАК СписанныеТовары
	              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах.Остатки(
	              |		&Дат,
	              |		Номенклатура В
	              |		    (ВЫБРАТЬ
	              |		        РегистрСведений.СписанныеТовары.Номенклатура
	              |		    ИЗ
	              |		        РегистрСведений.СписанныеТовары
	              |		    ГДЕ
	              |		        РегистрСведений.СписанныеТовары.Регистратор = &Ссылка)) КАК ПартииТоваровНаСкладах
	              |		ПО СписанныеТовары.Номенклатура = ПартииТоваровНаСкладах.Номенклатура
	              |			И СписанныеТовары.Номенклатура = ПартииТоваровНаСкладах.Номенклатура
	              |			И СписанныеТовары.ХарактеристикаНоменклатуры = ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры
	              |			И СписанныеТовары.ЗаказПартии = ПартииТоваровНаСкладах.Заказ
	              |ГДЕ
	              |	СписанныеТовары.Регистратор = &Ссылка
	              |ИТОГИ ПО
	              |	НомерСтрокиДокумента";
	

КонецПроцедуры // ЗаполнитьЗапросПартийНаСкладахУпрДляЗакрытияЗаказовПокупателей()

Процедура ЗаполнитьЗапросПартийНаСкладахДляОтложеннойОтгрузкиУпр(Запрос, ВестиПартионныйУчетПоСкладам)

	Запрос.Текст ="ВЫБРАТЬ РАЗРЕШЕННЫЕ
	              |	СписанныеТоварыОрдера.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	              |	ПартииТоваровНаСкладах.Номенклатура,
	              |	ПартииТоваровНаСкладах.Склад,
	              |	ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры,
	              |	ПартииТоваровНаСкладах.СерияНоменклатуры,
	              |	ПартииТоваровНаСкладах.Качество,
				  |	ПартииТоваровНаСкладах.ДокументОприходования,
				  |	ПартииТоваровНаСкладах.Заказ,
	              |	ПартииТоваровНаСкладах.КоличествоОстаток КАК Количество,
	              |	ПартииТоваровНаСкладах.СтоимостьОстаток КАК Стоимость,
	              |	ПартииТоваровНаСкладах.НДСОстаток КАК НДС,
	              |	ПартииТоваровНаСкладах.СтатусПартии
	              |ИЗ
	              |	РегистрСведений.СписанныеТовары КАК СписанныеТовары
	              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах.Остатки(
	              |		&Дат,
	              |		Номенклатура В
	              |		        (ВЫБРАТЬ
	              |		            РегистрСведений.СписанныеТовары.Номенклатура
	              |		        ИЗ
	              |		            РегистрСведений.СписанныеТовары
	              |		        ГДЕ
	              |		            РегистрСведений.СписанныеТовары.Регистратор = &ОсновнойДокумент)
	              |" + ?(ВестиПартионныйУчетПоСкладам, "
	              |		И (Склад В 
	              |		    (ВЫБРАТЬ
	              |		        РегистрСведений.СписанныеТовары.Склад
	              |		    ИЗ
	              |		        РегистрСведений.СписанныеТовары
	              |		    ГДЕ
	              |		        РегистрСведений.СписанныеТовары.Регистратор = &ОсновнойДокумент) ИЛИ Склад = &ПустойСклад)", "") + ") КАК ПартииТоваровНаСкладах
	              |		ПО СписанныеТовары.Номенклатура = ПартииТоваровНаСкладах.Номенклатура
	              |			И СписанныеТовары.ХарактеристикаНоменклатуры = ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры
	              |			И (ПартииТоваровНаСкладах.Качество = СписанныеТовары.Качество
	              |				ИЛИ СписанныеТовары.Качество = &ПустоеКачество)
	              |			" + ?(ВестиПартионныйУчетПоСкладам, "И (ПартииТоваровНаСкладах.Склад = СписанныеТовары.Склад ИЛИ ПартииТоваровНаСкладах.Склад = &ПустойСклад)", "") + "
	              |			И (СписанныеТовары.СерияНоменклатуры = ПартииТоваровНаСкладах.СерияНоменклатуры
	              |				ИЛИ ПартииТоваровНаСкладах.СерияНоменклатуры = &ПустаяСерияНоменклатуры)
	              |			И (ПартииТоваровНаСкладах.СтатусПартии = &СтатусПартииВозвратнаяТараОтложеннаяОтгрузка
				  |				ИЛИ ПартииТоваровНаСкладах.СтатусПартии = &СтатусПартииКупленныйОтложеннаяОтгрузка)
				  |ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СписанныеТовары КАК СписанныеТоварыОрдера
				  |ПО (СписанныеТоварыОрдера.Регистратор = &Ссылка)
				  |И СписанныеТоварыОрдера.Номенклатура = СписанныеТовары.Номенклатура
				  |И СписанныеТоварыОрдера.ХарактеристикаНоменклатуры = СписанныеТовары.ХарактеристикаНоменклатуры "
				  + ?(ВестиПартионныйУчетПоСкладам,"
				  |И (ВЫБОР
				  |	КОГДА СписанныеТоварыОрдера.Регистратор ССЫЛКА Документ.РасходныйОрдерНаТовары
				  |		ТОГДА СписанныеТоварыОрдера.Склад = СписанныеТовары.Склад
				  |	ИНАЧЕ ИСТИНА
				  |КОНЕЦ) ","")+"
				  |И (СписанныеТоварыОрдера.СерияНоменклатуры = ПартииТоваровНаСкладах.СерияНоменклатуры
				  |	ИЛИ ПартииТоваровНаСкладах.СерияНоменклатуры = &ПустаяСерияНоменклатуры)
				  
				  |ГДЕ
	              |	СписанныеТовары.Регистратор = &ОсновнойДокумент
	              |ИТОГИ ПО
	              |	СписанныеТоварыОрдера.НомерСтрокиДокумента";
	
	Запрос.УстановитьПараметр("СтатусПартииВозвратнаяТараОтложеннаяОтгрузка", Перечисления.СтатусыПартийТоваров.ВозвратнаяТараОтложеннаяОтгрузка);
	Запрос.УстановитьПараметр("СтатусПартииКупленныйОтложеннаяОтгрузка", Перечисления.СтатусыПартийТоваров.КупленныйОтложеннаяОтгрузка);

КонецПроцедуры // ЗаполнитьЗапросПартийНаСкладахДляОтложеннойОтгрузкиУпр()

Процедура ЗаполнитьЗапросПартийНаСкладахДляСписанияПоОрдернойСхемеУпр(Запрос,ВестиПартионныйУчетПоСкладам, СтратегияСтатусПартии, СпособОценкиМПЗ)

	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписанныеТоварыОрдера.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ПартииТоваровНаСкладах.Номенклатура,
	|	ПартииТоваровНаСкладах.ДокументОприходования КАК ДокументОприходования,
	|	ПартииТоваровНаСкладах.ДокументОприходования.Дата КАК ДокументОприходованияДата,
	|	ПартииТоваровНаСкладах.Склад,
	|	ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры,
	|	ПартииТоваровНаСкладах.СерияНоменклатуры,
	|	ПартииТоваровНаСкладах.Качество,
	|	ПартииТоваровНаСкладах.Заказ,
	|	ПартииТоваровНаСкладах.КоличествоОстаток КАК Количество,
	|	ПартииТоваровНаСкладах.СтоимостьОстаток КАК Стоимость,
    |	ПартииТоваровНаСкладах.НДСОстаток КАК НДС,
	|	ПартииТоваровНаСкладах.СтатусПартии,
	|	ВЫБОР
	|		КОГДА СписанныеТоварыОрдера.СерияНоменклатуры = ПартииТоваровНаСкладах.СерияНоменклатуры
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЧислоСерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА СписанныеТовары.ДокументПартии = НЕОПРЕДЕЛЕНО
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СписанныеТовары.ДокументПартии = ПартииТоваровНаСкладах.ДокументОприходования
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧислоДокументОприходования,
	|	ВЫБОР
	|		КОГДА СписанныеТовары.ЗаказСписания = НЕОПРЕДЕЛЕНО
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПартииТоваровНаСкладах.Заказ = &ПустойЗаказ
	|					ТОГДА 1
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧислоЗаказ,
	|	ВЫБОР
	|		КОГДА ПартииТоваровНаСкладах.СтатусПартии = &НаКомиссию
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЧислоСтатусПартии
	|ИЗ
	|	РегистрСведений.СписанныеТовары КАК СписанныеТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах.Остатки(
	|		&Дат,
	|		Номенклатура В
	|		    (ВЫБРАТЬ
	|		        РегистрСведений.СписанныеТовары.Номенклатура
	|		    ИЗ
	|		        РегистрСведений.СписанныеТовары
	|		    ГДЕ
	|		        РегистрСведений.СписанныеТовары.Регистратор = &Ссылка)" + ?(ВестиПартионныйУчетПоСкладам, "
	|		И (Склад В 
	|		    (ВЫБРАТЬ
	|		        РегистрСведений.СписанныеТовары.Склад
	|		    ИЗ
	|		        РегистрСведений.СписанныеТовары
	|		    ГДЕ
	|		        РегистрСведений.СписанныеТовары.Регистратор = &ОсновнойДокумент) ИЛИ Склад = &ПустойСклад)", "") + ") КАК ПартииТоваровНаСкладах
	|		ПО СписанныеТовары.Номенклатура = ПартииТоваровНаСкладах.Номенклатура
	|			И СписанныеТовары.ХарактеристикаНоменклатуры = ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры
	|			И (ВЫБОР
	|				КОГДА ПартииТоваровНаСкладах.Качество = &ПустоеКачество
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВЫБОР
	|						КОГДА СписанныеТовары.Качество = &ПустоеКачество
	|							ТОГДА ПартииТоваровНаСкладах.Качество = &КачествоНовый
	|						ИНАЧЕ ПартииТоваровНаСкладах.Качество = СписанныеТовары.Качество
	|					КОНЕЦ
	|			КОНЕЦ)
	|			" + ?(ВестиПартионныйУчетПоСкладам, "И (ПартииТоваровНаСкладах.Склад = СписанныеТовары.Склад ИЛИ ПартииТоваровНаСкладах.Склад = &ПустойСклад)", "") + "
	|			И (ВЫБОР
	|				КОГДА СписанныеТовары.ДопустимыйСтатус1 <> &ПустойСтатус
	|						ИЛИ СписанныеТовары.ДопустимыйСтатус2 <> &ПустойСтатус
	|						ИЛИ СписанныеТовары.ДопустимыйСтатус3 <> &ПустойСтатус
	|						ИЛИ СписанныеТовары.ДопустимыйСтатус4 <> &ПустойСтатус
	|					ТОГДА ПартииТоваровНаСкладах.СтатусПартии = &ПустойСтатус
	|							ИЛИ ПартииТоваровНаСкладах.СтатусПартии = &СтатусПартииПоОрдеру
	|							ИЛИ ПартииТоваровНаСкладах.СтатусПартии = СписанныеТовары.ДопустимыйСтатус1
	|							ИЛИ ПартииТоваровНаСкладах.СтатусПартии = СписанныеТовары.ДопустимыйСтатус2
	|							ИЛИ ПартииТоваровНаСкладах.СтатусПартии = СписанныеТовары.ДопустимыйСтатус3
	|							ИЛИ ПартииТоваровНаСкладах.СтатусПартии = СписанныеТовары.ДопустимыйСтатус4
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|	
	|		И (ВЫБОР
	|			КОГДА СписанныеТовары.СписыватьТолькоПоЗаказу = ИСТИНА
	|				ТОГДА ВЫБОР
	|						КОГДА ПартииТоваровНаСкладах.Заказ <> СписанныеТовары.ЗаказПартии
	|							ТОГДА ВЫБОР
	|									КОГДА (НЕ СписанныеТовары.ЗаказПартии = НЕОПРЕДЕЛЕНО)
	|										ТОГДА ЛОЖЬ
	|									ИНАЧЕ ПартииТоваровНаСкладах.Заказ = &ПустойЗаказ
	|								КОНЕЦ
	|						ИНАЧЕ ИСТИНА
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ПартииТоваровНаСкладах.Заказ <> СписанныеТовары.ЗаказПартии
	|						ТОГДА ПартииТоваровНаСкладах.Заказ = &ПустойЗаказ
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|		КОНЕЦ)
	|		И (СписанныеТовары.СерияНоменклатуры = ПартииТоваровНаСкладах.СерияНоменклатуры
	|			ИЛИ ПартииТоваровНаСкладах.СерияНоменклатуры = &ПустаяСерияНоменклатуры
	|				ИЛИ СписанныеТовары.СерияНоменклатуры = &ПустаяСерияНоменклатуры)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СписанныеТовары КАК СписанныеТоварыОрдера
	|		ПО (СписанныеТоварыОрдера.Регистратор = &Ссылка)
	|			И СписанныеТоварыОрдера.Номенклатура = СписанныеТовары.Номенклатура
	|			И СписанныеТоварыОрдера.ХарактеристикаНоменклатуры = СписанныеТовары.ХарактеристикаНоменклатуры
	|			И (ВЫБОР
	|				КОГДА СписанныеТоварыОрдера.Регистратор ССЫЛКА Документ.РасходныйОрдерНаТовары
	|					ТОГДА СписанныеТоварыОрдера.Склад = СписанныеТовары.Склад
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|			И (СписанныеТоварыОрдера.СерияНоменклатуры = ПартииТоваровНаСкладах.СерияНоменклатуры
	|				ИЛИ ПартииТоваровНаСкладах.СерияНоменклатуры = &ПустаяСерияНоменклатуры)
	|ГДЕ
	|	СписанныеТовары.Регистратор = &ОсновнойДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЧислоСерияНоменклатуры,
	|	ЧислоДокументОприходования,
	|	ЧислоЗаказ,
	|	ЧислоСтатусПартии" + ?(СтратегияСтатусПартии = Перечисления.СтретегииСписанияПартийТоваровПоСтатусам.СначалаПринятыеПотомСобственные, " Убыв", "") + ",
	|	ДокументОприходованияДата" + ?(СпособОценкиМПЗ = "ЛИФО", " Убыв","") + ",
	|	ДокументОприходования" + ?(СпособОценкиМПЗ = "ЛИФО", " Убыв","") + "
	|ИТОГИ ПО
	|	НомерСтрокиДокумента";

КонецПроцедуры // ЗаполнитьЗапросПартийНаСкладахДляСписанияПоОрдернойСхемеУпр()

Функция ПолучитьДеревоПартийНаСкладахУпр (МоментКон, СтруктураПараметров)

	Запрос = Новый Запрос;
	
	ОсновнойДокумент = Неопределено;
	СтруктураПараметров.Свойство("ОсновнойДокумент",ОсновнойДокумент);
	
	Регистратор = СтруктураПараметров.Регистратор;
	СпособОценкиМПЗ = СтруктураПараметров.СпособОценкиМПЗУпр;
	СтратегияСтатусПартии = СтруктураПараметров.СтратегияСтатусПартииУпр;
	ВестиПартионныйУчетПоСкладам = СтруктураПараметров.ВестиПартионныйУчетПоСкладамУпр;

	// Для повышения быстродействия остатки партий получаются различными способами 
	Если СтруктураПараметров.Свойство("ЗакрытиеЗаказовПокупателей") Тогда
		ЗаполнитьЗапросПартийНаСкладахДляЗакрытияЗаказовПокупателей(Запрос);
		
	ИначеЕсли ОсновнойДокумент <> Неопределено И НЕ СтруктураПараметров.СписыватьПартииРасходнымОрдером
		И ТипЗнч(ОсновнойДокумент) = Тип("ДокументСсылка.РеализацияТоваровУслуг")тогда
		// Списание расходным ордером товара реализованного и принятого на ответственное хранение (отложенная отгрузка)
		ЗаполнитьЗапросПартийНаСкладахДляОтложеннойОтгрузкиУпр(Запрос, ВестиПартионныйУчетПоСкладам);
	
	ИначеЕсли ОсновнойДокумент <> Неопределено тогда
		// Списание партий по ордерной схеме: 
		// - Списание партий по расходному ордеру
		// - Перемещение партий по приходному ордеру
		// - Перемещение партий поступлением товаров и услуг в НТТ
		// Движения реализации выполняет расходный ордер, движения перемещения выполняет приходный ордер
		ЗаполнитьЗапросПартийНаСкладахДляСписанияПоОрдернойСхемеУпр(Запрос, ВестиПартионныйУчетПоСкладам, СтратегияСтатусПартии, СпособОценкиМПЗ);
		
	Иначе
		// Общий случай списания
		ЗаполнитьЗапросПартийНаСкладахУпр(Запрос, ВестиПартионныйУчетПоСкладам, СтратегияСтатусПартии, СпособОценкиМПЗ);
		Если НЕ СтруктураПараметров.ИспользоватьУказаниеСерийНоменклатурыПриРезервировании тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"ИЛИ ПартииТоваровНаСкладах.СерияНоменклатуры = &ПустаяСерияНоменклатуры",
			"ИЛИ ПартииТоваровНаСкладах.СерияНоменклатуры = &ПустаяСерияНоменклатуры
			|ИЛИ СписанныеТовары.КодОперацииПартииТоваров = &КодРезервирование");
			Запрос.УстановитьПараметр("КодРезервирование" , СтруктураПараметров.КодыОпераций.РезервированиеПодЗаказ)
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПустаяСерияНоменклатуры", Справочники.СерииНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойЗаказ", Документы.ЗаказПокупателя.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойСтатус", Перечисления.СтатусыПартийТоваров.ПустаяСсылка());
	Запрос.УстановитьПараметр("СтатусПартииПоОрдеру", Перечисления.СтатусыПартийТоваров.ПоОрдеру);	
	Запрос.УстановитьПараметр("ПустоеКачество", Справочники.Качество.ПустаяСсылка());
	Запрос.УстановитьПараметр("КачествоНовый", Справочники.Качество.Новый);
	Запрос.УстановитьПараметр("ПустойСклад", Справочники.Склады.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("Ссылка", Регистратор);
	
	Если  ОсновнойДокумент <> Неопределено Тогда
		Запрос.УстановитьПараметр("ОсновнойДокумент", ОсновнойДокумент);
	Иначе
		Запрос.УстановитьПараметр("ОсновнойДокумент", Регистратор);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дат", МоментКон);
	
	Запрос.УстановитьПараметр("НаКомиссию", Перечисления.СтатусыПартийТоваров.НаКомиссию);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);

КонецФункции//ПолучитьДеревоПартийНаСкладахУпр

Функция ПолучитьДеревоПартийПереданныеУпр(МоментКон, Регистратор, ДоговорКонтрагента, СпособОценкиМПЗ, СтратегияСтатусПартии)

	Запрос = Новый Запрос;
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписанныеТовары.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ПартииТоваровПереданные.ДоговорКонтрагента,
	|	ПартииТоваровПереданные.ДокументПередачи,
	|	ПартииТоваровПереданные.ДокументОприходования,
	|	ПартииТоваровПереданные.ДокументОприходования.Представление КАК ДокументОприходованияПредставление,
	|	ПартииТоваровПереданные.ДокументОприходования.Дата КАК ДокументОприходованияДата,
	|	ПартииТоваровПереданные.Номенклатура,
	|	ПартииТоваровПереданные.ХарактеристикаНоменклатуры,
	|	ПартииТоваровПереданные.КоличествоОстаток КАК Количество,
	|	ПартииТоваровПереданные.СтоимостьОстаток КАК Стоимость,
	|	ПартииТоваровПереданные.НДСОстаток КАК НДС,
	|	ПартииТоваровПереданные.СтатусПартии,
	|	ПартииТоваровПереданные.СтатусПередачи,
	|	ВЫБОР
	|		КОГДА ПартииТоваровПереданные.СтатусПартии = &НаКомиссию
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЧислоСтатусПартии,
	|	ВЫБОР
	|		КОГДА СписанныеТовары.ДокументПартии = НЕОПРЕДЕЛЕНО
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СписанныеТовары.ДокументПартии = ПартииТоваровПереданные.ДокументОприходования
	|					ТОГДА 0
	|				ИНАЧЕ 1
	|			КОНЕЦ
	|	КОНЕЦ КАК ЧислоДокументОприходования,
	|	ВЫБОР
	|		КОГДА ПартииТоваровПереданные.ДокументПередачи = СписанныеТовары.ДокументПередачи
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЧислоДокументПередачи
	|ИЗ
	|	РегистрСведений.СписанныеТовары КАК СписанныеТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровПереданные.Остатки(&Дат,	
	|																					Номенклатура В
	|																					    (ВЫБРАТЬ
	|																					        РегистрСведений.СписанныеТовары.Номенклатура
	|																					    ИЗ
	|																					        РегистрСведений.СписанныеТовары
	|																					    ГДЕ
	|																					        РегистрСведений.СписанныеТовары.Регистратор = &Ссылка)" +
																						?(ДоговорКонтрагента = Неопределено, "", "
	|																					 	И ДоговорКонтрагента = &ДоговорКонтрагента") + "
	|																				) КАК ПартииТоваровПереданные
	|		ПО СписанныеТовары.Номенклатура = ПартииТоваровПереданные.Номенклатура
	|			И СписанныеТовары.ХарактеристикаНоменклатуры = ПартииТоваровПереданные.ХарактеристикаНоменклатуры
	|			И (ПартииТоваровПереданные.СтатусПартии = &ПустойСтатус
	|				ИЛИ ПартииТоваровПереданные.СтатусПартии = &СтатусПартииПоОрдеру
	|				ИЛИ ПартииТоваровПереданные.СтатусПартии = СписанныеТовары.ДопустимыйСтатус1
	|				ИЛИ ПартииТоваровПереданные.СтатусПартии = СписанныеТовары.ДопустимыйСтатус2
	|				ИЛИ ПартииТоваровПереданные.СтатусПартии = СписанныеТовары.ДопустимыйСтатус3
	|				ИЛИ ПартииТоваровПереданные.СтатусПартии = СписанныеТовары.ДопустимыйСтатус4)
	|			И (ВЫБОР
	|				КОГДА СписанныеТовары.КодОперацииПартииТоваров = &СписаниеПартийПереданныхВПроизводство
	|						ИЛИ СписанныеТовары.КодОперацииПартииТоваров = &ВозвратОтПереработчика
	|					ТОГДА СписанныеТовары.ЗаказПартии = ПартииТоваровПереданные.ДокументПередачи.Сделка
	|							ИЛИ ПартииТоваровПереданные.ДокументПередачи.Сделка = &ЗаказПоставщикуПустаяСсылка
	|								И СписанныеТовары.ЗаказПартии = НЕОПРЕДЕЛЕНО
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)  	
	|ГДЕ
	|	СписанныеТовары.Регистратор = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЧислоДокументПередачи,
	|	ЧислоДокументОприходования,
	|	ЧислоСтатусПартии " + ?(СтратегияСтатусПартии = Перечисления.СтретегииСписанияПартийТоваровПоСтатусам.СначалаПринятыеПотомСобственные, "Убыв", "") + ",
	|	ДокументОприходованияДата" + ?(СпособОценкиМПЗ = "ЛИФО", " Убыв","") + ",
	|	ДокументОприходования" + ?(СпособОценкиМПЗ = "ЛИФО", " Убыв","") + "
	|ИТОГИ ПО
	|	НомерСтрокиДокумента";
	
	Запрос.УстановитьПараметр("ПустойСтатус", Перечисления.СтатусыПартийТоваров.ПустаяСсылка());	
	Запрос.УстановитьПараметр("Ссылка", Регистратор);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("Дат", МоментКон);
	Запрос.УстановитьПараметр("НаКомиссию", Перечисления.СтатусыПартийТоваров.НаКомиссию);	
	Запрос.УстановитьПараметр("СтатусПартииПоОрдеру", Перечисления.СтатусыПартийТоваров.ПоОрдеру);
	Запрос.УстановитьПараметр("ЗаказПоставщикуПустаяСсылка", Документы.ЗаказПоставщику.ПустаяСсылка());
	Запрос.УстановитьПараметр("СписаниеПартийПереданныхВПроизводство", Перечисления.КодыОперацийПартииТоваров.СписаниеПартийПереданныхВПроизводство);
	Запрос.УстановитьПараметр("ВозвратОтПереработчика", Перечисления.КодыОперацийПартииТоваров.ВозвратОтПереработчика);
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
 
КонецФункции

// Остатки партий на складах по упр.учету
//
// Параметры:
//	СтруктураПараметров
//	МоментКон
//
Процедура ПолучитьОстаткиПартийНаСкладахУпр(СтруктураПараметров, МоментКон)
	
	Если СтруктураПараметров.ЕстьСтрокиОтражатьВУправленческомУчете Тогда
		
		СтруктураПараметров.Вставить("ДеревоПартииТоваровНаСкладахУпр", 
			ПолучитьДеревоПартийНаСкладахУпр (МоментКон,СтруктураПараметров));
		
		СтруктураИзмерений = Новый Структура;
		ТаблицаОстатковПартий = Новый ТаблицаЗначений;
		Для Каждого Измерение ИЗ Метаданные.РегистрыНакопления.ПартииТоваровНаСкладах.Измерения Цикл
			СтруктураИзмерений.Вставить(Измерение.Имя);
			ТаблицаОстатковПартий.Колонки.Добавить(Измерение.Имя);
		КонецЦикла;
		ТаблицаОстатковПартий.Колонки.Добавить("Количество");
		ТаблицаОстатковПартий.Колонки.Добавить("Стоимость");
		ТаблицаОстатковПартий.Колонки.Добавить("НДС");
		
		СтруктураПараметров.Вставить("ПартииТоваровНаСкладахУпр" + "СтруктураИзмерений", СтруктураИзмерений);
		СтруктураПараметров.Вставить("ПартииТоваровНаСкладахУпр" + "ТаблицаОстатков", ТаблицаОстатковПартий);
		
	КонецЕсли;
	
КонецПроцедуры // ПолучитьОстаткиПартийНаСкладахУпр()

Процедура ПолучитьОстаткиПартийПереданныхУпр(СтруктураПараметров, МоментКон, ДоговорКонтрагента)
	
	Если СтруктураПараметров.ЕстьСтрокиОтражатьВУправленческомУчете Тогда
		СтруктураПараметров.Вставить("ДеревоПартииТоваровПереданныеУпр", ПолучитьДеревоПартийПереданныеУпр(МоментКон, СтруктураПараметров.Регистратор, ДоговорКонтрагента, СтруктураПараметров.СпособОценкиМПЗУпр, СтруктураПараметров.СтратегияСтатусПартииУпр));
		
		СтруктураИзмерений = Новый Структура;
		ТаблицаОстатковПартий = Новый ТаблицаЗначений;
		Для Каждого Измерение ИЗ Метаданные.РегистрыНакопления.ПартииТоваровПереданные.Измерения Цикл
			СтруктураИзмерений.Вставить(Измерение.Имя);
			ТаблицаОстатковПартий.Колонки.Добавить(Измерение.Имя);
		КонецЦикла;
		ТаблицаОстатковПартий.Колонки.Добавить("Количество");
		ТаблицаОстатковПартий.Колонки.Добавить("Стоимость");
		ТаблицаОстатковПартий.Колонки.Добавить("НДС");
		
		СтруктураПараметров.Вставить("ПартииТоваровПереданныеУпр" + "СтруктураИзмерений", СтруктураИзмерений);
		СтруктураПараметров.Вставить("ПартииТоваровПереданныеУпр" + "ТаблицаОстатков", ТаблицаОстатковПартий);
	
	КонецЕсли;
	
КонецПроцедуры // ПолучитьОстаткиПартийПереданныхУпр()

// Получение остатков по упр. учету
//
// Параметры:
//	Нет.
//
Процедура ПолучитьОстаткиУпр(СтруктураПараметров, ТаблицаСписания, МоментКон)
	
	// По партионному учету остатки берутся из двух регистров
		
	Если СтруктураПараметров.ЕстьНаСкладах Тогда
		
		ПолучитьОстаткиПартийНаСкладахУпр(СтруктураПараметров, МоментКон);
		
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьПереданные Тогда
		
		ПолучитьОстаткиПартийПереданныхУпр(СтруктураПараметров, МоментКон,ТаблицаСписания[0].ДоговорКонтрагента);
		
	КонецЕсли;
	
КонецПроцедуры // ПолучитьОстаткиУпр()

Функция ПолучитьСтрокуОстатковПартий(СтрокаПартииРаспределения, СтруктураИзмерений, ТаблицаОстатковПартий)
	
	ЗаполнитьЗначенияСвойств(СтруктураИзмерений, СтрокаПартииРаспределения);
	МассивСтрок = ТаблицаОстатковПартий.НайтиСтроки(СтруктураИзмерений);
	Если МассивСтрок.Количество() = 0 Тогда
		СтрокаТаблицы = ТаблицаОстатковПартий.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаПартииРаспределения);
	Иначе
		СтрокаТаблицы = МассивСтрок[0];
	КонецЕсли;
	
	Возврат СтрокаТаблицы;
		
Конецфункции

// ПОЛУЧЕНИЕ ОСТАТКОВ ИЗ РЕГИСТРОВ ПАРТИЙ
////////////////////////////////////////////////////////////////////////////////

Процедура ВыполнитьВосстановлениеНаСервере (ДатаКонцаОбработки,
										   Организация,
										   ОтражатьВУправленческомУчете,
										   ОтражатьВБухгалтерскомУчете,
										   ОтражатьВНалоговомУчете,
										   Останавливаться,
										   МаксимальноеКоличествоДокументовВТранзакции,
										   МаксимальноеКоличествоСтрокВТранзакции) Экспорт

	ЗаполнениеДокументов.ВыполнитьВосстановление (ДатаКонцаОбработки,
						Организация,
						ОтражатьВУправленческомУчете,
						ОтражатьВБухгалтерскомУчете,
						ОтражатьВНалоговомУчете,
						Останавливаться,
						МаксимальноеКоличествоДокументовВТранзакции,
						МаксимальноеКоличествоСтрокВТранзакции);

КонецПроцедуры //ВыполнитьВосстановлениеНаСервере

// Списание со склада по строке
//
// Параметры
//  СтруктураПараметров  – Структура, содержащая общие параметры
//
Процедура СписаниеСоСклада(СтрокаДокумента, СтруктураПараметров, Отказ)

	// Партии для данного учета
	ДеревоОстатковПартий = СтруктураПараметров["ДеревоПартийТоваровНаСкладахБух"];
	
	// Переменная определяет списывать номенклатуру исходя из стоимости остатка
	// номенклатуры на счете или по указанной в параметрах стоимости.
	СписыватьПоУказаннойСтоимости = Ложь;
	УказаннаяСтоимость = 0;
	
	
	// Переменная определяет, нужно ли изменять количество при списании
    ИзменятьКоличество = СтрокаДокумента.ИзменятьКоличество;
	

	СчетУчета = СтрокаДокумента.СчетУчетаБУ;
	СтрокаДокумента.СписанныеПартииБУ = ПустаяТаблицаСписанныхПартий();
	ТаблицаСписанныхПартии = СтрокаДокумента.СписанныеПартииБУ;
	
	// Если стоимость списания указана, то списывать надо по указанной стоимости.
	СписыватьПоУказаннойСтоимости = СтрокаДокумента.СписыватьПоУказаннойСтоимости;
	
	Если СписыватьПоУказаннойСтоимости Тогда
		УказаннаяСтоимость = СтрокаДокумента.СуммаБезНДС;
	КонецЕсли; 
	
	// Подлежащее погашению при списании количество
	КоличествоОсталосьПогасить = Окр(СтрокаДокумента.Количество,3,1);
	
	Если ИзменятьКоличество Тогда
		КоличествоВсегоНужноПогасить     = Окр(СтрокаДокумента.Количество,3,1);
		КоличествоВсегоНужноРаспределить = Окр(СтрокаДокумента.КоличествоНовое,3,1);
		КоличествоОсталосьРаспределить   = КоличествоВсегоНужноРаспределить;
	КонецЕсли;

	Если ДеревоОстатковПартий.Строки.Количество() <> 0 Тогда

		// Если указана конкретная партия и на счете учета ведется
		// партионный учет, то в структура отбора добавим партию.
		ОтборПоДокументуОприходования = Неопределено;
		Если ЗначениеЗаполнено(СтрокаДокумента.ДокументОприходования) Тогда
				

			Если (НаСчетеВедетсяПартионныйУчет(СчетУчета) 
				И НЕ СтруктураПараметров.СпособОценкиБух = Перечисления.СпособыОценки.ПоСредней) Тогда
				
				ОтборПоДокументуОприходования = СтрокаДокумента.ДокументОприходования;
				
			КонецЕсли;
		КонецЕсли;

		// Если указан конкретный договор и на счете учета ведется
		// партионный учет, то в структуру отбора добавим договор поставщика.
		ОтборПоДоговоруПостащика = Неопределено;
		Если ЗначениеЗаполнено(СтрокаДокумента.ДоговорПоставщика) Тогда
			
			Если (НаСчетеВедетсяПартионныйУчет(СчетУчета) 
				И НЕ СтруктураПараметров.СпособОценкиБух = Перечисления.СпособыОценки.ПоСредней) Тогда
				
				ОтборПоДоговоруПостащика = СтрокаДокумента.ДоговорПоставщика;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если СчетУчета.НалоговыйУчет Тогда
			ОтборПоНалоговомуНазначению = СтрокаДокумента.НалоговоеНазначение;
		Иначе
			ОтборПоНалоговомуНазначению = NULL;
		КонецЕсли;
		
		// Полученную таблицу отсортируем в соответствии со стратегией
		Если КомиссионныйТовар(СчетУчета) Тогда
			// Для товаров, принятых на комиссию, в качестве стратегиии
			// списания устанавливается стратегия - ФИФО.
			СпособОценкиМПЗ = Перечисления.СпособыОценки.ФИФО;
			
		Иначе
		
			СпособОценкиМПЗ = СтруктураПараметров["СпособОценкиБух"];

		КонецЕсли;

		// Пустой склад покупателя - значение измерения "Склад" когда не ведется учет по складам (или не ведется суммовой учет по складам)
		ПустойСклад = Справочники.Склады.ПустаяСсылка();
		
		Если  СпособОценкиМПЗ = Перечисления.СпособыОценки.ЛИФО Тогда
			НапрДата = "Убыв";
		ИначеЕсли СпособОценкиМПЗ = Перечисления.СпособыОценки.ФИФО Тогда
			НапрДата = "Возр";
		Иначе
			НапрДата = "Убыв";
		КонецЕсли;
		
		СтрокаНоменклатуры = ДеревоОстатковПартий.Строки.Найти(СтрокаДокумента.Номенклатура, "Номенклатура");
					
		ПартийПоСтроке = ?(СтрокаНоменклатуры = Неопределено, -1, СтрокаНоменклатуры.Строки.Количество() - 1);
		
		Если СтруктураПараметров.ВестиУчетПоДопРазрезамРегл Тогда
			Для индекс = 0 по ПартийПоСтроке Цикл
				
				ЗаказПокупателяПартии = СтрокаНоменклатуры.Строки[индекс].ЗаказПокупателя;
				Если НЕ ЗначениеЗаполнено(ЗаказПокупателяПартии) Тогда
					ЗаказПокупателяПартии = Документы.ЗаказПокупателя.ПустаяСсылка();
				КонецЕсли;
				
				ЗаказПокупателяДокумента = СтрокаДокумента.ЗаказПокупателя;
				Если НЕ ЗначениеЗаполнено(ЗаказПокупателяДокумента) ИЛИ ТипЗнч(ЗаказПокупателяДокумента) <> Тип("ДокументСсылка.ЗаказПокупателя") Тогда
					ЗаказПокупателяДокумента = Документы.ЗаказПокупателя.ПустаяСсылка();
				ИначеЕсли Не ЗаказПокупателяДокумента.ДоговорКонтрагента.ОбособленныйУчетТоваровПоЗаказамПокупателей Тогда
					ЗаказПокупателяДокумента = Документы.ЗаказПокупателя.ПустаяСсылка();
				КонецЕсли;
				
				Если ЗаказПокупателяПартии = ЗаказПокупателяДокумента Тогда
					СтрокаНоменклатуры.Строки[индекс].СовпалЗаказ = 1;
				КонецЕсли;
				
			КонецЦикла;
			Если (ПартийПоСтроке > 0) Тогда
				СтрокаНоменклатуры.Строки.Сортировать("СовпалЗаказ Убыв, ДокументОприходованияДата " + НапрДата);
			КонецЕсли;
		КонецЕсли;
        
		Для индекс = 0 по ПартийПоСтроке Цикл
			
			СтрокаПартии = СтрокаНоменклатуры.Строки[индекс];
			
			Если СтрокаПартии.СчетУчета <> СчетУчета Тогда
					Продолжить;
				КонецЕсли;
			
			Если СтруктураПараметров.ВестиУчетПоДопРазрезамРегл Тогда
				
				Если НЕ ЗначениеЗаполнено(СтрокаПартии.ХарактеристикаНоменклатуры) И НЕ ЗначениеЗаполнено(СтрокаДокумента.ХарактеристикаНоменклатуры) Тогда
				ИначеЕсли СтрокаПартии.ХарактеристикаНоменклатуры <> СтрокаДокумента.ХарактеристикаНоменклатуры Тогда
					Продолжить;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(СтрокаПартии.СерияНоменклатуры) И НЕ ЗначениеЗаполнено(СтрокаДокумента.СерияНоменклатуры) Тогда
				ИначеЕсли СтрокаПартии.СерияНоменклатуры <> СтрокаДокумента.СерияНоменклатуры Тогда
					Продолжить;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(СтрокаПартии.Качество) Тогда
					КачествоПартии = Справочники.Качество.Новый;
				Иначе
					КачествоПартии = СтрокаПартии.Качество;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(СтрокаДокумента.Качество) Тогда
					КачествоДокумента = Справочники.Качество.Новый;
				Иначе 
					КачествоДокумента = СтрокаДокумента.Качество;
				КонецЕсли;
				
				Если КачествоПартии <> КачествоДокумента Тогда
					Продолжить;
				КонецЕсли;
				
				Если СтрокаДокумента.СписыватьТолькоПоЗаказуРегл Тогда
					Если (СтрокаПартии.СовпалЗаказ = 0) Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
			Если ОтборПоДокументуОприходования <> Неопределено Тогда
				
				Если СтрокаПартии.ДокументОприходования <> ОтборПоДокументуОприходования Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ОтборПоНалоговомуНазначению <> Неопределено Тогда
				Если СтрокаПартии.НалоговоеНазначение <> ОтборПоНалоговомуНазначению Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если (ОтборПоДоговоруПостащика <> Неопределено) И (ЗначениеЗаполнено(СтрокаПартии.ДокументОприходования)) Тогда
				МетаданныеДокумента = СтрокаПартии.ДокументОприходования.Метаданные();
				Если ОбщегоНазначения.ЕстьРеквизитДокумента("ДоговорКонтрагента", МетаданныеДокумента) 
					И (НЕ ТипЗнч(СтрокаПартии.ДокументОприходования) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")) Тогда
					
					Если СтрокаПартии.ДокументОприходования.ДоговорКонтрагента <> ОтборПоДоговоруПостащика Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

			Если КоличествоОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;

			// Проверки условий:

			// Количество по строке больше 0
			Если НЕ СтрокаПартии.Количество > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			// Склад, если ведется суммовой учет по складам
			Если ВедетсяСуммовойУчетПоСкладам(СчетУчета) Тогда

				Если СтрокаПартии.Склад <> СтрокаДокумента.Склад 
				   И СтрокаПартии.Склад <> ПустойСклад Тогда // пустые склады могут остаться со времени, когда не было учета по складам
					Продолжить;

				КонецЕсли;
						
			КонецЕсли;

			Если СтрокаПартии.Количество >= КоличествоОсталосьПогасить Тогда
				КоэффСписания = КоличествоОсталосьПогасить / СтрокаПартии.Количество;
			Иначе
				КоэффСписания = 1;
			КонецЕсли;

			Количество = Окр(СтрокаПартии.Количество * КоэффСписания,3,1);
			
			Стоимость  = Окр(СтрокаПартии.Стоимость  * КоэффСписания,2,1);
			СтоимостьНУ  = Окр(СтрокаПартии.СтоимостьНУ  * КоэффСписания,2,1);
			
			Если ИзменятьКоличество Тогда
				КоличествоНовое = Окр(КоличествоВсегоНужноРаспределить*Количество/КоличествоВсегоНужноПогасить,3,1);
				КоличествоОсталосьРаспределить = КоличествоОсталосьРаспределить - КоличествоНовое;
			Иначе
				КоличествоНовое = Количество;
			КонецЕсли;
			
			Если СписыватьПоУказаннойСтоимости Тогда // списывать по стоимост, указанной в параметрах
				Стоимость = Окр(УказаннаяСтоимость  * Количество / КоличествоОсталосьПогасить,2,1);
				СтоимостьНУ = Стоимость;
				УказаннаяСтоимость = УказаннаяСтоимость - Стоимость;
				
			Иначе  // списывать исходы из стоиммостного остатка номенклатуры на складе
			//	Стоимость = Окр(СтрокаПартии.Стоимость  * КоэффСписания,2,1);
			КонецЕсли;

			КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - Количество;

			Если ИзменятьКоличество Тогда
				Если (КоличествоОсталосьПогасить = 0) Тогда
					КоличествоНовое = КоличествоНовое + КоличествоОсталосьРаспределить;
					КоличествоОсталосьРаспределить = 0;
				КонецЕсли;
			КонецЕсли;
			
			СтрокаПартии.Количество = СтрокаПартии.Количество - Количество;
			СтрокаПартии.Стоимость  = СтрокаПартии.Стоимость  - Стоимость;
			СтрокаПартии.СтоимостьНУ  = СтрокаПартии.СтоимостьНУ - СтоимостьНУ;
			
			Проводка = СтрокаДокумента.Регистратор.Движения.Хозрасчетный.Добавить();
			Проводка.Период      = СтрокаДокумента.Регистратор.Дата;
			Проводка.Организация = СтрокаДокумента.Организация;
			Проводка.Содержание = СтрокаДокумента.СодержаниеПроводки;
			Проводка.СчетКт = СчетУчета;
			
			НоменклатурнаяПозицияСтроки = СтрокаПартии.НоменклатурнаяПозиция;
			
			// Балансовый счет учета не может корреспондировать с забалансовым
			Если СтрокаДокумента.КорСчетСписанияБУ.Забалансовый = СчетУчета.Забалансовый Тогда
				Проводка.СчетДт = СтрокаДокумента.КорСчетСписанияБУ;
				
				Если НаСчетеВедетсяПартионныйУчет(Проводка.СчетДт) Тогда
					Проводка.СубконтоДт.Партии = СтрокаПартии.ДокументОприходования;
				КонецЕсли;
				Если НаСчетеВедетсяУчетПоОтгрузкам(Проводка.СчетДт) Тогда
					Проводка.СубконтоДт.ДокументыРасчетовСКонтрагентами = СтрокаПартии.ДокументОтгрузки;
				КонецЕсли;
				
			КонецЕсли;
			
			Если (СтрокаДокумента.КорСубконтоСписанияБУ4 = Неопределено) Тогда
				СтрокаДокумента.КорСубконтоСписанияБУ4 = НоменклатурнаяПозицияСтроки;
			КонецЕсли;
			
			ЗаполнитьСубконто(Проводка, СтрокаДокумента, 
			СтрокаДокумента.КорСубконтоСписанияБУ1,
			СтрокаДокумента.КорСубконтоСписанияБУ2,
			СтрокаДокумента.КорСубконтоСписанияБУ3,
			СтрокаДокумента.КорСубконтоСписанияБУ4,
			СтрокаПартии.ДокументОприходования,
			НоменклатурнаяПозицияСтроки,,);
			
			Если НаСчетеВедетсяУчетПоОтгрузкам(Проводка.СчетКт) Тогда
				Проводка.СубконтоКт.ДокументыРасчетовСКонтрагентами = СтрокаПартии.ДокументОтгрузки;
			КонецЕсли;
			Проводка.Сумма = Стоимость;
			
			ПроводкаДо2015 = Проводка.Период < глЗначениеПеременной("ДатаНКУ2015");
			Если ПроводкаДо2015 Тогда
				
				Если Проводка.СчетДт.НалоговыйУчет Тогда
					Проводка.НалоговоеНазначениеДт = СтрокаДокумента.НалоговоеНазначениеНовое;
					
					Если Проводка.НалоговоеНазначениеДт = Справочники.НалоговыеНазначенияАктивовИЗатрат.НКУ_НеХозДеятельность
						ИЛИ Проводка.НалоговоеНазначениеДт = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность
						Тогда
						Проводка.СуммаНУДт = 0;
					Иначе
						Проводка.СуммаНУДт = СтоимостьНУ;
					КонецЕсли;
					
				КонецЕсли;
				Если Проводка.СчетКт.НалоговыйУчет Тогда
					Проводка.СуммаНУКт = СтоимостьНУ;
					Проводка.НалоговоеНазначениеКт = СтрокаДокумента.НалоговоеНазначение;
				КонецЕсли;
			
			Иначе
				Если Проводка.СчетДт.НалоговыйУчет Тогда
					Если Проводка.СчетДт.УчетПоНалоговымНазначениямНДС Тогда
						Проводка.НалоговоеНазначениеДт = СтрокаДокумента.НалоговоеНазначениеНовое;
					КонецЕсли;  
					
					Если (СтруктураПараметров.ЕстьНалогНаПрибыльНал = Истина) И Проводка.СчетДт.УчетСуммНУ Тогда
						Если Проводка.СчетКт.УчетСуммНУ Тогда
							Проводка.СуммаНУДт = СтоимостьНУ;
						Иначе
							Если Проводка.НалоговоеНазначениеДт = Справочники.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность
								Тогда
								Проводка.СуммаНУДт = 0;
							Иначе
								Проводка.СуммаНУДт = Стоимость;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
				Если Проводка.СчетКт.НалоговыйУчет Тогда
					Если Проводка.СчетКт.УчетПоНалоговымНазначениямНДС Тогда
						Проводка.НалоговоеНазначениеКт = СтрокаДокумента.НалоговоеНазначение;
					КонецЕсли;  
					Если Проводка.СчетКт.УчетСуммНУ Тогда
						Проводка.СуммаНУКт = СтоимостьНУ;
					КонецЕсли;  
				КонецЕсли;
			КонецЕсли;
			
			Если Проводка.СчетДт.Количественный Тогда
				Проводка.КоличествоДт = КоличествоНовое;
			КонецЕсли;
			
			Если Проводка.СчетКт.Количественный Тогда
				Проводка.КоличествоКт = Количество;
			КонецЕсли;
			
			Если Проводка.СчетДт.Валютный Тогда
				Если СтрокаДокумента.ПроводкиСуммаСНДСРегл = СтрокаДокумента.ПроводкиСуммаСНДСВал Тогда
					Проводка.ВалютаДт = глЗначениеПеременной("ВалютаРегламентированногоУчета");
					Проводка.ВалютнаяСуммаДт = Стоимость;
				Иначе
					Если (ЗначениеЗаполнено(СтрокаДокумента.ДоговорКонтрагента)) И (ТипЗнч(СтрокаДокумента.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов")) Тогда
						Проводка.ВалютаДт = СтрокаДокумента.ДоговорКонтрагента.ВалютаВзаиморасчетов;
						Проводка.ВалютнаяСуммаДт = ?(СтрокаДокумента.ПроводкиСуммаСНДСРегл = 0, Стоимость, Стоимость * СтрокаДокумента.ПроводкиСуммаСНДСВал / СтрокаДокумента.ПроводкиСуммаСНДСРегл);
					Конецесли;
				КонецЕсли;
			КонецЕсли;
			
			// В таблицу списанных партий записывается строка с информацией.
			СтрокаСписаннаяПартия                       = ТаблицаСписанныхПартии.Добавить();
			СтрокаСписаннаяПартия.Партия                = СтрокаПартии.ДокументОприходования;
			СтрокаСписаннаяПартия.Количество            = Количество;
			СтрокаСписаннаяПартия.СуммаСписания         = Стоимость;
			СтрокаСписаннаяПартия.СуммаСписанияНУ       = СтоимостьНУ;
			СтрокаСписаннаяПартия.Комиссионный          = КомиссионныйТовар(СчетУчета);

			СтрокаСписаннаяПартия.СчетУчетаБУ = СчетУчета;
			СтрокаСписаннаяПартия.НоменклатурнаяПозиция = СтрокаПартии.НоменклатурнаяПозиция;
			
		КонецЦикла;

	КонецЕсли;
	
	// Не списанное количесвто добавим отдельной строкой 
	// в таблицу списанных партий.
	Если КоличествоОсталосьПогасить > 0  Тогда
		
		// В таблицу списанных партий записывается строка с информацией.
		СтрокаСписаннаяПартия               = ТаблицаСписанныхПартии.Добавить();
		СтрокаСписаннаяПартия.Количество    = КоличествоОсталосьПогасить;
		СтрокаСписаннаяПартия.Комиссионный  = КомиссионныйТовар(СчетУчета);
		
		СтрокаСписаннаяПартия.СчетУчетаБУ = СчетУчета;

	КонецЕсли;

	ОтключитьКонтрольОстатков = СтруктураПараметров.ОтключитьКонтрольОстатков;
	
	Если ((КоличествоОсталосьПогасить > 0) И (ОтключитьКонтрольОстатков) И (НЕ СтрокаДокумента.СчетУчетаБУ.Забалансовый)) или 
		(НЕ СтруктураПараметров.ПроводитьДокументПоРазделуУчета) Тогда
		
		Количество = КоличествоОсталосьПогасить;
		
			Проводка = СтрокаДокумента.Регистратор.Движения.Хозрасчетный.Добавить();
			Проводка.Содержание = СтрокаДокумента.СодержаниеПроводки;
			Проводка.СчетКт = СчетУчета;
			
			Если СчетУчета.Забалансовый = СтрокаДокумента.КорСчетСписанияБУ.Забалансовый Тогда
				Проводка.СчетДт = СтрокаДокумента.КорСчетСписанияБУ;
				Если Проводка.СчетДт.Количественный Тогда
					Если ИзменятьКоличество Тогда
						Проводка.КоличествоДт = ?(СтрокаДокумента.Количество = 0, 0, КоличествоОсталосьПогасить * СтрокаДокумента.КоличествоНовое / СтрокаДокумента.Количество);
					Иначе
						Проводка.КоличествоДт = КоличествоОсталосьПогасить;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если СтруктураПараметров.ВестиУчетПоДопРазрезамРегл Тогда
				НоменклатурнаяПозицияСтроки = ОбработкаТабличныхЧастей.ПолучитьНоменклатурнуюПозициюПоСоставляющим(СтрокаДокумента.Номенклатура,
																					  СтрокаДокумента.ХарактеристикаНоменклатуры,
																					  СтрокаДокумента.СерияНоменклатуры,
																					  СтрокаДокумента.Качество,
																					  СтрокаДокумента.ЗаказПокупателя);
			Иначе 
				НоменклатурнаяПозицияСтроки = Неопределено;
			КонецЕсли;
			
			Если (СтрокаДокумента.КорСубконтоСписанияБУ4 = Неопределено) Тогда
				СтрокаДокумента.КорСубконтоСписанияБУ4 = НоменклатурнаяПозицияСтроки;
			КонецЕсли;
			
			ЗаполнитьСубконто(Проводка, СтрокаДокумента, 
			СтрокаДокумента.КорСубконтоСписанияБУ1,
			СтрокаДокумента.КорСубконтоСписанияБУ2,
			СтрокаДокумента.КорСубконтоСписанияБУ3,
			СтрокаДокумента.КорСубконтоСписанияБУ4,
			НоменклатурнаяПозицияСтроки,,,);
			
			Проводка.Сумма = 0;
			
			ПроводкаДо2015 = СтрокаДокумента.Регистратор.Дата < глЗначениеПеременной("ДатаНКУ2015");
			Если ПроводкаДо2015 Тогда

				Если Проводка.СчетДт.НалоговыйУчет Тогда
					Проводка.СуммаНУДт = 0;
					Проводка.НалоговоеНазначениеДт = СтрокаДокумента.НалоговоеНазначениеНовое;
				КонецЕсли;
				Если Проводка.СчетКт.НалоговыйУчет Тогда
					Проводка.СуммаНУКт = 0;
					Проводка.НалоговоеНазначениеКт = СтрокаДокумента.НалоговоеНазначение;
				КонецЕсли;
			Иначе
				Если Проводка.СчетДт.НалоговыйУчет Тогда
					Если Проводка.СчетДт.УчетПоНалоговымНазначениямНДС Тогда
						Проводка.НалоговоеНазначениеДт = СтрокаДокумента.НалоговоеНазначениеНовое;
					КонецЕсли;
					Если (СтруктураПараметров.ЕстьНалогНаПрибыльНал = Истина) И Проводка.СчетДт.УчетСуммНУ Тогда
						Проводка.СуммаНУДт = Проводка.Сумма;
					КонецЕсли;
				КонецЕсли;
				Если Проводка.СчетКт.НалоговыйУчет Тогда
					Если Проводка.СчетКт.УчетПоНалоговымНазначениямНДС Тогда
						Проводка.НалоговоеНазначениеКт = СтрокаДокумента.НалоговоеНазначение;
					КонецЕсли;
					Если (СтруктураПараметров.ЕстьНалогНаПрибыльНал = Истина) И Проводка.СчетКт.УчетСуммНУ Тогда
						Проводка.СуммаНУКт = Проводка.Сумма;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		
			Если Проводка.СчетКт.Количественный Тогда
				Проводка.КоличествоКт = Количество;
			КонецЕсли;  
			
			Если СтруктураПараметров.ПроводитьДокументПоРазделуУчета Тогда
				
				Если СчетУчета.НалоговыйУчет Тогда
					ТекстДополнительный = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru=' , налог. назначение: %1';uk=' , подат. призначення: %1'"),СтрокаДокумента.НалоговоеНазначение);
				КонецЕсли;
				
				ОбщегоНазначения.СообщитьОбОшибке(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1. учет. Строка :%2"
				"Не списано %3 %4 товара %5, счет учета %6%7", "Бух", СтрокаДокумента.НомерСтроки, КоличествоОсталосьПогасить, СтрокаДокумента.Номенклатура.ЕдиницаХраненияОстатков, СтрокаДокумента.Номенклатура, СчетУчета, ТекстДополнительный), "");
				
			КонецЕсли;
		
		Если Проводка <> Неопределено Тогда
			Проводка.Период      = СтрокаДокумента.Регистратор.Дата;
			Проводка.Организация = СтрокаДокумента.Организация;
		КонецЕсли;
		
	ИначеЕсли КоличествоОсталосьПогасить > 0  Тогда

		Если СтруктураПараметров.ПроводитьДокументПоРазделуУчета Тогда
			
			Если СчетУчета.НалоговыйУчет Тогда
				ТекстДополнительный = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru=' , налог. назначение: %1';uk=' , подат. призначення: %1'"),СтрокаДокумента.НалоговоеНазначение);
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьОбОшибке(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1. учет. Строка :%2"
			"Не списано %3 %4 товара %5, счет учета %6%7", "Бух", СтрокаДокумента.НомерСтроки, КоличествоОсталосьПогасить, СтрокаДокумента.Номенклатура.ЕдиницаХраненияОстатков, СтрокаДокумента.Номенклатура, СчетУчета, ТекстДополнительный), "");
			
		КонецЕсли;

		Отказ = Истина;

	КонецЕсли;
		
КонецПроцедуры // СписаниеСоСклада()

Функция ВедетсяСуммовойУчетПоСкладам(Счет) Экспорт

	УчетПоСкладам = Ложь;

	СтрокаВидаСубконто = Счет.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады, "ВидСубконто");

	Если СтрокаВидаСубконто <> Неопределено Тогда
		УчетПоСкладам = СтрокаВидаСубконто.Суммовой;
	КонецЕсли;

	Возврат УчетПоСкладам;

КонецФункции // ВедетсяСуммовойУчетПоСкладам()

Функция НаСчетеВедетсяУчетПоСтатьямПриростаУбыли(Счет) Экспорт
	
	УчетПоСтатьям = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Счет) Тогда
	Иначе
		СтрокаВидаСубконто = Счет.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиНалоговогоУчета, "ВидСубконто");

		Если СтрокаВидаСубконто <> Неопределено Тогда
			УчетПоСтатьям = Истина;
		КонецЕсли;

	КонецЕсли;

	Возврат УчетПоСтатьям;
	
КонецФункции

Функция НаСчетеВедетсяУчетПоВидамНалоговойДеятельности(Счет) Экспорт
	
	УчетПоВНД = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Счет) Тогда
	Иначе
		СтрокаВидаСубконто = Счет.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыНалоговойДеятельности, "ВидСубконто");

		Если СтрокаВидаСубконто <> Неопределено Тогда
			УчетПоВНД = Истина;
		КонецЕсли;

	КонецЕсли;

	Возврат УчетПоВНД;
	
КонецФункции

Функция НаСчетеВедетсяПартионныйУчет(Счет) Экспорт

	ПартионныйУчет = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Счет) Тогда
	Иначе
		СтрокаВидаСубконто = Счет.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии, "ВидСубконто");

		Если СтрокаВидаСубконто <> Неопределено Тогда
			ПартионныйУчет = Истина;
		КонецЕсли;

	КонецЕсли;

	Возврат ПартионныйУчет;

КонецФункции // НаСчетеВедетсяПартионныйУчет()

Функция НаСчетеВедетсяУчетПоНоменклатурнымПозициям(Счет) Экспорт
	
	УчетПоНоменклатурнымПозициям = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Счет) Тогда
	Иначе
		СтрокаВидаСубконто = Счет.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеПозиции, "ВидСубконто");

		Если СтрокаВидаСубконто <> Неопределено Тогда
			УчетПоНоменклатурнымПозициям = Истина;
		КонецЕсли;

	КонецЕсли;

	Возврат УчетПоНоменклатурнымПозициям;
	
КонецФункции

Функция НаСчетеВедетсяУчетПоОтгрузкам(Счет) 

	ПартионныйУчет = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Счет) Тогда
	Иначе
		СтрокаВидаСубконто = Счет.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами, "ВидСубконто");

		Если СтрокаВидаСубконто <> Неопределено Тогда
			ПартионныйУчет = Истина;
		КонецЕсли;

	КонецЕсли;

	Возврат ПартионныйУчет;

КонецФункции // НаСчетеВедетсяПартионныйУчет()

Функция НаСчетеВедетсяУчетПоКонтрагентам(Счет)

	УчетПоКонтрагентам = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Счет) Тогда
	Иначе
		СтрокаВидаСубконто = Счет.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты, "ВидСубконто");

		Если СтрокаВидаСубконто <> Неопределено Тогда
			УчетПоКонтрагентам = Истина;
		КонецЕсли;
		
	КонецЕсли;

	Возврат УчетПоКонтрагентам;

КонецФункции // НаСчетеВедетсяУчетПоКонтрагентам()

Процедура ЗаполнитьСубконтоПоРеквизитам(ВидСубконто, Субконто, ЗначениеСубконто1, ЗначениеСубконто2, ЗначениеСубконто3, ЗначениеСубконто4)

	Если ЗначениеЗаполнено(ЗначениеСубконто1) И ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(ТипЗнч(ЗначениеСубконто1)) Тогда
		Субконто.Вставить(ВидСубконто.ВидСубконто, ЗначениеСубконто1);

	ИначеЕсли ЗначениеЗаполнено(ЗначениеСубконто2) И ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(ТипЗнч(ЗначениеСубконто2)) Тогда
		Субконто.Вставить(ВидСубконто.ВидСубконто, ЗначениеСубконто2);

	ИначеЕсли ЗначениеЗаполнено(ЗначениеСубконто3) И ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(ТипЗнч(ЗначениеСубконто3)) Тогда
		Субконто.Вставить(ВидСубконто.ВидСубконто, ЗначениеСубконто3);

	ИначеЕсли ЗначениеЗаполнено(ЗначениеСубконто4) И ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(ТипЗнч(ЗначениеСубконто4)) Тогда
		Субконто.Вставить(ВидСубконто.ВидСубконто, ЗначениеСубконто4);	
		
	КонецЕсли;

КонецПроцедуры

// Процедура устанавливает субконто на счете. Если такое субконто не счете
// отсутствует, то ничего не делается.
//
// Параметры:
//		Счет - Счет, к которому относится субконто
//      Субконто - вид субконто
//		Номер или имя установливаемого субконто
//      Значение субконто - значение устанавливаемого субконто
//
Процедура ЗаполнитьСубконто(Проводка, СтрокаДокумента = Неопределено, СубконтоДт1 = Неопределено, СубконтоДт2 = Неопределено, СубконтоДт3 = Неопределено, СубконтоДт4 = Неопределено, СубконтоКт1 = Неопределено, СубконтоКт2 = Неопределено, СубконтоКт3 = Неопределено, СубконтоКт4 = Неопределено) Экспорт

	// Заполняем дебет:
	Если ЗначениеЗаполнено(Проводка.СчетДт) Тогда

		Для каждого ВидСубконто Из Проводка.СчетДт.ВидыСубконто Цикл

			Если ЗначениеЗаполнено(СтрокаДокумента) Тогда
			
				Если ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Номенклатура")) Тогда
					Проводка.СубконтоДт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.Номенклатура);

				ИначеЕсли ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Организации")) Тогда
					Проводка.СубконтоДт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.Организация);

				ИначеЕсли ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Склады")) Тогда
					Проводка.СубконтоДт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.Склад);
					Если ЗначениеЗаполнено(СтрокаДокумента.Склад) Тогда
						Проводка.СубконтоДт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.Склад);
					КонецЕсли;

				ИначеЕсли ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Контрагенты")) Тогда
					Если ТипЗнч(СтрокаДокумента.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
						Проводка.СубконтоДт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.ДоговорКонтрагента.Владелец);
					КонецЕсли;

				ИначеЕсли ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ДоговорыКонтрагентов")) Тогда
					Проводка.СубконтоДт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.ДоговорКонтрагента);
				
				ИначеЕсли ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ВидыНалоговойДеятельности")) Тогда
					Проводка.СубконтоДт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.ВидНалоговойДеятельности);
				
				ИначеЕсли ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.НоменклатурныеГруппы")) Тогда
					Проводка.СубконтоДт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.НоменклатурнаяГруппа);
				
				КонецЕсли;
				
			КонецЕсли;

			ЗаполнитьСубконтоПоРеквизитам(ВидСубконто, Проводка.СубконтоДт, СубконтоДт1, СубконтоДт2, СубконтоДт3, СубконтоДт4);

		КонецЦикла;

	КонецЕсли;

	// Заполняем кредит
	Если ЗначениеЗаполнено(Проводка.СчетКт) Тогда

		Для каждого ВидСубконто Из Проводка.СчетКт.ВидыСубконто Цикл
			
			Если ЗначениеЗаполнено(СтрокаДокумента) Тогда

				Если ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Номенклатура")) Тогда
					Проводка.СубконтоКт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.Номенклатура);

				ИначеЕсли ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Организации")) Тогда
					Проводка.СубконтоКт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.Организация);

				ИначеЕсли ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Склады")) Тогда
					Проводка.СубконтоКт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.Склад);

				ИначеЕсли ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Контрагенты")) Тогда
					Если ТипЗнч(СтрокаДокумента.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
						Проводка.СубконтоКт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.ДоговорКонтрагента.Владелец);
					ИначеЕсли ТипЗнч(СубконтоДт1) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(СубконтоДт1) Тогда
						Проводка.СубконтоКт.Вставить(ВидСубконто.ВидСубконто, СубконтоДт1);
					ИначеЕсли ТипЗнч(СубконтоДт2) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(СубконтоДт2) Тогда
						Проводка.СубконтоКт.Вставить(ВидСубконто.ВидСубконто, СубконтоДт2);
					ИначеЕсли ТипЗнч(СубконтоДт3) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(СубконтоДт3) Тогда
						Проводка.СубконтоКт.Вставить(ВидСубконто.ВидСубконто, СубконтоДт3);
					КонецЕсли;

				ИначеЕсли ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ДоговорыКонтрагентов")) Тогда
					Проводка.СубконтоКт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.ДоговорКонтрагента);

				ИначеЕсли ВидСубконто.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ВидыНалоговойДеятельности")) Тогда
					Проводка.СубконтоКт.Вставить(ВидСубконто.ВидСубконто, СтрокаДокумента.ВидНалоговойДеятельности);
	
				КонецЕсли;
				
			КонецЕсли;

			ЗаполнитьСубконтоПоРеквизитам(ВидСубконто, Проводка.СубконтоКт, СубконтоКт1, СубконтоКт2, СубконтоКт3, СубконтоКт4);

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры // ЗаполнитьСубконто()

// Выполняется сортировка списка счетов на счета, по которм вдется разный
// суммовой учет (в части аналитики).
//
// Параметры
//  СчетаУчета  – Список значений - список счетов, который необходимо рассортировать.
//  СчетаУчетаН – Список значений - список счетов, состоящий из счетов
//				  на которых ведется суммовой учет только по номенклатуре.
//  СчетаУчетаНС – Список значений - список счетов, состоящий из счетов
//				  на которых ведется суммовой учет по номенклатуре и по складам.
//  СчетаУчетаНП – Список значений - список счетов, состоящий из счетов
//				  на которых ведется суммовой учет по номенклатуре и по партиям.
//  СчетаУчетаНСП – Список значений - список счетов, состоящий из счетов
//				  на которых ведется суммовой учет по номенклатуре, складам и партиям.
//  СчетаУчетаКН – Список значений - список счетов, состоящий из счетов
//				  на которых ведется суммовой учет по номенклатуре и контрагентам.
//  СчетаУчетаКНП – Список значений - список счетов, состоящий из счетов
//				  на которых ведется суммовой учет по номенклатуре, контрагентам и партиям.
//
Процедура СортировкаСчетовУчета(СчетаУчета, СчетаУчетаН, СчетаУчетаНД, СчетаУчетаНС, СчетаУчетаНП, СчетаУчетаНСП, СчетаУчетаНСД, СчетаУчетаНПД, СчетаУчетаНСПД, СчетаУчетаКН, СчетаУчетаКНП, СчетаУчетаКНД, СчетаУчетаКНПД, СчетаУчетаНПО, СчетаУчетаНПОД)

	Для каждого Счет Из СчетаУчета Цикл

		Если ВедетсяСуммовойУчетПоСкладам(Счет) И НаСчетеВедетсяПартионныйУчет(Счет) И НаСчетеВедетсяУчетПоНоменклатурнымПозициям(Счет) И (Счет.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Комиссионеры, "ВидСубконто") = Неопределено) Тогда
			СчетаУчетаНСПД.Добавить(Счет);
			
		ИначеЕсли НаСчетеВедетсяУчетПоКонтрагентам(Счет) И НаСчетеВедетсяПартионныйУчет(Счет) И НаСчетеВедетсяУчетПоНоменклатурнымПозициям(Счет) Тогда
			СчетаУчетаКНПД.Добавить(Счет);
			
		ИначеЕсли НаСчетеВедетсяУчетПоОтгрузкам(Счет) И НаСчетеВедетсяПартионныйУчет(Счет) И НаСчетеВедетсяУчетПоНоменклатурнымПозициям(Счет) Тогда
			СчетаУчетаНПОД.Добавить(Счет);
		
		ИначеЕсли ВедетсяСуммовойУчетПоСкладам(Счет) И НаСчетеВедетсяПартионныйУчет(Счет) И (Счет.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Комиссионеры, "ВидСубконто") = Неопределено) Тогда
			СчетаУчетаНСП.Добавить(Счет);
			
		ИначеЕсли ВедетсяСуммовойУчетПоСкладам(Счет) И НаСчетеВедетсяУчетПоНоменклатурнымПозициям(Счет) И (Счет.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Комиссионеры, "ВидСубконто") = Неопределено) Тогда
			СчетаУчетаНСД.Добавить(Счет);
			
		ИначеЕсли НаСчетеВедетсяУчетПоНоменклатурнымПозициям(Счет) И НаСчетеВедетсяПартионныйУчет(Счет) И (Счет.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Комиссионеры, "ВидСубконто") = Неопределено) Тогда
			СчетаУчетаНПД.Добавить(Счет);

		ИначеЕсли НаСчетеВедетсяПартионныйУчет(Счет) И НаСчетеВедетсяУчетПоКонтрагентам(Счет) Тогда
			СчетаУчетаКНП.Добавить(Счет);
			
		ИначеЕсли НаСчетеВедетсяУчетПоНоменклатурнымПозициям(Счет) И НаСчетеВедетсяУчетПоКонтрагентам(Счет) Тогда
			СчетаУчетаКНД.Добавить(Счет);
			
		ИначеЕсли ВедетсяСуммовойУчетПоСкладам(Счет) Тогда
			СчетаУчетаНС.Добавить(Счет);
			
		ИначеЕсли НаСчетеВедетсяУчетПоНоменклатурнымПозициям(Счет) Тогда
			СчетаУчетаНД.Добавить(Счет);

		ИначеЕсли НаСчетеВедетсяПартионныйУчет(Счет) И НаСчетеВедетсяУчетПоОтгрузкам(Счет) Тогда
			СчетаУчетаНПО.Добавить(Счет);
			
		ИначеЕсли НаСчетеВедетсяПартионныйУчет(Счет) Тогда
			СчетаУчетаНП.Добавить(Счет);
			
		ИначеЕсли НаСчетеВедетсяУчетПоКонтрагентам(Счет) Тогда
			СчетаУчетаКН.Добавить(Счет);
			
		Иначе
			СчетаУчетаН.Добавить(Счет);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Формирует запрос по остаткам партий товаров на конец рабочего  периода
//
// Параметры:
//  МоментКон      - момент времени, на которой возвращаются остатки.
//
// Возвращаемое значение:
//	Результат запроса, выгруженный в таблицу значений.
//
Функция ПолучитьТаблицуПартийНаСкладах(СтруктураПараметров, Учет, Организации, НалоговыеНазначения, СчетаУчета, Номенклатура = Неопределено, Склады = Неопределено, Контрагент = Неопределено, Партия = Неопределено, Договор = Неопределено) Экспорт
	
	Если НЕ СтруктураПараметров.ПроводитьДокументПоРазделуУчета Тогда
		Возврат Новый ДеревоЗначений();
	КонецЕсли;
	
	ТекстПустоеНалоговоеНазначение = "";
	Для каждого СчетУчета из СчетаУчета Цикл
		Если НЕ СчетУчета.НалоговыйУчет Тогда
			ТекстПустоеНалоговоеНазначение = " ИЛИ НалоговоеНазначение ЕСТЬ NULL";
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Сортировка счетов учета
	СчетаУчетаН    = Новый СписокЗначений; // счета, с суммовым учетом только по номенклатуре
	СчетаУчетаНС   = Новый СписокЗначений; // счета, с суммовым учетом по номенклатуре и складам
	СчетаУчетаНП   = Новый СписокЗначений; // счета, с суммовым учетом по номенклатуре и партиям
	СчетаУчетаНД   = Новый СписокЗначений; // счета, с суммовым учетом по номенклатуре и номенклатурным позициям
	СчетаУчетаНСП  = Новый СписокЗначений; // счета, с суммовым учетом по номенклатуре, складам и партиям
	СчетаУчетаНСД  = Новый СписокЗначений; // счета, с суммовым учетом по номенклатуре, складам и номенклатурным позициям
	СчетаУчетаНПД  = Новый СписокЗначений; // счета, с суммовым учетом по номенклатуре, партиям и номенклатурным позициям
	СчетаУчетаНСПД = Новый СписокЗначений; // счета, с суммовым учетом по номенклатуре, складам, партиям и номенклатурным позициям
	СчетаУчетаКН   = Новый СписокЗначений; // счета, с суммовым учетом по контрагентам, и номенклатуре
	СчетаУчетаКНП  = Новый СписокЗначений; // счета, с суммовым учетом по контрагентам, номенклатуре и партиям
	СчетаУчетаКНД  = Новый СписокЗначений; // счета, с суммовым учетом по контрагентам, номенклатуре и номенклатурным позициям
	СчетаУчетаКНПД = Новый СписокЗначений; // счета, с суммовым учетом по контрагентам, номенклатуре, партиям и номенклатурным позициям
	
	СчетаУчетаНПО  = Новый СписокЗначений; // счета, с суммовым учетом по номенклатуре партиям и отгрузкам
	СчетаУчетаНПОД = Новый СписокЗначений; // счета, с суммовым учетом по номенклатуре партиям, отгрузкам и номенклатурным позициям
	
	СортировкаСчетовУчета(СчетаУчета, СчетаУчетаН, СчетаУчетаНД, СчетаУчетаНС, СчетаУчетаНП, СчетаУчетаНСП, СчетаУчетаНСД, СчетаУчетаНПД, СчетаУчетаНСПД, СчетаУчетаКН, СчетаУчетаКНП, СчетаУчетаКНД, СчетаУчетаКНПД, СчетаУчетаНПО, СчетаУчетаНПОД);
	
	ВидСубконтоНоменклатура          = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура;
	ВидСубконтоПартии                = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии;
	ВидСубконтоСклады                = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады;
	ВидСубконтоКонтрагенты           = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты;
	ВидСубконтоОтгрузки              = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами;
	ВидСубконтоНоменклатурныеПозиции = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеПозиции;
	
	ВидыСубконтоН = Новый Массив;
	ВидысубконтоН.Добавить(ВидСубконтоНоменклатура);
	
	ВидыСубконтоНС = Новый Массив;
	ВидысубконтоНС.Добавить(ВидСубконтоНоменклатура);
	ВидысубконтоНС.Добавить(ВидСубконтоСклады);
	
	ВидыСубконтоНП = Новый Массив;
	ВидысубконтоНП.Добавить(ВидСубконтоНоменклатура);
	ВидысубконтоНП.Добавить(ВидСубконтоПартии);
	
	ВидыСубконтоНД = Новый Массив;
	ВидысубконтоНД.Добавить(ВидСубконтоНоменклатура);
	ВидысубконтоНД.Добавить(ВидСубконтоНоменклатурныеПозиции);
	
	ВидыСубконтоНПО = Новый Массив;
	ВидыСубконтоНПО.Добавить(ВидСубконтоНоменклатура);
	ВидыСубконтоНПО.Добавить(ВидСубконтоПартии);
	ВидыСубконтоНПО.Добавить(ВидСубконтоОтгрузки);
	
	ВидыСубконтоНПОД = Новый Массив;
	ВидыСубконтоНПОД.Добавить(ВидСубконтоНоменклатура);
	ВидыСубконтоНПОД.Добавить(ВидСубконтоПартии);
	ВидыСубконтоНПОД.Добавить(ВидСубконтоОтгрузки);
	ВидыСубконтоНПОД.Добавить(ВидСубконтоНоменклатурныеПозиции);
	
	ВидыСубконтоНСП = Новый Массив;
	ВидысубконтоНСП.Добавить(ВидСубконтоНоменклатура);
	ВидысубконтоНСП.Добавить(ВидСубконтоПартии);
	ВидысубконтоНСП.Добавить(ВидСубконтоСклады);
	
	ВидыСубконтоНСД = Новый Массив;
	ВидысубконтоНСД.Добавить(ВидСубконтоНоменклатура);
	ВидысубконтоНСД.Добавить(ВидСубконтоСклады);
	ВидысубконтоНСД.Добавить(ВидСубконтоНоменклатурныеПозиции);
	
	ВидыСубконтоНПД = Новый Массив;
	ВидысубконтоНПД.Добавить(ВидСубконтоНоменклатура);
	ВидысубконтоНПД.Добавить(ВидСубконтоПартии);
	ВидысубконтоНПД.Добавить(ВидСубконтоНоменклатурныеПозиции);
	
	ВидыСубконтоНСПД = Новый Массив;
	ВидысубконтоНСПД.Добавить(ВидСубконтоНоменклатура);
	ВидысубконтоНСПД.Добавить(ВидСубконтоПартии);
	ВидысубконтоНСПД.Добавить(ВидСубконтоСклады);
	ВидысубконтоНСПД.Добавить(ВидСубконтоНоменклатурныеПозиции);
	
	ВидыСубконтоКН = Новый Массив;
	ВидыСубконтоКН.Добавить(ВидСубконтоНоменклатура);
	ВидыСубконтоКН.Добавить(ВидСубконтоКонтрагенты);
	
	ВидыСубконтоКНП = Новый Массив;
	ВидыСубконтоКНП.Добавить(ВидСубконтоНоменклатура);
	ВидыСубконтоКНП.Добавить(ВидСубконтоПартии);
	ВидыСубконтоКНП.Добавить(ВидСубконтоКонтрагенты);
	
	ВидыСубконтоКНД = Новый Массив;
	ВидыСубконтоКНД.Добавить(ВидСубконтоНоменклатура);
	ВидыСубконтоКНД.Добавить(ВидСубконтоКонтрагенты);
	ВидыСубконтоКНД.Добавить(ВидСубконтоНоменклатурныеПозиции);
	
	ВидыСубконтоКНПД = Новый Массив;
	ВидыСубконтоКНПД.Добавить(ВидСубконтоНоменклатура);
	ВидыСубконтоКНПД.Добавить(ВидСубконтоПартии);
	ВидыСубконтоКНПД.Добавить(ВидСубконтоКонтрагенты);
	ВидыСубконтоКНПД.Добавить(ВидСубконтоНоменклатурныеПозиции);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МоментКон",       СтруктураПараметров.МоментКон);
	Запрос.УстановитьПараметр("Организации",     Организации);
	Запрос.УстановитьПараметр("НалоговыеНазначения", НалоговыеНазначения);
	Запрос.УстановитьПараметр("СчетаУчетаН",     СчетаУчетаН);
	Запрос.УстановитьПараметр("СчетаУчетаНС",    СчетаУчетаНС);
	Запрос.УстановитьПараметр("СчетаУчетаНП",    СчетаУчетаНП);
	Запрос.УстановитьПараметр("СчетаУчетаНД",    СчетаУчетаНД);
	Запрос.УстановитьПараметр("СчетаУчетаНПО",   СчетаУчетаНПО);
	Запрос.УстановитьПараметр("СчетаУчетаНПОД",  СчетаУчетаНПОД);
	Запрос.УстановитьПараметр("СчетаУчетаНСП",   СчетаУчетаНСП);
	Запрос.УстановитьПараметр("СчетаУчетаНСД",   СчетаУчетаНСД);
	Запрос.УстановитьПараметр("СчетаУчетаНПД",   СчетаУчетаНПД);
	Запрос.УстановитьПараметр("СчетаУчетаНСПД",  СчетаУчетаНСПД);
	Запрос.УстановитьПараметр("СчетаУчетаКН",    СчетаУчетаКН);
	Запрос.УстановитьПараметр("СчетаУчетаКНП",   СчетаУчетаКНП);
	Запрос.УстановитьПараметр("СчетаУчетаКНД",   СчетаУчетаКНД);
	Запрос.УстановитьПараметр("СчетаУчетаКНПД",  СчетаУчетаКНПД);
	
	Запрос.УстановитьПараметр("Номенклатура",    Номенклатура);
	Запрос.УстановитьПараметр("Склады",          Склады);
	Запрос.УстановитьПараметр("ВидысубконтоН",   ВидысубконтоН);
	Запрос.УстановитьПараметр("ВидысубконтоНС",  ВидысубконтоНС);
	Запрос.УстановитьПараметр("ВидысубконтоНП",  ВидысубконтоНП);
	Запрос.УстановитьПараметр("ВидысубконтоНД",  ВидысубконтоНД);
	Запрос.УстановитьПараметр("ВидысубконтоНПО", ВидысубконтоНПО);
	Запрос.УстановитьПараметр("ВидысубконтоНПОД",ВидысубконтоНПОД);
	Запрос.УстановитьПараметр("ВидысубконтоНСП", ВидысубконтоНСП);
	Запрос.УстановитьПараметр("ВидысубконтоНСД", ВидысубконтоНСД);
	Запрос.УстановитьПараметр("ВидысубконтоНПД", ВидысубконтоНПД);
	Запрос.УстановитьПараметр("ВидысубконтоНСПД",ВидысубконтоНСПД);
	Запрос.УстановитьПараметр("ВидысубконтоКН",  ВидысубконтоКН);
	Запрос.УстановитьПараметр("ВидыСубконтоКНП", ВидыСубконтоКНП);
	Запрос.УстановитьПараметр("ВидысубконтоКНД", ВидысубконтоКНД);
	Запрос.УстановитьПараметр("ВидыСубконтоКНПД",ВидыСубконтоКНПД);
	Запрос.УстановитьПараметр("ПустойСклад",     Справочники.Склады.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяПартия",    Документы.Партия.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяОтгрузка",  Документы.ДокументРасчетовСКонтрагентом.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяНоменклатурнаяПозиция", Справочники.НоменклатурныеПозиции.ПустаяСсылка());
	Запрос.УстановитьПараметр("Контрагент",      Контрагент);
	Запрос.УстановитьПараметр("Договор",	     Договор);
	Запрос.УстановитьПараметр("Партия",      	 Партия);
	Запрос.УстановитьПараметр("ПустаяДата",      Дата("00010101"));
	Запрос.УстановитьПараметр("ПустаяХарактеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСерия", Справочники.СерииНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустоеКачество", Справочники.Качество.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойЗаказ", Документы.ЗаказПокупателя.ПустаяСсылка());
	
	Текст = "";
	
	Если СчетаУчетаН.Количество() > 0 Тогда
		
		Текст = Текст + "
		|ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	&ПустаяХарактеристика КАК ХарактеристикаНоменклатуры,
		|	&ПустаяСерия КАК СерияНоменклатуры,
		|	&ПустойЗаказ КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	&ПустоеКачество КАК Качество,
		|	&ПустаяНоменклатурнаяПозиция КАК НоменклатурнаяПозиция,
		|	&ПустаяДата КАК ДокументОприходованияДата,
		|	&ПустаяПартия КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	&ПустойСклад КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаН), &ВидыСубконтоН,";
		
		
		Текст = Текст + "Субконто1 В (&Номенклатура) И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,";
		
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
	КонецЕсли;
	
	Если СчетаУчетаНС.Количество() > 0 Тогда
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	&ПустаяХарактеристика КАК ХарактеристикаНоменклатуры,
		|	&ПустаяСерия КАК СерияНоменклатуры,
		|	&ПустойЗаказ КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	&ПустоеКачество КАК Качество,
		|	&ПустаяНоменклатурнаяПозиция КАК НоменклатурнаяПозиция,
		|	&ПустаяДата КАК ДокументОприходованияДата,
		|	&ПустаяПартия КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	Остатки.Субконто2 КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаНС), &ВидыСубконтоНС,";
		
		
		Текст = Текст + "Субконто1 В (&Номенклатура) И Субконто2 В (&Склады) И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,";
		
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	Если СчетаУчетаНП.Количество() > 0 Тогда
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	&ПустаяХарактеристика КАК ХарактеристикаНоменклатуры,
		|	&ПустаяСерия КАК СерияНоменклатуры,
		|	&ПустойЗаказ КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	&ПустоеКачество КАК Качество,
		|	&ПустаяНоменклатурнаяПозиция КАК НоменклатурнаяПозиция,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДокументОприходованияДата,
		|	Остатки.Субконто2 КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	&ПустойСклад КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаНП), &ВидыСубконтоНП,";
		
		
		Текст = Текст + "Субконто1 В (&Номенклатура) " + ?(НЕ ЗначениеЗаполнено(Партия), "", "И Субконто2 В (&Партия) ")+ "И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,";
		
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	Если СчетаУчетаНД.Количество() > 0 Тогда
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	Остатки.Субконто2.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	Остатки.Субконто2.СерияНоменклатуры КАК СерияНоменклатуры,
		|	Остатки.Субконто2.ЗаказПокупателя КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	Остатки.Субконто2.Качество КАК Качество,
		|	Остатки.Субконто2 КАК НоменклатурнаяПозиция,
		|	&ПустаяДата КАК ДокументОприходованияДата,
		|	&ПустаяПартия КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	&ПустойСклад КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаНД), &ВидыСубконтоНД,";
		
		
		Текст = Текст + "Субконто1 В (&Номенклатура) И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2.ХарактеристикаНоменклатуры,
		|	Остатки.Субконто2.СерияНоменклатуры,
		|	Остатки.Субконто2.ЗаказПокупателя,
		|	Остатки.Субконто2.Качество,
		|	Остатки.Субконто2,";
		
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	////////////
	//НПО
	Если СчетаУчетаНПО.Количество() > 0 Тогда
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	&ПустаяХарактеристика КАК ХарактеристикаНоменклатуры,
		|	&ПустаяСерия КАК СерияНоменклатуры,
		|	&ПустойЗаказ КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	&ПустоеКачество КАК Качество,
		|	&ПустаяНоменклатурнаяПозиция КАК НоменклатурнаяПозиция,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДокументОприходованияДата,
		|	Остатки.Субконто2 КАК ДокументОприходования,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДокументОтгрузкиДата,
		|	Остатки.Субконто3 КАК ДокументОтгрузки,
		|	&ПустойСклад КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаНПО), &ВидыСубконтоНПО,";
		
		
		Текст = Текст + "Субконто1 В (&Номенклатура) " + ?(НЕ ЗначениеЗаполнено(Партия), "", "И Субконто2 В (&Партия) ")+ "И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|ГДЕ
		|   Субконто3.ДоговорКонтрагента = &Договор
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,
		|	Остатки.Субконто3,";
		
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	////////////
	//НПОД
	Если СчетаУчетаНПОД.Количество() > 0 Тогда
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	Остатки.Субконто4.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	Остатки.Субконто4.СерияНоменклатуры КАК СерияНоменклатуры,
		|	Остатки.Субконто4.ЗаказПокупателя КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	Остатки.Субконто4.Качество КАК Качество,
		|	Остатки.Субконто4 КАК НоменклатурнаяПозиция,
		|	&ПустаяНоменклатурнаяПозиция КАК НоменклатурнаяПозиция,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДокументОприходованияДата,
		|	Остатки.Субконто2 КАК ДокументОприходования,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДокументОтгрузкиДата,
		|	Остатки.Субконто3 КАК ДокументОтгрузки,
		|	&ПустойСклад КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаНПОД), &ВидыСубконтоНПОД,";
		
		
		Текст = Текст + "Субконто1 В (&Номенклатура) " + ?(НЕ ЗначениеЗаполнено(Партия), "", "И Субконто2 В (&Партия) ")+ "И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|ГДЕ
		|   Субконто3.ДоговорКонтрагента = &Договор
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,
		|	Остатки.Субконто3,
		|	Остатки.Субконто4.ХарактеристикаНоменклатуры,
		|	Остатки.Субконто4.СерияНоменклатуры,
		|	Остатки.Субконто4.ЗаказПокупателя,
		|	Остатки.Субконто4.Качество,
		|	Остатки.Субконто4,";
		
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	//НСПД
	////////////
	Если СчетаУчетаНСПД.Количество() > 0 Тогда
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	Остатки.Субконто4.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	Остатки.Субконто4.СерияНоменклатуры КАК СерияНоменклатуры,
		|	Остатки.Субконто4.ЗаказПокупателя КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	Остатки.Субконто4.Качество КАК Качество,
		|	Остатки.Субконто4 КАК НоменклатурнаяПозиция,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДокументОприходованияДата,
		|	Остатки.Субконто2 КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	Остатки.Субконто3 КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаНСПД), &ВидысубконтоНСПД,";
		
		
		Текст = Текст + "Субконто1 В (&Номенклатура) " + ?(НЕ ЗначениеЗаполнено(Партия), "", "И Субконто2 В (&Партия) ")+ "И Субконто3 В (&Склады) И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,
		|	Остатки.Субконто3,
		|	Остатки.Субконто4.ХарактеристикаНоменклатуры,
		|	Остатки.Субконто4.СерияНоменклатуры,
		|	Остатки.Субконто4.ЗаказПокупателя,
		|	Остатки.Субконто4.Качество,
		|	Остатки.Субконто4,";
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	//НСД
	////////////	
	Если СчетаУчетаНСД.Количество() > 0 Тогда
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	Остатки.Субконто3.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	Остатки.Субконто3.СерияНоменклатуры КАК СерияНоменклатуры,
		|	Остатки.Субконто3.ЗаказПокупателя КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	Остатки.Субконто3.Качество КАК Качество,
		|	Остатки.Субконто3 КАК НоменклатурнаяПозиция,
		|	&ПустаяДата КАК ДокументОприходованияДата,
		|	&ПустаяПартия КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	Остатки.Субконто2 КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаНСД), &ВидысубконтоНСД,";
		
		
		Текст = Текст + "Субконто1 В (&Номенклатура) И Субконто2 В (&Склады) И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,
		|	Остатки.Субконто3.ХарактеристикаНоменклатуры,
		|	Остатки.Субконто3.СерияНоменклатуры,
		|	Остатки.Субконто3.ЗаказПокупателя,
		|	Остатки.Субконто3.Качество,
		|	Остатки.Субконто3,";
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	//НПД
	////////////
	Если СчетаУчетаНПД.Количество() > 0 Тогда
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	Остатки.Субконто3.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	Остатки.Субконто3.СерияНоменклатуры КАК СерияНоменклатуры,
		|	Остатки.Субконто3.ЗаказПокупателя КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	Остатки.Субконто3.Качество КАК Качество,
		|	Остатки.Субконто3 КАК НоменклатурнаяПозиция,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДокументОприходованияДата,
		|	Остатки.Субконто2 КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	&ПустойСклад КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаНПД), &ВидысубконтоНПД,";
		
		
		Текст = Текст + "Субконто1 В (&Номенклатура) " + ?(НЕ ЗначениеЗаполнено(Партия), "", "И Субконто2 В (&Партия) ")+ "И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,
		|	Остатки.Субконто3.ХарактеристикаНоменклатуры,
		|	Остатки.Субконто3.СерияНоменклатуры,
		|	Остатки.Субконто3.ЗаказПокупателя,
		|	Остатки.Субконто3.Качество,
		|	Остатки.Субконто3,";
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	//НСП
	////////////
	Если СчетаУчетаНСП.Количество() > 0 Тогда
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	&ПустаяХарактеристика КАК ХарактеристикаНоменклатуры,
		|	&ПустаяСерия КАК СерияНоменклатуры,
		|	&ПустойЗаказ КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	&ПустоеКачество КАК Качество,
		|	&ПустаяНоменклатурнаяПозиция КАК НоменклатурнаяПозиция,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДокументОприходованияДата,
		|	Остатки.Субконто2 КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	Остатки.Субконто3 КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаНСП), &ВидысубконтоНСП,";
		
		
		Текст = Текст + "Субконто1 В (&Номенклатура) " + ?(НЕ ЗначениеЗаполнено(Партия), "", "И Субконто2 В (&Партия) ")+ "И Субконто3 В (&Склады) И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,
		|	Остатки.Субконто3,";
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	//КН
	////////////
	Если СчетаУчетаКН.Количество() > 0 Тогда // субкомиссия
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	&ПустаяХарактеристика КАК ХарактеристикаНоменклатуры,
		|	&ПустаяСерия КАК СерияНоменклатуры,
		|	&ПустойЗаказ КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	&ПустоеКачество КАК Качество,
		|	&ПустаяНоменклатурнаяПозиция КАК НоменклатурнаяПозиция,
		|	&ПустаяДата КАК ДокументОприходованияДата,
		|	&ПустаяПартия КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	Остатки.Субконто2 КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаКН), &ВидыСубконтоКН,";
		
		Текст = Текст + "Субконто1 В (&Номенклатура) И Субконто2 В (&Контрагент) И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,";
		
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	//КНД
	////////////
	Если СчетаУчетаКНД.Количество() > 0 Тогда // субкомиссия
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	Остатки.Субконто3.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	Остатки.Субконто3.СерияНоменклатуры КАК СерияНоменклатуры,
		|	Остатки.Субконто3.ЗаказПокупателя КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	Остатки.Субконто3.Качество КАК Качество,
		|	Остатки.Субконто3 КАК НоменклатурнаяПозиция,
		|	&ПустаяДата КАК ДокументОприходованияДата,
		|	&ПустаяПартия КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	Остатки.Субконто2 КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаКНД), &ВидыСубконтоКНД,";
		
		Текст = Текст + "Субконто1 В (&Номенклатура) И Субконто2 В (&Контрагент) И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,
		|	Остатки.Субконто3.ХарактеристикаНоменклатуры,
		|	Остатки.Субконто3.СерияНоменклатуры,
		|	Остатки.Субконто3.ЗаказПокупателя,
		|	Остатки.Субконто3.Качество,
		|	Остатки.Субконто3,";
		
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	//КНП
	////////////
	Если СчетаУчетаКНП.Количество() > 0 Тогда // субкомиссия
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	&ПустаяХарактеристика КАК ХарактеристикаНоменклатуры,
		|	&ПустаяСерия КАК СерияНоменклатуры,
		|	&ПустойЗаказ КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	&ПустоеКачество КАК Качество,
		|	&ПустаяНоменклатурнаяПозиция КАК НоменклатурнаяПозиция,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДокументОприходованияДата,
		|	Остатки.Субконто2 КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	Остатки.Субконто3 КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаКНП), &ВидыСубконтоКНП,";
		
		Текст = Текст + "Субконто1 В (&Номенклатура) " + ?(НЕ ЗначениеЗаполнено(Партия), "", "И Субконто2 В (&Партия) ")+ "И Субконто3 В (&Контрагент) И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,
		|	Остатки.Субконто3,";
		
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	//КНПД
	////////////
	Если СчетаУчетаКНПД.Количество() > 0 Тогда // субкомиссия
		
		Если Текст <> "" Тогда
			Текст = Текст + "
			|
			| ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		Текст = Текст + "ВЫБРАТЬ
		|	Остатки.Счет КАК СчетУчета,
		|	Остатки.Субконто1 КАК Номенклатура,
		|	Остатки.Субконто4.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	Остатки.Субконто4.СерияНоменклатуры КАК СерияНоменклатуры,
		|	Остатки.Субконто4.ЗаказПокупателя КАК ЗаказПокупателя,
		|	0 КАК СовпалЗаказ,
		|	Остатки.Субконто4.Качество КАК Качество,
		|	Остатки.Субконто4 КАК НоменклатурнаяПозиция,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДокументОприходованияДата,
		|	Остатки.Субконто2 КАК ДокументОприходования,
		|	&ПустаяДата КАК ДокументОтгрузкиДата,
		|	&ПустаяОтгрузка КАК ДокументОтгрузки,
		|	Остатки.Субконто3 КАК Склад,
		|	Остатки.Организация КАК Организация,
		|	Остатки.НалоговоеНазначение КАК НалоговоеНазначение,";
		
		
		Текст = Текст + "
		|	СУММА(ВЫБОР КОГДА Остатки.КоличествоОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.КоличествоОстатокДт КОНЕЦ) КАК Количество,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаОстатокДт КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА Остатки.СуммаНУОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ Остатки.СуммаНУОстатокДт КОНЕЦ) КАК СтоимостьНУ";
		
		Текст = Текст + "
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментКон, Счет В (&СчетаУчетаКНПД), &ВидыСубконтоКНПД,";
		
		Текст = Текст + "Субконто1 В (&Номенклатура) " + ?(НЕ ЗначениеЗаполнено(Партия), "", "И Субконто2 В (&Партия) ")+ "И Субконто3 В (&Контрагент) И Организация В (&Организации) И (НалоговоеНазначение В(&НалоговыеНазначения)" + ТекстПустоеНалоговоеНазначение + ")) КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.Субконто1,
		|	Остатки.Субконто2,
		|	Остатки.Субконто3,
		|	Остатки.Субконто4.ХарактеристикаНоменклатуры,
		|	Остатки.Субконто4.СерияНоменклатуры,
		|	Остатки.Субконто4.ЗаказПокупателя,
		|	Остатки.Субконто4.Качество,
		|	Остатки.Субконто4,";
		
		
		Текст = Текст + "
		|	Остатки.Организация,
		|	Остатки.НалоговоеНазначение";
		
	КонецЕсли;
	
	Запрос.Текст = Текст;
	
	СпособОценки = СтруктураПараметров["СпособОценкиБух"];
	Если СпособОценки = Перечисления.СпособыОценки.ЛИФО Тогда
		НапрДата = "Убыв";
	ИначеЕсли СпособОценки = Перечисления.СпособыОценки.ФИФО Тогда
		НапрДата = "Возр";
	Иначе
		НапрДата = "Убыв";                                                                                             
	КонецЕсли; 
	
	Запрос.Текст = Текст + "		
	|ИТОГИ ПО Номенклатура";
	
	Результат = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Результат.Колонки.Добавить("ДокументОприходованияМоментВремени", Новый ОписаниеТипов("МоментВремени"));
	Результат.Колонки.Добавить("ДокументОтгрузкиМоментВремени", Новый ОписаниеТипов("МоментВремени"));
	
	Если Не СчетаУчетаКНП.Количество() + СчетаУчетаНСП.Количество() + СчетаУчетаНПО.Количество() + СчетаУчетаНП.Количество()
		   +СчетаУчетаКНПД.Количество()+ СчетаУчетаНСПД.Количество()+ СчетаУчетаНПОД.Количество()+ СчетаУчетаНПД.Количество()= 0 Тогда
		
		ДобавитьДатуДокументаКРезультатуЗапроса(Результат,"ДокументОприходования","ДокументОприходованияДата", "ДокументОприходованияМоментВремени");
		
		Если  СчетаУчетаНПО.Количество() > 0 
			+ СчетаУчетаНПОД.Количество() > 0 Тогда
			ДобавитьДатуДокументаКРезультатуЗапроса(Результат,"ДокументОтгрузки","ДокументОтгрузкиДата", "ДокументОтгрузкиМоментВремени");
		КонецЕсли;
		
		Результат.Строки.Сортировать("ДокументОприходованияМоментВремени " + НапрДата + ", ДокументОтгрузкиМоментВремени "+ НапрДата, Истина, Новый СравнениеЗначений);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ПолучитьТаблицуПартийНаСкладах()

Функция ДобавитьДатуДокументаКРезультатуЗапроса(Результат,КолонкаДокумента,КолонкаСДатой, КолонкаСМоментомВремени)

	КэшПоТипам = Новый Соответствие;

	Для каждого Строка из Результат.Строки Цикл
		Для каждого СтрокаТаблицы из Строка.строки Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы[КолонкаДокумента]) тогда 
				Продолжить;
			КонецЕсли;
			
			ТипТекущегоДокумента = ТипЗнч(СтрокаТаблицы[КолонкаДокумента]);
			МассивТипа = КэшПоТипам[ТипТекущегоДокумента];
			Если МассивТипа = Неопределено Тогда
				МассивТипа = Новый Массив;
				КэшПоТипам.Вставить(ТипТекущегоДокумента, МассивТипа);
			КонецЕсли;
			МассивТипа.Добавить(СтрокаТаблицы[КолонкаДокумента]);
		КонецЦикла;
	КонецЦикла;
	
	Если КэшПоТипам.Количество()=0 тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = новый запрос;

	Для Каждого КлючИЗначение ИЗ КэшПоТипам Цикл
		ИмяМетаданных = Метаданные.НайтиПоТипу(КлючИЗначение.Ключ).Имя;

		Запрос.Текст = Запрос.Текст+
		?(Запрос.Текст="","",
		" 
		|Объединить все")+
		"
		|	ВЫБРАТЬ
		|		Док.Ссылка Как Ссылка,
		|		Док.Дата,
		|		Док.МоментВремени
		|	ИЗ Документ."+ИмяМетаданных+" КАК Док
		|	Где Док.ссылка в  (&ДокументыТипа_"+ИмяМетаданных+")";
		Запрос.УстановитьПараметр("ДокументыТипа_"+ИмяМетаданных, КлючИЗначение.Значение);
		
	КонецЦикла;

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить(КолонкаДокумента, Выборка.Ссылка);

		НайденныеСтроки = Результат.строки.НайтиСтроки(ПараметрыОтбора,Истина);
		Для Каждого Строка Из НайденныеСтроки Цикл
			Строка[КолонкаСДатой] 			= Выборка.Дата;
			Строка[КолонкаСМоментомВремени] = Выборка.МоментВремени;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ДобавитьДатуДокументаКРезультатуЗапроса()

// Создается пустая таблица, в которую будут в дальнейшем записыватся списанные партии.
//
Функция ПустаяТаблицаСписанныхПартий() Экспорт

	ТаблицаСписанныхПартий = Новый ТаблицаЗначений;
	ТаблицаСписанныхПартий.Колонки.Добавить("Партия");
	ТаблицаСписанныхПартий.Колонки.Добавить("Количество", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 3));
	ТаблицаСписанныхПартий.Колонки.Добавить("СуммаСписания", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	ТаблицаСписанныхПартий.Колонки.Добавить("СуммаСписанияНУ", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	
	ТаблицаСписанныхПартий.Колонки.Добавить("СчетУчетаБУ");
	ТаблицаСписанныхПартий.Колонки.Добавить("НоменклатурнаяПозиция", Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеПозиции"));
		
	ТаблицаСписанныхПартий.Колонки.Добавить("Комиссионный");
	
	Возврат ТаблицаСписанныхПартий;
	
КонецФункции // ПустаяТаблицаСписанныхПартий()

// По счету учета опреедляется комиссионный товар или собственный
//
// Возвращаемое значение:
//   Булево – ИСТИНА - если товар комисионный,
//	 		  иначе функция возвращает ЛОЖЬ
Функция КомиссионныйТовар(СчетУчета) Экспорт

	Комиссионный = Ложь;
	Если НЕ ЗначениеЗаполнено(СчетУчета) Тогда
	     Возврат Ложь;
	КонецЕсли; 
	
	Если ТипЗнч(СчетУчета) = Тип("ПланСчетовСсылка.Хозрасчетный") Тогда
		
		Комиссионный = СчетУчета.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ТоварыПринятыеНаКомиссиюВсего);
		
	КонецЕсли;
	
	Возврат Комиссионный;

КонецФункции // КомиссионныйТовар()

Функция ПолучитьОбъектПоТипу(ТипСтр, Субконто1, Субконто2, Субконто3) Экспорт

	ИскомыйТип = Тип(ТипСтр);
	ИскомыйОбъект = Неопределено;
	
	Если ТипЗнч(Субконто1) = ИскомыйТип Тогда
		ИскомыйОбъект = Субконто1;
		
	ИначеЕсли ТипЗнч(Субконто2) = ИскомыйТип Тогда
		ИскомыйОбъект = Субконто2;
		
	ИначеЕсли ТипЗнч(Субконто3) = ИскомыйТип Тогда
		ИскомыйОбъект = Субконто3;
	КонецЕсли; 	
	
	Возврат ИскомыйОбъект;

КонецФункции // ПолучитьОбъектПоТипу()

// Розница в продажных ценах

Процедура ВыполнитьПереоценку(ТаблицаСписания) Экспорт
	
	Если ТаблицаСписания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МоментКон = Новый МоментВремени(ТаблицаСписания[0].Регистратор.Дата, ТаблицаСписания[0].Регистратор.Ссылка);
	
	// Структура общих параметров, используемых в большинстве процедур
	СтруктураПараметров = Новый Структура;

	Если ТаблицаСписания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Бух = Истина;
	
	СтруктураПараметров.Вставить("МоментКон", МоментКон);
	СтруктураПараметров.Вставить("Организация", ТаблицаСписания[0].Организация);                           	
	СтруктураПараметров.Вставить("УчетнаяПолитика", ПолучитьУчетнуюПолитику(СтруктураПараметров.МоментКон, СтруктураПараметров.Организация));	
	СтруктураПараметров.Вставить("ПроводитьДокументПоРазделуУчета", ОбщегоНазначения.ПроводитьДокументПоРазделуУчета(СтруктураПараметров.Организация, Перечисления.РазделыУчета.ОценкаМПЗ, СтруктураПараметров.МоментКон.Дата));
	
	СтруктураПараметров.Вставить("СпособОценкиБух", УчетнаяПолитика("СпособОценкиМПЗ", "Бух", СтруктураПараметров.Организация, СтруктураПараметров));
	
	// Сформируем структура отбора данных при формировании запроса по номенклатуре
	МассивНоменклатуры = ТаблицаСписания.ВыгрузитьКолонку("Номенклатура");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивНоменклатуры);

	МассивСчетовУчетаБУ = ТаблицаСписания.ВыгрузитьКолонку("СчетУчетаБУ");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСчетовУчетаБУ);

	МассивСкладов = ТаблицаСписания.ВыгрузитьКолонку("Склад");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивСкладов);

	МассивОрганизаций = ТаблицаСписания.ВыгрузитьКолонку("Организация");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивОрганизаций);

	МассивНалоговыхНазначений = ТаблицаСписания.ВыгрузитьКолонку("НалоговоеНазначение");
	ОбщегоНазначения.УдалитьПовторяющиесяЭлементы(МассивНалоговыхНазначений);

	СтруктураПараметров.Вставить("ДеревоПартийТоваровНаСкладахБух", ПолучитьТаблицуПартийНаСкладах(СтруктураПараметров, "Бух", МассивОрганизаций, МассивНалоговыхНазначений, МассивСчетовУчетаБУ, МассивНоменклатуры, МассивСкладов, , , ));
		
	// Добавим колонку в которой будут отражаться списанные по БУ партии
	Если ТаблицаСписания.Колонки.Найти("СписанныеПартииБУ") = Неопределено Тогда
		ТаблицаСписания.Колонки.Добавить("СписанныеПартииБУ");
	КонецЕсли;

	СтруктураПараметров.Вставить("УчетнаяПолитика",    ПолучитьУчетнуюПолитику(МоментКон, ТаблицаСписания[0].Организация));
	
	БУ = ПланыСчетов.Хозрасчетный.ТоварыНаСкладе.ПолучитьОбъект();
	ВестиУчетПоДопРазрезамРегл  = ?(БУ.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеПозиции, "ВидСубконто") = Неопределено, Ложь, Истина);
	СтруктураПараметров.Вставить("ВестиУчетПоДопРазрезамРегл", ВестиУчетПоДопРазрезамРегл);
		
	Для Каждого СтрокаДокумента Из ТаблицаСписания Цикл

		// Списание со склада
		ПереоценкаПоСкладу(СтрокаДокумента, СтруктураПараметров);

	КонецЦикла;
	
КонецПроцедуры

Процедура ПереоценкаПоСкладу(СтрокаДокумента, СтруктураПараметров)

	Учет = "Бух";
	
	// Партии для данного учета
	ДеревоОстатковПартий = СтруктураПараметров["ДеревоПартийТоваровНаСкладах" + Учет];
	
	ОтборПоНалоговомуНазначению = СтрокаДокумента.НалоговоеНазначение;
	
	СчетУчета = СтрокаДокумента.СчетУчетаБУ;
	СтрокаДокумента.СписанныеПартииБУ = ПустаяТаблицаСписанныхПартий();
	ТаблицаСписанныхПартии = СтрокаДокумента.СписанныеПартииБУ;

	Если ДеревоОстатковПартий.Строки.Количество() <> 0 Тогда

		// Пустой склад покупателя - значение измерения "Склад" когда не ведется учет по складам (или не ведется суммовой учет по складам)
		ПустойСклад = Справочники.Склады.ПустаяСсылка();
		
		СтрокаНоменклатуры = ДеревоОстатковПартий.Строки.Найти(СтрокаДокумента.Номенклатура, "Номенклатура");		
		ПартийПоСтроке = ?(СтрокаНоменклатуры = Неопределено, -1, СтрокаНоменклатуры.Строки.Количество() - 1);

		Для индекс = 0 по ПартийПоСтроке Цикл
			
			СтрокаПартии = СтрокаНоменклатуры.Строки[индекс];
			
			Если СтрокаПартии.СчетУчета <> СчетУчета Тогда
				Продолжить;
			КонецЕсли;
			
			// Склад, если ведется суммовой учет по складам
			Если ВедетсяСуммовойУчетПоСкладам(СчетУчета) Тогда

				Если СтрокаПартии.Склад <> СтрокаДокумента.Склад 
				   И СтрокаПартии.Склад <> ПустойСклад Тогда // пустые склады могут остаться со времени, когда не было учета по складам
					Продолжить;
				КонецЕсли;
				
			КонецЕсли;
			
			Если ОтборПоНалоговомуНазначению <> Неопределено Тогда
				Если СтрокаПартии.НалоговоеНазначение <> ОтборПоНалоговомуНазначению Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;

			Если     СтруктураПараметров.ВестиУчетПоДопРазрезамРегл Тогда
				Если НЕ ЗначениеЗаполнено(СтрокаДокумента.ХарактеристикаНоменклатуры) = ЗначениеЗаполнено(СтрокаПартии.ХарактеристикаНоменклатуры) Тогда
					Продолжить
				ИначеЕсли ЗначениеЗаполнено(СтрокаДокумента.ХарактеристикаНоменклатуры)
					 И НЕ СтрокаДокумента.ХарактеристикаНоменклатуры = СтрокаПартии.ХарактеристикаНоменклатуры Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Количество = Окр(СтрокаПартии.Количество,3,1);
			Стоимость  = Окр(СтрокаПартии.Стоимость,2,1);
			СтоимостьНУ= Окр(СтрокаПартии.СтоимостьНУ,2,1);
			
			СтрокаПартии.Количество = СтрокаПартии.Количество - Количество;
            СтрокаПартии.Стоимость  = СтрокаПартии.Стоимость  - Стоимость;
            СтрокаПартии.СтоимостьНУ= СтрокаПартии.СтоимостьНУ  - СтоимостьНУ;
		
			//Формирование проводок Бух
			СуммаПереоценки = (Количество * СтрокаДокумента.ЦенаВРознице) - Стоимость;
			СуммаПереоценкиНУ = (Количество * СтрокаДокумента.ЦенаВРознице) - СтоимостьНУ;
			
			Если СуммаПереоценки <> 0 ИЛИ СуммаПереоценкиНУ<>0 Тогда
				Проводка 			 = СтрокаДокумента.Регистратор.Движения.Хозрасчетный.Добавить();
				Проводка.Период      = СтрокаДокумента.Регистратор.Дата;
				Проводка.Организация = СтрокаДокумента.Организация;
				Проводка.Содержание  = "Переоценка товаров в рознице";
				
				Проводка.СчетДт      = СчетУчета;
				Проводка.СчетКт      = СтрокаДокумента.КорСчетСписанияБУ;
				
				ОбщегоНазначения.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Склады",  СтрокаДокумента.Склад);
				ОбщегоНазначения.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Склады",  СтрокаДокумента.Склад);
				
				ОбщегоНазначения.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Номенклатура",  СтрокаДокумента.Номенклатура);
				ОбщегоНазначения.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Номенклатура",  СтрокаДокумента.Номенклатура);
				
				ОбщегоНазначения.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НоменклатурныеПозиции",  СтрокаПартии.НоменклатурнаяПозиция);
				ОбщегоНазначения.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "НоменклатурныеПозиции",  СтрокаПартии.НоменклатурнаяПозиция);
				
				ОбщегоНазначения.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "Партии",  СтрокаПартии.ДокументОприходования);
				ОбщегоНазначения.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Партии",  СтрокаПартии.ДокументОприходования);
				
				Проводка.Сумма       = СуммаПереоценки;
				
				ПроводкаДо2015 = Проводка.Период < глЗначениеПеременной("ДатаНКУ2015");
				Если ПроводкаДо2015 Тогда
					Если Проводка.СчетДт.НалоговыйУчет Тогда
						Проводка.СуммаНУДт = СуммаПереоценкиНУ;
						Проводка.НалоговоеНазначениеДт = СтрокаДокумента.НалоговоеНазначение;
					КонецЕсли;
					Если Проводка.СчетКт.НалоговыйУчет Тогда
						Проводка.СуммаНУКт = СуммаПереоценкиНУ;
						Проводка.НалоговоеНазначениеКт = СтрокаДокумента.НалоговоеНазначение;
					КонецЕсли;
				Иначе
					Если Проводка.СчетДт.НалоговыйУчет Тогда
						Если Проводка.СчетДт.УчетСуммНУ Тогда
							Проводка.СуммаНУДт = СуммаПереоценкиНУ;
						КонецЕсли;
						Если Проводка.СчетДт.УчетПоНалоговымНазначениямНДС Тогда
							Проводка.НалоговоеНазначениеДт = СтрокаДокумента.НалоговоеНазначение;
						КонецЕсли; 
					КонецЕсли;
					Если Проводка.СчетКт.НалоговыйУчет Тогда
						Если Проводка.СчетКт.УчетСуммНУ Тогда
							Проводка.СуммаНУКт = СуммаПереоценкиНУ;
						КонецЕсли;
						Если Проводка.СчетКт.УчетПоНалоговымНазначениямНДС Тогда
							Проводка.НалоговоеНазначениеКт = СтрокаДокумента.НалоговоеНазначение;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
			// В таблицу списанных партий записывается строка с информацией.
			СтрокаСписаннаяПартия               = ТаблицаСписанныхПартии.Добавить();
			СтрокаСписаннаяПартия.Партия        = СтрокаПартии.ДокументОприходования;
			СтрокаСписаннаяПартия.Количество    = Количество;
			СтрокаСписаннаяПартия.СуммаСписания = Стоимость;
			СтрокаСписаннаяПартия.СуммаСписанияНУ = СтоимостьНУ;
			СтрокаСписаннаяПартия.Комиссионный  = КомиссионныйТовар(СчетУчета);

			СтрокаСписаннаяПартия.СчетУчетаБУ = СчетУчета;
						
		КонецЦикла;

	КонецЕсли;
    	
КонецПроцедуры // ПереоценкаПоСкладу()

Функция ТорговаяНаценкаНТТ(ДокументСсылка, Склады, РазделятьПоСтавкамНДС) Экспорт
	
	Запрос = Новый Запрос;
	// В НУ период определения наценки - квартал
	Запрос.УстановитьПараметр("ДатаНач", 			НачалоМесяца(ДокументСсылка.Дата));
	Запрос.УстановитьПараметр("ДатаКон", 			ДокументСсылка.МоментВремени());
	Запрос.УстановитьПараметр("Склады", 			Склады);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыВНТТВПродажныхЦенахОстаткиИОбороты.Склад КАК Склад,"+?(РазделятьПоСтавкамНДС,"
	|	ТоварыВНТТВПродажныхЦенахОстаткиИОбороты.СтавкаНДС КАК СтавкиНДС,","")+"
	|	СУММА(ЕСТЬNULL(ТоварыВНТТВПродажныхЦенахОстаткиИОбороты.СуммаНачальныйОстаток,0) + ЕСТЬNULL(ТоварыВНТТВПродажныхЦенахОстаткиИОбороты.СуммаПриход,0)) КАК ПС,
	|	СУММА(ЕСТЬNULL(ТоварыВНТТВПродажныхЦенахОстаткиИОбороты.СуммаНаценкиНачальныйОстаток,0) + ЕСТЬNULL(ТоварыВНТТВПродажныхЦенахОстаткиИОбороты.СуммаНаценкиПриход,0)) КАК ТН
	|ИЗ
	|	РегистрНакопления.ТоварыВНТТВПродажныхЦенах.ОстаткиИОбороты(&ДатаНач, &ДатаКон, , , Склад В (&Склады)) КАК ТоварыВНТТВПродажныхЦенахОстаткиИОбороты
	|СГРУППИРОВАТЬ ПО
	|	ТоварыВНТТВПродажныхЦенахОстаткиИОбороты.Склад"+?(РазделятьПоСтавкамНДС,",
	|	ТоварыВНТТВПродажныхЦенахОстаткиИОбороты.СтавкаНДС","")+"";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

//Расчитывает параметры для определения средней наценки сагласно стандарту "Запасы"
Функция ТорговаяНаценкаНТТБух(ДокументСсылка, Склады, НалоговоеНазначение, РазделятьПоСтавкамНДС) Экспорт
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	Если РазделятьПоСтавкамНДС Тогда
		ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС);
	КонецЕсли;
	Запрос = Новый Запрос;
	// В БУ период определения наценки месяц
	Запрос.УстановитьПараметр("ДатаНач", 			НачалоМесяца(ДокументСсылка.Дата));
	Запрос.УстановитьПараметр("ДатаКон", 			ДокументСсылка.МоментВремени());
	Запрос.УстановитьПараметр("Организация", 		ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("СчетТН", 			ПланыСчетов.Хозрасчетный.ТорговаяНаценкаНТТ);
	Запрос.УстановитьПараметр("СчетПС", 			ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ);
	Запрос.УстановитьПараметр("Склады", 			Склады);
	Запрос.УстановитьПараметр("НалоговоеНазначение",НалоговоеНазначение);
	Запрос.УстановитьПараметр("ВидыСубконто", 		ВидыСубконто);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОборотыТН.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто1 КАК Склад,"+?(РазделятьПоСтавкамНДС,"
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто2 КАК СтавкиНДС,","")+"
	|	СУММА(
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыТН.СуммаНачальныйОстатокКт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыТН.СуммаНачальныйОстатокКт КОНЕЦ + 
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыТН.СуммаОборотКт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыТН.СуммаОборотКт КОНЕЦ
	|	) КАК ТН,
	|	СУММА(
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыПС.СуммаНачальныйОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыПС.СуммаНачальныйОстатокДт КОНЕЦ + 
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыПС.СуммаОборотДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыПС.СуммаОборотДт КОНЕЦ 
	|) КАК ПС,
	|	СУММА(
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыТН.СуммаНУНачальныйОстатокКт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыТН.СуммаНУНачальныйОстатокКт КОНЕЦ + 
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыТН.СуммаНУОборотКт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыТН.СуммаНУОборотКт КОНЕЦ
	|	) КАК ТН_НУ,
	|	СУММА(
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыПС.СуммаНУНачальныйОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыПС.СуммаНУНачальныйОстатокДт КОНЕЦ + 
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыПС.СуммаНУОборотДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыПС.СуммаНУОборотДт КОНЕЦ 
	|	) КАК ПС_НУ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&ДатаНач,&ДатаКон, , ,Счет = &СчетТН, &ВидыСубконто, НалоговоеНазначение В (&НалоговоеНазначение) И Организация = &Организация И Субконто1 В (&Склады)) КАК ХозрасчетныйОстаткиИОборотыТН
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&ДатаНач,&ДатаКон, , ,Счет = &СчетПС, &ВидыСубконто, НалоговоеНазначение В (&НалоговоеНазначение) И Организация = &Организация И Субконто1 В (&Склады)) КАК ХозрасчетныйОстаткиИОборотыПС
	|		ПО ХозрасчетныйОстаткиИОборотыТН.НалоговоеНазначение = ХозрасчетныйОстаткиИОборотыПС.НалоговоеНазначение 
	|			И ХозрасчетныйОстаткиИОборотыТН.Субконто1 = ХозрасчетныйОстаткиИОборотыПС.Субконто1"+?(РазделятьПоСтавкамНДС,"
	|			И ХозрасчетныйОстаткиИОборотыТН.Субконто2 = ХозрасчетныйОстаткиИОборотыПС.Субконто2","")+"
	|			
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстаткиИОборотыТН.НалоговоеНазначение,
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто1"+?(РазделятьПоСтавкамНДС,",
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто2","")+"
	|";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ТорговаяНаценкаАТТ(ДокументСсылка, Склады, Номенклатура, НалоговоеНазначение) Экспорт
	
	БУ = ПланыСчетов.Хозрасчетный.ТоварыНаСкладе.ПолучитьОбъект();
	ВестиУчетПоДопРазрезамРегл  = ?(БУ.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеПозиции, "ВидСубконто") = Неопределено, Ложь, Истина);
	
	РазделятьПоПартиям = НаСчетеВедетсяПартионныйУчет(ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ);
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	
	Если РазделятьПоПартиям Тогда
		ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии);
		НомерСубконтоНоменклатурнойПозиции = 4;
	Иначе
		НомерСубконтоНоменклатурнойПозиции = 3;
	КонецЕсли;
	
	Если ВестиУчетПоДопРазрезамРегл Тогда
		ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеПозиции);
	КонецЕсли;
		
	Запрос = Новый Запрос;
	// В БУ период определения наценки месяц
	Запрос.УстановитьПараметр("ДатаНач", 			НачалоМесяца(ДокументСсылка.Дата));
	Запрос.УстановитьПараметр("ДатаКон", 			ДокументСсылка.МоментВремени());
	Запрос.УстановитьПараметр("Организация", 		ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("СчетТН", 			ПланыСчетов.Хозрасчетный.ТорговаяНаценкаАТТ);
	Запрос.УстановитьПараметр("СчетПС", 			ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ);
	Запрос.УстановитьПараметр("Склады", 			Склады);
	Запрос.УстановитьПараметр("Номенклатура", 		Номенклатура);
	Запрос.УстановитьПараметр("НалоговоеНазначение",НалоговоеНазначение);
	Запрос.УстановитьПараметр("ВидыСубконто", 		ВидыСубконто);
	Запрос.УстановитьПараметр("ПустаяПартия",    	Неопределено);
	Запрос.УстановитьПараметр("ПустаяНоменклатурнаяПозиция",   Справочники.НоменклатурныеПозиции.ПустаяСсылка());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОборотыТН.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто1 КАК Номенклатура,
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто2 КАК Склад,
	|"+?(РазделятьПоПартиям, "
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто3 КАК Партия,", "
	|	&ПустаяПартия КАК Партия,")+ "
	|"+?(ВестиУчетПоДопРазрезамРегл, "
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто"+НомерСубконтоНоменклатурнойПозиции+" КАК НоменклатурнаяПозиция,", "
	|	&ПустаяНоменклатурнаяПозиция КАК НоменклатурнаяПозиция,") + "
	|	СУММА(
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыТН.СуммаНачальныйОстатокКт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыТН.СуммаНачальныйОстатокКт КОНЕЦ + 
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыТН.СуммаОборотКт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыТН.СуммаОборотКт КОНЕЦ
	|	) КАК ТН,
	|	СУММА(
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыПС.СуммаНачальныйОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыПС.СуммаНачальныйОстатокДт КОНЕЦ + 
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыПС.СуммаОборотДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыПС.СуммаОборотДт КОНЕЦ 
	|) КАК ПС,
	|	СУММА(
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыТН.СуммаНУНачальныйОстатокКт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыТН.СуммаНУНачальныйОстатокКт КОНЕЦ + 
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыТН.СуммаНУОборотКт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыТН.СуммаНУОборотКт КОНЕЦ
	|	) КАК ТН_НУ,
	|	СУММА(
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыПС.СуммаНУНачальныйОстатокДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыПС.СуммаНУНачальныйОстатокДт КОНЕЦ + 
	|	ВЫБОР КОГДА ХозрасчетныйОстаткиИОборотыПС.СуммаНУОборотДт ЕСТЬ NULL ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОборотыПС.СуммаНУОборотДт КОНЕЦ
	|	) КАК ПС_НУ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&ДатаНач,&ДатаКон, , ,Счет = &СчетТН, &ВидыСубконто, НалоговоеНазначение В (&НалоговоеНазначение) И Организация = &Организация И Субконто1 В (&Номенклатура) И Субконто2 В (&Склады)) КАК ХозрасчетныйОстаткиИОборотыТН
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&ДатаНач,&ДатаКон, , ,Счет = &СчетПС, &ВидыСубконто, НалоговоеНазначение В (&НалоговоеНазначение) И Организация = &Организация И Субконто1 В (&Номенклатура) И Субконто2 В (&Склады)) КАК ХозрасчетныйОстаткиИОборотыПС
	|		ПО ХозрасчетныйОстаткиИОборотыТН.НалоговоеНазначение = ХозрасчетныйОстаткиИОборотыПС.НалоговоеНазначение
	|			И ХозрасчетныйОстаткиИОборотыТН.Субконто1 = ХозрасчетныйОстаткиИОборотыПС.Субконто1
	|			И ХозрасчетныйОстаткиИОборотыТН.Субконто2 = ХозрасчетныйОстаткиИОборотыПС.Субконто2"+?(РазделятьПоПартиям,"
	|			И ХозрасчетныйОстаткиИОборотыТН.Субконто3 = ХозрасчетныйОстаткиИОборотыПС.Субконто3","")+?(ВестиУчетПоДопРазрезамРегл,"
	|			И ХозрасчетныйОстаткиИОборотыТН.Субконто"+НомерСубконтоНоменклатурнойПозиции+" = ХозрасчетныйОстаткиИОборотыПС.Субконто"+НомерСубконтоНоменклатурнойПозиции,"")+"
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстаткиИОборотыТН.НалоговоеНазначение,
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто1,
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто2"+?(РазделятьПоПартиям, ",
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто3", "")+?(ВестиУчетПоДопРазрезамРегл, ",
	|	ХозрасчетныйОстаткиИОборотыТН.Субконто"+НомерСубконтоНоменклатурнойПозиции, "")+"
	|";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

/////////////////////////////////////////////////////////////////////////////////
// КОРРЕСПОНДИРУЮЩИЕ СПИСАНИЮ ДВИЖЕНИЯ (ДЕБЕТ)

// Упр

// Поступление товаров в незавершенное производство
//
// Параметры
//  СтруктураПараметров  – Структура, содержащая общие параметры
//
Процедура ПоступлениеВПроизводствоУпр(СписаноИз, СтрокаДокумента, СтруктураПараметров, Строка)
	
	Учет = "Упр";
	
	Движение = ДобавитьДвижениеВСтруктуруПараметров("НезавершенноеПроизводство"+Учет, СтруктураПараметров);
	
	// Свойства
	Движение.Период      = Строка.Период;
	Движение.Регистратор = Строка.Регистратор;
	Движение.Активность = Истина;
	
	Движение.ВидДвижения			= ВидДвиженияНакопления.Приход;
	
	Движение.Подразделение 			= СтрокаДокумента.Подразделение;
	Движение.НоменклатурнаяГруппа 	= СтрокаДокумента.НоменклатурнаяГруппа;
	
	
	
	// Ресурсы
	
	Движение.Стоимость	= Строка.Стоимость;
	Движение.НДС  		= Строка.НДС;
	
	// Реквизиты
	Движение.КодОперации		    = Перечисления.КодыОперацийНезавершенноеПроизводство.СписаниеПартийВПроизводствоОперативно; 
	Движение.СтатьяЗатрат 			= СтрокаДокумента.СтатьяЗатрат;
	Движение.НоменклатураЗатрат	    = Строка.Номенклатура;
	Движение.НоменклатураЗатратКоличество = Строка.Количество;
	
	Движение.ДокументОприходования  = Строка.ДокументОприходования;
	Если СписаноИз = "НаСкладах" Тогда
		// Склад, откуда списана номенклатура есть только, если списуем со склада
		Движение.Склад	    			= Строка.Склад;
	КонецЕсли;
	
	
	Строка.НомерКорСтроки = СтруктураПараметров["ТекНомерСтрокиНезавершенноеПроизводство"+Учет];
	
КонецПроцедуры

// Списание на брак
//
// Параметры
//
Процедура СписаниеНаБракУпр(СтрокаДокумента, СтруктураПараметров, Строка)
	
	Учет = "Упр";
	
	Движение = ДобавитьДвижениеВСтруктуруПараметров("БракВПроизводстве"+Учет, СтруктураПараметров);
	
	// Свойства
	Движение.Период      = Строка.Период;
	Движение.Регистратор = Строка.Регистратор;
	Движение.Активность = Истина;
	
	Движение.ВидДвижения			= ВидДвиженияНакопления.Приход;
	
	Движение.Подразделение 			= СтрокаДокумента.Подразделение;
	Движение.НоменклатурнаяГруппа 	= СтрокаДокумента.НоменклатурнаяГруппа;
	Движение.СтатьяЗатрат 			= СтрокаДокумента.СтатьяЗатрат;
	
	
	Движение.Сумма				= Строка.Стоимость;
	Движение.НДС 				= Строка.НДС;
	
	// Реквизиты
	Движение.КодОперации		    = Перечисления.КодыОперацийБракВПроизводстве.СписаниеПартийВПроизводствоОперативно; 
	
	Строка.НомерКорСтроки = СтруктураПараметров["ТекНомерСтрокиБракВПроизводстве"+Учет];
	
КонецПроцедуры

// Списание на амортизацию
Процедура СписаниеНаАмортизациюУпр(СтрокаДокумента, СтруктураПараметров, Строка)
	
	Учет = "Упр";
	
	Движение = ДобавитьДвижениеВСтруктуруПараметров("СтоимостьОС"+Учет, СтруктураПараметров);
	
	// Свойства
	Движение.Период      = Строка.Период;
	Движение.Регистратор = Строка.Регистратор;
	Движение.Активность  = Истина;
	
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	
	Движение.ОсновноеСредство = СтрокаДокумента.ОсновноеСредство;
	
	Движение.Стоимость = Строка.Стоимость;
	
	Если СтрокаДокумента.Регистратор.СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС._50_50 Тогда
		Движение.Амортизация = Строка.Стоимость / 2;
	ИначеЕсли СтрокаДокумента.Регистратор.СпособНачисленияАмортизацииБУ = Перечисления.СпособыНачисленияАмортизацииОС._100 Тогда
		Движение.Амортизация = Строка.Стоимость;
	КонецЕсли;	
	
	Строка.НомерКорСтроки = СтруктураПараметров["ТекНомерСтрокиСтоимостьОС"+Учет];
	
КонецПроцедуры

// Передача малоценки в эксплуатацию
Процедура ПередачаМалоценкиВЭксплуатациюУпр(СтрокаДокумента, СтруктураПараметров, Строка)
	
	ИмяРегистра = "ПартииМалоценкиВЭксплуатации" 
	+ "Упр";
	
	Движение = ДобавитьДвижениеВСтруктуруПараметров(ИмяРегистра, СтруктураПараметров);
	
	// Свойства
	Движение.Период      = Строка.Период;
	Движение.Регистратор = Строка.Регистратор;
	Движение.Активность  = Истина;
	
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	
	Движение.КодОперации = Перечисления.КодыОперацийПартииМалоценкиВЭксплуатации.СписаниеПартийВЭксплуатацию;
	
	Движение.Подразделение           = СтрокаДокумента.Подразделение;
	Движение.Номенклатура            = Строка.Номенклатура;
	Движение.ХарактеристикаНоменклатуры = Строка.ХарактеристикаНоменклатуры;
	Движение.СерияНоменклатуры       = Строка.СерияНоменклатуры;
	Движение.Качество   = Справочники.Качество.Новый;
	
	Движение.ПартияМалоценкиВЭксплуатации = СтрокаДокумента.ДокументПередачи;
	Движение.ФизЛицо                 = СтрокаДокумента.ФизЛицо;
	Движение.НазначениеИспользования = СтрокаДокумента.НазначениеИспользования;
	
	Движение.ДокументОприходования   = Строка.ДокументОприходования;
	
	Движение.Количество = Строка.Количество;
	Движение.Стоимость  = Строка.Стоимость;
	
	Строка.НомерКорСтроки = СтруктураПараметров["ТекНомерСтроки"+ИмяРегистра];
	
КонецПроцедуры // ПередачаМатериаловВЭксплуатациюУпр()

// Вложения в объекты внеоборотных активов 
//
// Параметры
//
Процедура ВложенияВоВнеоборотныеАктивыУпр(СтрокаДокумента, СтруктураПараметров, Строка)
	
	Учет = "Упр";
	
	// Движение по регистру накопления стоимости объектов строительства
	Движение = ДобавитьДвижениеВСтруктуруПараметров("СтроительствоОбъектовОсновныхСредствУпр", СтруктураПараметров);
	
	// Свойства
	Движение.Период      = Строка.Период;
	Движение.Регистратор = Строка.Регистратор;
	Движение.Активность = Истина;
	
	Движение.ВидДвижения         = ВидДвиженияНакопления.Приход;
	
	Движение.ОбъектСтроительства = СтрокаДокумента.ОбъектСтроительства;
	
	Движение.Сумма				 = Строка.Стоимость;
	
	// Реквизиты
	//Строка.НомерКорСтроки = СтруктураПараметров["ТекНомерСтрокиСтроительствоОбъектовОсновныхСредств"];
	
	// Движение по регистру учета оборота затрат
	//Движение = ДобавитьДвижениеВСтруктуруПараметров("ЗатратыНаСтроительствоОбъектовОсновныхСредств", СтруктураПараметров);
	//
	//Движение.Период      = Строка.Период;
	//Движение.Регистратор = Строка.Регистратор;
	//Движение.Активность = Истина;
	//
	//Движение.ОбъектСтроительства = СтрокаДокумента.ОбъектСтроительства;
	//Движение.СтатьяЗатрат        = СтрокаДокумента.СтатьяЗатрат;
	//
	//Движение.Сумма				 = Строка.Стоимость;
	//
	//Строка.НомерКорСтроки = СтруктураПараметров["ТекНомерСтрокиЗатратыНаСтроительствоОбъектовОсновныхСредств"];
	
	// Реквизиты
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СПЕЦИФИЧЕСКИЕ ПРОЦЕДУРЫ ДВИЖЕНИЯ ПАРТИЙ ДЛЯ ОПРЕДЕЛЕННЫХ ВИДОВ ДОКУМЕНТОВ

////////////////////////////////////////////////////////////////////////////////
// прочие специфические процедуры

// Перемещение в НТТ с суммовым учетом с розничного, оптового либо НТТ без суммового учета
//
Процедура ПеремещениеВСНТТ(СтрокаДокумента, СтруктураПараметров, РегистрУчета)
	
	Учет = "Упр";
	
	КодыОпераций = Перечисления.КодыОперацийПартииТоваров;
	
	БУ = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ.ПолучитьОбъект();
	РазделятьПоСтавкамНДС  = ?(БУ.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС, "ВидСубконто") = Неопределено, Ложь, Истина);
	
	ВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	ВалютаУправленческогоУчета = глЗначениеПеременной("ВалютаУправленческогоУчета");
	Данные = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаУправленческогоУчета, СтруктураПараметров.Период);
		
	// Партии для данного учета
	ИмяРегистра = ПолучитьИмяРегистра("ПартииТоваров", РегистрУчета, СтрокаДокумента);
	
	// Если нет регистра, по которому списываем, тогда пропускаем
	Если ИмяРегистра="" Тогда
		Возврат;
	КонецЕсли;
	
	ДеревоПартий = СтруктураПараметров["Дерево"+ИмяРегистра];
	
	СтруктураИзмерений = СтруктураПараметров[ИмяРегистра + "СтруктураИзмерений"];
	ТаблицаОстатковПартий = СтруктураПараметров[ИмяРегистра + "ТаблицаОстатков"];
	
	СтрокаДереваПартий = ДеревоПартий.Строки.Найти(СтрокаДокумента.НомерСтрокиДокумента, "НомерСтрокиДокумента");
	
	// Подлежащее погашению при списании количество
	КоличествоОсталосьПогасить = СтрокаДокумента.Количество;
	
	// В строке может быть указана стоимость поступления, если затем товар будет оприходован с другой стоимостью
	СтоимостьПоступлениеОсталосьПогасить = 0;
	Если СтрокаДокумента.ОтражатьВУправленческомУчете Тогда
		СтоимостьПоступлениеОсталосьПогасить = СтрокаДокумента.СтоимостьПоступление;
	КонецЕсли;
	
	КоличествоПоступление = СтрокаДокумента.КоличествоПоступление;
	
	СтоимостьСписано = 0;
	Если СтрокаДереваПартий <> Неопределено Тогда
		
		Для Каждого СтрокаПартииРаспределения ИЗ СтрокаДереваПартий.Строки Цикл
			
			Если КоличествоОсталосьПогасить <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			СтрокаПартии = ПолучитьСтрокуОстатковПартий(СтрокаПартииРаспределения, СтруктураИзмерений, ТаблицаОстатковПартий);
			
			// Количество по строке больше 0
			Если НЕ СтрокаПартии.Количество > 0 Тогда
				Продолжить;
			КонецЕсли; 
			
			Если СтрокаПартии.Количество >= КоличествоОсталосьПогасить Тогда
				КоэффСписания = КоличествоОсталосьПогасить/СтрокаПартии.Количество;
			Иначе
				КоэффСписания = 1;
			КонецЕсли;
			
			// Добавим новую строку
			Движение = ДобавитьДвижениеВСтруктуруПараметров(ИмяРегистра, СтруктураПараметров);
			
			// Свойства
			Движение.Период 	 = СтрокаДокумента.Период;
			Движение.Регистратор = СтрокаДокумента.Регистратор;
			Движение.Активность  = Истина;
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			
			// Измерения
			Движение.Номенклатура = СтрокаПартии.Номенклатура;
			
			// Ресурсы 
			Движение.Количество	= Окр(СтрокаПартии.Количество * КоэффСписания,3,1);
			Движение.Стоимость	= Окр(СтрокаПартии.Стоимость  * КоэффСписания,2,1);
			Движение.НДС		= Окр(СтрокаПартии.НДС  	  * КоэффСписания,2,1);
			
			Если Движение.Количество < КоличествоОсталосьПогасить Тогда
				КоэффПоступления = Движение.Количество / КоличествоОсталосьПогасить;
			Иначе
				КоэффПоступления = 1;
			КонецЕсли;
			
			// Вспомогательное поле, не являющееся ресурсом, но используемое
			// для формирования записей по переоценке принятого на комиссию.
			Движение.СтоимостьПоступление = СтоимостьПоступлениеОсталосьПогасить * КоэффПоступления;
			СтоимостьПоступлениеОсталосьПогасить = СтоимостьПоступлениеОсталосьПогасить - Движение.СтоимостьПоступление;
			
			// Реквизиты
			Движение.КодОперации	= СтрокаДокумента.КодОперацииПартииТоваров;
			
			КоличествоОсталосьПогасить = КоличествоОсталосьПогасить - Движение.Количество;
			
			СтрокаПартии.Количество = СтрокаПартии.Количество - Движение.Количество;
			СтрокаПартии.Стоимость  = СтрокаПартии.Стоимость  - Движение.Стоимость;
			СтрокаПартии.НДС  		= СтрокаПартии.НДС		  - Движение.НДС;
			
			//СтоимостьСписано = СтоимостьСписано + Движение.Стоимость;			
			СтоимостьСписано = СтоимостьСписано + Движение.Стоимость + Движение.НДС;			
			
			// Заполнение полей, специфических для учета
			ЗаполнитьПоляЗаписиСписания(Движение, СтрокаПартии, СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоэффСписания, КоэффПоступления);
			
		КонецЦикла;
	КонецЕсли;
		
	// Обработка поступления в СНТТ   
	ДвижениеПоступление = ДобавитьДвижениеВСтруктуруПараметров("ТоварыВНТТВПродажныхЦенах"+Учет, СтруктураПараметров);
	    						
	ДвижениеПоступление.Активность    = Истина;
	ДвижениеПоступление.ВидДвижения   = ВидДвиженияНакопления.Приход;
	ДвижениеПоступление.Период 	      = СтруктураПараметров.Период;
	ДвижениеПоступление.Регистратор   = СтруктураПараметров.Регистратор;
	   						
	ДвижениеПоступление.Склад 	      = ?(НЕ ЗначениеЗаполнено(СтрокаДокумента.СкладПолучатель), СтрокаДокумента.Склад, СтрокаДокумента.СкладПолучатель);
	   						
	Если РазделятьПоСтавкамНДС Тогда
	   	ДвижениеПоступление.СтавкаНДС = СтрокаДокумента.СтавкаНДСВРознице;
	КонецЕсли; 
		
	СуммаВРознице = СтрокаДокумента.СуммаВРознице;
	СуммаПокупная = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтоимостьСписано, 
												   ВалютаУправленческогоУчета, ВалютаРегламентированногоУчета,
												   Данные.Курс, 1, 
												   Данные.Кратность, 1);
		
	ДвижениеПоступление.Сумма 		  = СуммаВРознице;
	ДвижениеПоступление.СуммаНаценки  = ?(НЕ ЗначениеЗаполнено(СтрокаДокумента.СуммаВРознице), 0, СуммаВРознице - СуммаПокупная);
	    			
	ДвижениеПоступление.Номенклатура  = СтрокаДокумента.Номенклатура;
	ДвижениеПоступление.ХарактеристикаНоменклатуры = СтрокаДокумента.ХарактеристикаНоменклатуры;
	    			
	ДвижениеПоступление.КодОперации   = Перечисления.КодыОперацийТоварыВНТТВПродажныхЦенах.Поступление;
	
	// Товара не хватило
	Если (КоличествоОсталосьПогасить > 0) Тогда
		СообщитьОНехваткеПартии(СтрокаДокумента, СтруктураПараметров, РегистрУчета, КоличествоОсталосьПогасить);
	КонецЕсли;
		
КонецПроцедуры // ПеремещениеВСНТТ()

////////////////////////////////////////////////////////////////////////////////
// ЗАПРОСЫ, ВОЗВРАЩАЮЩИЕ ТАБЛИЦЫ ОСТАТКОВ

// Общие

////////////////////////////////////////////////////////////////////////////////
// РАСПРЕДЕЛЕНИЕ ДОП.РАСХОДОВ

// Распределение за период доп расходов на поступление товаров
//
// Параметры:
//	Нет.
//
Процедура ВыполнитьРаспределениеДопРасходов(ДатаНачалаВосстановления, ДатаКонцаОбработки, СтОтборОстатков = Неопределено, Документ=Неопределено, ДатаДокумента=Неопределено) Экспорт
	
	// Остатки нераспределенных доп.расходов
	Если ТипЗнч(СтОтборОстатков) = Тип("Структура") Тогда
		ТабОстатков = РегистрыНакопления.ДопРасходыНаПриобретениеТоваров.Остатки(ДатаКонцаОбработки, СтОтборОстатков);
	Иначе
		ТабОстатков = РегистрыНакопления.ДопРасходыНаПриобретениеТоваров.Остатки(ДатаКонцаОбработки);
	КонецЕсли;
	
	// Поступление партий
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Парт.Номенклатура,
	|	Парт.Склад,
	|	Парт.ХарактеристикаНоменклатуры,
	|	Парт.СерияНоменклатуры,
	|	Парт.ДокументОприходования,
	|	Парт.СтатусПартии,
	|	Парт.Заказ,
	|	Парт.Качество,
	|	СУММА(Парт.Количество) КАК Количество,
	//|	СУММА(Парт.Стоимость) КАК Стоимость,
	|	Парт.Регистратор
	|ИЗ
	|	РегистрНакопления.ПартииТоваровНаСкладах КАК Парт
	|
	|ГДЕ
	|	Парт.КодОперации В(&СписокКодыОперации) И
	|	Парт.ВидДвижения = &ВидДвижения И
	|	Парт.Период МЕЖДУ &ДатаНач И &ДатаКон И
	|	Парт.Номенклатура В(&Номенклатура)
	|
	|СГРУППИРОВАТЬ ПО
	|	Парт.Номенклатура,
	|	Парт.Склад,
	|	Парт.ХарактеристикаНоменклатуры,
	|	Парт.СерияНоменклатуры,
	|	Парт.ДокументОприходования,
	|	Парт.СтатусПартии,
	|	Парт.Заказ,
	|	Парт.Качество,
	|	Парт.Регистратор");
	
	
	СписокКодыОперации = Новый Массив;
	СписокКодыОперации.Добавить(Перечисления.КодыОперацийПартииТоваров.Поступление);
	
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ДатаНач", ДатаНачалаВосстановления);
	Запрос.УстановитьПараметр("ДатаКон", ДатаКонцаОбработки);
	Запрос.УстановитьПараметр("СписокКодыОперации", СписокКодыОперации);
	Запрос.УстановитьПараметр("Номенклатура", ТабОстатков.ВыгрузитьКолонку("Номенклатура"));
	
	ТабПоступлений = Запрос.Выполнить().Выгрузить();
	ТабПоступлений.Колонки.Добавить("Стоимость", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	ТабПоступлений.Колонки.Добавить("НДС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	
	СоотвДокументов = Новый Соответствие;
	
	Для Каждого СтрокаОстатка Из ТабОстатков Цикл
		
		НайдСтроки = ТабПоступлений.НайтиСтроки(Новый Структура(
		"Номенклатура, ХарактеристикаНоменклатуры, СерияНоменклатуры", 
		СтрокаОстатка.Номенклатура, 
		СтрокаОстатка.ХарактеристикаНоменклатуры,
		СтрокаОстатка.СерияНоменклатуры));
		
		// База распределения - количество
		ВсегоКоличество = 0;
		Для Каждого Строка Из НайдСтроки Цикл
			ВсегоКоличество = ВсегоКоличество + Строка.Количество;
		КонецЦикла;
		
		СуммаРаспределить  = СтрокаОстатка.Сумма;
		НДСРаспределить    = СтрокаОстатка.НДС;
		КоличествоосталосьПогасить = ВсегоКоличество;
		
		Для Каждого Строка Из НайдСтроки Цикл
			
			Если НЕ (КоличествоосталосьПогасить > 0) Тогда
				Прервать;
			КонецЕсли;
			
			Если Строка.Количество >= КоличествоОсталосьПогасить Тогда
				КоэффСписания = 1;
			Иначе
				КоэффСписания = Строка.Количество/КоличествоосталосьПогасить;
			КонецЕсли;
			
			Строка.Стоимость = СуммаРаспределить * КоэффСписания;
			Строка.НДС 		 = НДСРаспределить * КоэффСписания;
			
			КоличествоосталосьПогасить = КоличествоосталосьПогасить - Строка.Количество;
			
			СуммаРаспределить = СуммаРаспределить - Строка.Стоимость;
			НДСРаспределить   = НДСРаспределить - Строка.НДС;
			
			СоотвДокументов.Вставить(Строка.Регистратор);
		КонецЦикла;
		
	КонецЦикла;
	
	// Запись в регистр
	Если Документ<>Неопределено Тогда
		
		// Партии
		НаборЗаписей = РегистрыНакопления.ПартииТоваровНаСкладах.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Регистратор.Установить(Документ);
		
		ТаблицаДвижений = НаборЗаписей.Выгрузить();
		
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТабПоступлений, ТаблицаДвижений);
		
		ТаблицаДвижений.ЗаполнитьЗначения(0, "Количество");
		ТаблицаДвижений.ЗаполнитьЗначения(Перечисления.КодыОперацийПартииТоваров.ПоступлениеДопРасходов, "КодОперации");
		
		НаборЗаписей.мПериод = ДатаДокумента;
		
		НаборЗаписей.мТаблицаДвижений = ТаблицаДвижений;
		
		НаборЗаписей.ВыполнитьПриход();
		
		НаборЗаписей.Записать();
		
		// Доп расходы
		НаборЗаписей = РегистрыНакопления.ДопРасходыНаПриобретениеТоваров.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Регистратор.Установить(Документ);
		
		ТаблицаДвижений = НаборЗаписей.Выгрузить();
		
		// Стоимостью является сумма
		ТабПоступлений.Колонки.Стоимость.Имя = "Сумма";
		
		ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТабПоступлений, ТаблицаДвижений);
		
		НаборЗаписей.мПериод = ДатаДокумента;
		
		НаборЗаписей.мТаблицаДвижений = ТаблицаДвижений;
		
		НаборЗаписей.ВыполнитьРасход();
		
		НаборЗаписей.Записать();
	КонецЕсли;

	
КонецПроцедуры // ВыполнитьРаспределениеДопРасходов(ДатаНачалаВосстановления, ДатаКонцаОбработки)

